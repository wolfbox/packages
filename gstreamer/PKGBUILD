pkgname=gstreamer
pkgver=1.4.5
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL2')
url="http://gstreamer.freedesktop.org/"

makedepends=('intltool' 'pkg-config' 'gtk-doc' 'gobject-introspection' 'python2')
depends=('libxml2' 'glib2')

source=(http://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz)
sha256sums=('40801aa7f979024526258a0e94707ba42b8ab6f7d2206e56adbc4433155cb0ae')

build() {
    cd gstreamer-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libdir=/usr/lib/${MULTIARCH} \
                --libexecdir=/usr/lib/${MULTIARCH} \
                --with-package-name="GStreamer (Wolfbox Linux)" \
                --with-package-origin="http://wolfbox.org/" \
                --disable-gtk-doc --disable-static

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

check() {
    cd gstreamer-${pkgver}
    make check
}

package() {
    cd gstreamer-${pkgver}
    make DESTDIR="${pkgdir}" install
}

