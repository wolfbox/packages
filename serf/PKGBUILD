pkgname=serf
pkgver=1.3.8
pkgrel=1
pkgdesc="High-performance asynchronous HTTP client library"
arch=('x86_64')
url="http://code.google.com/p/serf/"
license=('Apache')

depends=('glibc' 'zlib' 'openssl' 'expat' 'apr-util' 'util-linux' 'openldap')
makedepends=('scons')

source=(http://serf.googlecode.com/svn/src_releases/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e0500be065dbbce490449837bb2ab624e46d64fc0b090474d9acaa87c82b2590')

prepare() {
    cd ${pkgname}-${pkgver}

    # The tests are not actually C89, and so fail
    sed -i 's:-std=c89:-std=gnu89:' SConstruct
}

build() {
    cd ${pkgname}-${pkgver}
    scons PREFIX=/usr LIBDIR=/usr/lib/${MULTIARCH}
}

check() {
    cd ${pkgname}-${pkgver}
    scons check
}

package() {
    cd ${pkgname}-${pkgver}
    install -d "${pkgdir}/usr"
    scons PREFIX="${pkgdir}/usr" LIBDIR="${pkgdir}/usr/lib/${MULTIARCH}" install
}
