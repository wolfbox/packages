pkgname=libpng
pkgver=1.6.18
pkgrel=1
pkgdesc="A library for working with the PNG image format"
arch=("x86_64")
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')

depends=('glibc' 'zlib')

source=(http://downloads.sourceforge.net/project/libpng/libpng16/${pkgver}/libpng-${pkgver}.tar.xz{,.asc})
sha256sums=('2e10c13b7949883ac961db6177c516d778184432d440317e9f0391305c360963'
            'SKIP')
validpgpkeys=(8048643BA2C840F4F92A195FF54984BFA16C640F) # Glenn Randers-Pehrson (libpng) <glennrp@users.sourceforge.net>

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --mandir=/usr/share/man
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
	install -m644 LICENSE "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
}
