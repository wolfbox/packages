pkgname=kate
pkgver=14.12.2
pkgrel=1
pkgdesc='The KDE text editor'
arch=('x86_64')
url='http://kate-editor.org/'
license=('GPL2')

# Someday we'll enable libgit support, but not now; stability issues TBD

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5'
         'kconfig' 'kdoctools' 'kguiaddons' 'ki18n' 'kinit' 'kjobwidgets' 'kio'
         'kparts' 'ktexteditor' 'kwindowsystem' 'kxmlgui' 'kdbusaddons' 'knewstuff')

source=(http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz)
sha256sums=('ceb95148543b89b799a1dff9ee45b0a14c2883965853d7537f4890e5992ccb58')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=on \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
