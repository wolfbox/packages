# Derived from Arch Linux

pkgname=polkit-qt5
pkgver=0.112.0
pkgrel=1
pkgdesc='A Qt library for accessing PolicyKit'
arch=('x86_64')
url='https://projects.kde.org/projects/kdesupport/polkit-qt-1'
license=('LGPL2.1')

makedepends=('cmake')
depends=('gcc-libs' 'glibc' 'polkit' 'qt5' 'glib2')

source=(http://download.kde.org/stable/apps/KDE4.x/admin/polkit-qt-1-${pkgver}.tar.bz2
        systembus-usage.patch)
sha256sums=('67fb03bf6ca3e0bdbd98d374dfb5b1651a07d17ae6c23e11a81b4b084447e7c6'
            '5687f3a1a587b7f80a369f314326120de1a17d8a37328299c74fdb6e01aec9ca')

prepare() {
  cd polkit-qt-1-${pkgver}
  patch -p1 -i "${srcdir}"/systembus-usage.patch
}

build() {
  mkdir -p build && cd build
  cmake ../polkit-qt-1-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib/${MULTIARCH}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
