pkgname=kactivities
pkgver=5.7.0
pkgrel=1
pkgdesc='KDE infrastructure for managing user "activities"'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kactivities'
license=('LGPLv2.1')

makedepends=('cmake' 'extra-cmake-modules' "kcmutils>=${pkgver}" "kdeclarative>=${pkgver}" 'boost')
depends=('glibc' 'gcc-libs' 'qt5' "kdbusaddons>=${pkgver}" 'kconfig' "ki18n>=${pkgver}"
         'kservice' 'kwindowsystem' "kcoreaddons>=${pkgver}" "kio>=${pkgver}")

source=(http://download.kde.org/stable/frameworks/5.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('2978ab55a9ce484db5790a13fa1ae081bb1e3b3925c34c1ac91a9700148882b4')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
