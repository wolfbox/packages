pkgname=kemoticons
pkgver=5.16.0
pkgrel=1
pkgdesc='KDE library for converting text emoticons to graphical emoticons'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kemoticons'
license=('LGPL2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5' "karchive>=${pkgver}" "kconfig>=${pkgver}"
         "kservice>=${pkgver}")

source=(http://download.kde.org/stable/frameworks/5.16/${pkgname}-${pkgver}.tar.xz)
sha256sums=('ba4ec30be11629fc046f1727eaa2c6ee80c26c23d953cf08214e6e600251d498')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
