pkgname=networkmanager-qt
pkgver=5.7.0
pkgrel=1
pkgdesc='A Qt binding for NetworkManager'
arch=('x86_64')
url='https://kde.org'
license=('LGPLv2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt>=5.0' 'networkmanager')

source=(http://download.kde.org/stable/frameworks/5.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('535e6b04fa041c38bb9af1bf4aea293ee5ae4f9883df7dd1cb0e1671b5dca1d3')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
