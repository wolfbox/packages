pkgname=kdesu
pkgver=5.7.0
pkgrel=1
pkgdesc='KDE UI for running commands as root'
arch=('x86_64')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPL2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5' 'kpty' 'kcoreaddons' 'kservice' 'kconfig')

source=(http://download.kde.org/stable/frameworks/5.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('adab63b45de0b5b614a4b5185658782d5ae1a6ef0c96792927b3ff3adf9f5b2e')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
