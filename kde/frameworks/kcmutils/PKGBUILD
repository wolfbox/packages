pkgname=kcmutils
pkgver=5.16.0
pkgrel=1
pkgdesc='KDE framework for working with control modules'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kcmutils'
license=('LGPL2.1')

makedepends=('cmake' "extra-cmake-modules>=${pkgver}")
depends=('glibc' 'gcc-libs' 'qt5' "ki18n>=${pkgver}" "kconfig>=${pkgver}"
         "kservice>=${pkgver}" "kcoreaddons>=${pkgver}" "kiconthemes>=${pkgver}"
         "kxmlgui>=${pkgver}" "kauth>=${pkgver}" "kwidgetsaddons>=${pkgver}"
         "kconfigwidgets>=${pkgver}" "kdeclarative>=${pkgver}")

source=(http://download.kde.org/stable/frameworks/5.16/${pkgname}-${pkgver}.tar.xz)
sha256sums=('341330dba9cc50cfa035460c94f79892f5908e116f7a0f084be36e53f9a3e3b3')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
