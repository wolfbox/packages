pkgname=ktexteditor
pkgver=5.16.0
pkgrel=1
pkgdesc='The KDE text editor component'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/ktexteditor'
license=('LGPL2.1')

makedepends=('cmake' "extra-cmake-modules>=${pkgver}")
depends=('glibc' 'gcc-libs' 'qt5' "karchive>=${pkgver}" "kconfig>=${pkgver}"
         "kguiaddons>=${pkgver}" "ki18n>=${pkgver}" "kio>=${pkgver}"
         "kparts>=${pkgver}" "sonnet>=${pkgver}" "kiconthemes>=${pkgver}")


source=(http://download.kde.org/stable/frameworks/5.16/${pkgname}-${pkgver}.tar.xz)
sha256sums=('1cbb7e838a4aebc08e53add09988ed58475e2f512d1689081b084b6290ae3e3c')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
