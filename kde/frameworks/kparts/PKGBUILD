pkgname=kparts
pkgver=5.7.0
pkgrel=1
pkgdesc='KDE framework for embedding user interfaces'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kparts'
license=('LGPL2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5' 'kio' 'kxmlgui' 'knotifications' 'ki18n'
         'kiconthemes' 'kjobwidgets' 'kwidgetsaddons' 'kservice' 'kconfig'
         'kcoreaddons')

source=(http://download.kde.org/stable/frameworks/5.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('4f27f0d239607a74fd4c7c42a04f427051f87d273a0da6c632a36980c3ac4873')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
