pkgname=knewstuff
pkgver=5.16.0
pkgrel=1
pkgdesc='KDE support library for downloading application assets'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/knewstuff'
license=('LGPL2.1')

makedepends=('cmake' "extra-cmake-modules>=${pkgver}")
depends=('glibc' 'gcc-libs' 'qt5' "attica>=${pkgver}" "kxmlgui>=${pkgver}"
         "kio>=${pkgver}" "karchive>=${pkgver}" "kcompletion>=${pkgver}"
         "ki18n>=${pkgver}" "kitemviews>=${pkgver}" "kiconthemes>=${pkgver}"
         "ktextwidgets>=${pkgver}" "kconfig>=${pkgver}" "kcoreaddons>=${pkgver}"
         "kwidgetsaddons>=${pkgver}")

source=(http://download.kde.org/stable/frameworks/5.16/${pkgname}-${pkgver}.tar.xz)
sha256sums=('191a1d963f990fbcd75cb247abeb63806140bbfc7cad5c14c2524045ada52308')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
