pkgname=kxmlgui
pkgver=5.16.0
pkgrel=1
pkgdesc='KDE framework for managing menu and toolbar items'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kxmlgui'
license=('GPLv2' 'LGPLv2.1')

makedepends=('cmake' "extra-cmake-modules>=${pkgver}")
depends=('glibc' 'gcc-libs' 'qt5' "kconfig>=${pkgver}" "ktextwidgets>=${pkgver}"
         "kitemviews>=${pkgver}" "ki18n>=${pkgver}" "attica>=${pkgver}"
         "kiconthemes>=${pkgver}" "kwidgetsaddons>=${pkgver}"
         "kglobalaccel>=${pkgver}" "kwindowsystem>=${pkgver}"
         "kcoreaddons>=${pkgver}")

source=(http://download.kde.org/stable/frameworks/5.16/${pkgname}-${pkgver}.tar.xz)
sha256sums=('5ac0d43884b48da7af3b90df2d8a1a26945d165db679163a497696f69270f0ef')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
