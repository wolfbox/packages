pkgname=kconfigwidgets
pkgver=5.7.0
pkgrel=1
pkgdesc='KDE widgets for configuration dialogs'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kconfigwidgets'
license=('GPLv2' 'LGPLv2.1')

makedepends=('cmake' 'extra-cmake-modules' "kdoctools>=${pkgver}")
depends=('glibc' 'gcc-libs' 'qt5' 'kconfig' "kauth>=${pkgver}" "kcodecs>=${pkgver}"
         "kguiaddons>=${pkgver}" "kwidgetsaddons>=${pkgver}")

source=(http://download.kde.org/stable/frameworks/5.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('fb3d04efebb0ac570defd92eff778cc78052ca8a33c2e5b88b415c02e2d9791c')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
