pkgname=solid
pkgver=5.7.0
pkgrel=1
pkgdesc='KDE hardware abstraction layer'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPLv2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('gcc-libs' 'glibc' 'qt5' 'udev' 'upower' 'udisks2')

source=(http://download.kde.org/stable/frameworks/5.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('24dc18630476db845af6a14553659c91a40b36d6c4ebbca470e061cbd9c2bf29')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_HTML_DOCS=OFF \
    -DWITH_DOC=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
