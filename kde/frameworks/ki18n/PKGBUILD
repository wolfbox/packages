pkgname=ki18n
pkgver=5.16.0
pkgrel=1
pkgdesc='KDE component for gettext-based internationalization'
arch=('x86_64')
url='https://projects.kde.org/projects/kdesupport/extra-cmake-modules'
license=('LGPLv2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('gcc-libs' 'glibc' 'qt5' 'gettext')

source=(http://download.kde.org/stable/frameworks/5.16/${pkgname}-${pkgver}.tar.xz)
sha256sums=('fc0adac69568672e26fd4daa201a33a2e9c4e854460dd1f3bd64a10eba8d1d22')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
