pkgname=kwin
pkgver=5.2.0.1
pkgrel=1
pkgdesc='The KDE window manager'
arch=('x86_64')
url='https://kde.org'
license=('LGPLv2.1' 'GPLv2')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'libepoxy' 'mesa' 'qt5' 'kinit' 'kcrash'
         'knotifications' 'plasma-framework' 'kglobalaccel' 'kconfigwidgets'
         'kcodecs' 'kauth' 'kwidgetsaddons' 'kconfig' 'kactivities' 'libx11'
         'libxext' 'libxcursor' 'libice' 'libsm' 'libxxf86vm' 'libxcb')

source=(http://download.kde.org/stable/plasma/5.2.0/${pkgname}-${pkgver}.tar.xz
        libinput-version-check.patch)
sha256sums=('aed82bd23376b44534a01b277cab3aed012c778540320814469b446719ccdf49'
            'eb27f84daf0404ad1248977d0313810365afbabd1daaba8764d693711c7b99a4')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../libinput-version-check.patch
}

build() {
  mkdir -p build && cd build
  
  # --as-needed breaks this build
  export LDFLAGS="-Wl,-O1,--no-as-needed,--sort-common,-z,relro -ldl"

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=on \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
