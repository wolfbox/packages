pkgname=kwin
pkgver=5.1.95
pkgrel=1
pkgdesc='The KDE window manager'
arch=('x86_64')
url='https://kde.org'
license=('LGPLv2.1' 'GPLv2')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'libepoxy' 'mesa' 'qt5' 'kinit' 'kcrash'
         'knotifications' 'plasma-framework' 'kglobalaccel' 'kconfigwidgets'
         'kcodecs' 'kauth' 'kwidgetsaddons' 'kconfig' 'kactivities' 'libx11'
         'libxext' 'libxcursor' 'libice' 'libsm' 'libxxf86vm' 'libxcb')

source=(http://download.kde.org/unstable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz
        libinput-version-check.patch)
sha256sums=('ca1f61613448c2ac6d675061ddd53df5ca3b3277ee1079d4f2327eb8bb14d7a1'
            'eb27f84daf0404ad1248977d0313810365afbabd1daaba8764d693711c7b99a4')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../libinput-version-check.patch
}

build() {
  mkdir -p build && cd build
  
  # --as-needed breaks this build
  export LDFLAGS="-Wl,-O1,--no-as-needed,--sort-common,-z,relro -ldl"

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=on \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
