pkgname=systemsettings
pkgver=5.2.1
pkgrel=1
pkgdesc='The KDE system settings manager'
arch=('x86_64')
url='https://kde.org'
license=('GPL2')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5'
         'kcmutils' 'ki18n' 'kio' 'kservice' 'kiconthemes' 'kwindowsystem'
         'kxmlgui' 'kdbusaddons' 'khtml' 'kdoctools' 'kitemviews')

source=(http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('c492b03c19b86d0ca5422ac9913d19af73c152e952b3a39e1e4daef672246845')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=on \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
