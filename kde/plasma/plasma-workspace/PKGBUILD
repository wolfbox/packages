pkgname=plasma-workspace
pkgver=5.2.1
pkgrel=1
pkgdesc='The KDE Plasma workspace environment'
arch=('x86_64')
url='https://kde.org'
license=('LGPL2.1' 'GPL2')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5' 'kdewebkit' 'kidletime' 'baloo' 'kjsembed'
         'libksysguard' 'libkscreen' 'ktexteditor' 'xorg-xmessage' 'xorg-xsetroot'
         'breeze' 'libdbusmenu-qt5' 'kwayland' 'ttf-oxygen')

source=(http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('c3dca77ea4deb872cf5225f5982055cf71fda3d63ff6cd08ab11215770d0b451')

build() {
  mkdir -p build && cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=on \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # For some reason, kcheckpass isn't being installed setuid, which
  # breaks the unlock pane
  # IF we switch to OpenWall's TCB shadow, this will no longer be necessary
  chmod u+s "${pkgdir}"/usr/lib/${MULTIARCH}/kcheckpass
}
