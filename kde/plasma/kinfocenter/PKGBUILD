pkgname=kinfocenter
pkgver=5.2.0
pkgrel=1
pkgdesc='KDE system information center'
arch=('x86_64')
url='https://kde.org'
license=('GPL2')

makedepends=('cmake' 'extra-cmake-modules' 'mesa')
depends=('glibc' 'gcc-libs' 'qt5' 'libgl' 'wayland' 'pciutils' 'libraw1394')

source=(http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz
        fix-libpci-segfault.patch)
sha256sums=('89a976d2eb6aef02db02db82d645eacbf710f6e0c3ad9fdd932455c309a62e94'
            'e6666a521db60502ae630220c6a72e8fe1edbd585a6ed90df4902bae6265d21f')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../fix-libpci-segfault.patch
}

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=on \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
