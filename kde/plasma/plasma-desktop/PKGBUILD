pkgname=plasma-desktop
pkgver=5.2.1
pkgrel=1
pkgdesc='The KDE Plasma desktop environment'
arch=('x86_64')
url='https://kde.org'
license=('GPL2')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5' 'knotifyconfig' 'knewstuff' 'krunner'
         'kdelibs4support' 'kemoticons' 'kitemmodels' 'kwin' 'plasma-workspace'
         'systemsettings' 'fontconfig' 'pulseaudio' 'libcanberra' 'libusb-compat' 'kmenuedit')

source=(http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz
        panel-layout.js
        kicker-main.xml)
sha256sums=('ada8f06f922418be2bdca5e98b1ec03fdfc7a461688ac3ea054d7e8ad6f6f222'
            '22052a45d6996f2a42324f8c8c6de7652fb955b369bc13cd5010f9a93cbe8a9d'
            '07ceb5c0e5a03c23f5d21cbb5cf0d2c72e1a94f99f22510160721c29683dbb28')

prepare() {
  install -m644 panel-layout.js ${pkgname}-${pkgver}/layout-templates/org.kde.plasma.desktop.defaultPanel/contents/layout.js
  install -m644 kicker-main.xml ${pkgname}-${pkgver}/applets/kicker/package/contents/config/main.xml
}

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=on \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
