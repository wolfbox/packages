pkgname=ttf-oxygen
pkgver=5.2.1
pkgrel=1
pkgdesc='The Oxygen font set'
arch=('any')
url='https://kde.org'
license=('LGPL2.1' 'GPL2')
install=ttf-oxygen.install

makedepends=('cmake' 'extra-cmake-modules' 'fontforge')
depends=('fontconfig')
provides=('ttf-font')

source=(http://download.kde.org/stable/plasma/${pkgver}/oxygen-fonts-${pkgver}.tar.xz)
sha256sums=('950237428816bb22759699cc1aec7abd39f6f98a75b41a01d915465f4680d076')

build() {
  mkdir -p build && cd build
  cmake ../oxygen-fonts-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
