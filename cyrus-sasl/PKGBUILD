pkgname=cyrus-sasl
pkgver=2.1.23
pkgrel=1
pkgdesc="Simple Authentication and Security Layer."
arch=("x86_64")
url="http://cyrusimap.org"
license=(custom)

depends=('glibc')

source=(ftp://ftp.cyrusimap.org/cyrus-sasl/cyrus-sasl-${pkgver}.tar.gz
		cyrus-sasl-2.1.23-glibc217-crypt.diff.gz
		cyrus-sasl.bad_elif.diff.gz)
sha256sums=(20efcc4040cbab6e89a636a07dcf5720ee07b5c62359a4e7bf2500ef6020b136
			6b5015c1ca8853085adeb6c7918bacecdf6d13ef6028a206bef45ade9c708660
			5ce74eaafc0f811957e43a9404bb070c4eca9eb6fc9461ec21bfa60ceb34b37f)

prepare() {
	cd "${srcdir}"/cyrus-sasl-${pkgver}

	# Apply Slackware patches
	# Fix compile
	zcat ../cyrus-sasl.bad_elif.diff.gz | patch -p1
	
	# Fix for glibc-2.17 crypt() NULL return
	zcat ../cyrus-sasl-2.1.23-glibc217-crypt.diff.gz | patch -p1
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr --sbindir=/usr/bin --libdir=/usr/lib/${MULTIARCH} \
	            --with-plugindir=/usr/lib/${MULTIARCH}/sasl2 --with-configdir=/etc/sasl2 \
	            --sysconfdir=/etc --localstatedir=/var --mandir=/usr/man \
	            --disable-static --enable-login --disable-anon --without-ldap \
	            --with-saslauthd --with-gdbm --with-dblib=gdbm
	make sasldir=/usr/lib/${MULTIARCH}/sasl2
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make sasldir=/usr/lib/${MULTIARCH}/sasl2 install DESTDIR="${pkgdir}"

	# Fix sloppy man page installation
	rm -f "${pkgdir}"/usr/man/cat8
	cat saslauthd/saslauthd.mdoc > "${pkgdir}"/usr/man/man8/saslauthd.8
	
	# Install license info
	mkdir -p "${pkgdir}"/usr/doc/cyrus-sasl-${pkgver}
	cp -a AUTHORS COPYING* README* "${pkgdir}"/usr/doc/cyrus-sasl-${pkgver}
}
