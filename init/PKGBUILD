pkgname=init
pkgver=1.0
pkgrel=1
pkgdesc="A lightweight init system"
arch=("x86_64")
url="https://github.com/archsys/init"
license=('MIT')
options=('emptydirs')

conflicts=('upstart')
depends=('glibc' 'sh' 'util-linux' 'libcgroup' 'inotify-tools')

source=(https://github.com/arachsys/init/archive/init-${pkgver}.tar.gz
        rc.startup
        rc.shutdown
        rc.functions
        rc.pidfile
        initemit
        initstatus
        launch-cgroup
        agent.sh)
sha256sums=('87b80e77935ace9e18e8e70255f046281550686526e5607912221ab6a1344e10'
            '9ae20df9bc2c137b1b3467fe952f71961b8d30663444d5276ef76d115c94ac57'
            '246c24765a0fedecfe7f284a11ab63be391fcb8aa8fb7917f7d6164aebd620e9'
            'ecdedd713ec8fabc19074fb193356a7b7ddce77386233f6220dc68fe73814633'
            'ca8de593974728bcd33ae289cdb952f17dc9a2481ba0262fe5190144c36a65e9'
            '4f6c6a12e4999c9db1a5f273c81eb5dd714706c8bedb61c57130e7de9c95cfe1'
            '2e08357efb4aaeae511f8359c76c9b818e7ade12cf5450b7c737ca649e6b69f9'
            '2febe0c82492786ec1b3af576e8b7ac8aab5c3cedb4fed9890a26cbd2752b381'
            '32b51cf76459b6ac773ded20893b30ab57a67ca4c9406d2c7d4995a664861ac3')

build() {
	cd "${srcdir}"/init-${pkgname}-${pkgver}
	make
}

package() {
	cd "${srcdir}"
	
	(cd init-${pkgname}-${pkgver}
	  make install DESTDIR="${pkgdir}" PREFIX=/usr )
	
	# Don't package uevent or syslog at this point
	rm "${pkgdir}"/usr/bin/syslog*
	rm "${pkgdir}"/usr/bin/uevent*
	
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m744 rc.startup rc.shutdown "${pkgdir}"/etc/
	install -m644 rc.functions "${pkgdir}"/etc/rc.d/
	install -m644 rc.pidfile "${pkgdir}"/etc/rc.d/

	install -m755 initemit "${pkgdir}"/usr/bin
	install -m755 initstatus "${pkgdir}"/usr/bin
	install -m755 launch-cgroup "${pkgdir}"/usr/bin

	mkdir -p "${pkgdir}"/usr/lib/init
	install -m755 agent.sh "${pkgdir}"/usr/lib/init/

	mkdir -p "${pkgdir}"/var/run/services
}
