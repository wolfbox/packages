# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xf86-video-cirrus
pkgver=1.5.2
pkgrel=1
pkgdesc="X.org Cirrus Logic video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=19')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<19' 'X-ABI-VIDEODRV_VERSION>=20')
groups=('xorg-drivers' 'xorg')

source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        'git-fixes.diff')
sha256sums=('3361e1a65d9b84c464752fd612bdf6087622c6dd204121715366a170e5c3ccd7'
            '918037bbfc182815e77707b79059a51acb32bee68d16d38e355433136364354f')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../git-fixes.diff
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
