# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xorg-fonts-encodings
pkgver=1.0.4
pkgrel=1
pkgdesc="X.org font encoding files"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
makedepends=('xorg-mkfontscale' 'xorg-util-macros' 'xorg-font-util')
groups=('xorg-fonts' 'xorg')
options=(!makeflags)

source=(${url}/releases/individual/font/encodings-${pkgver}.tar.bz2)
sha256sums=('ced6312988a45d23812c2ac708b4595f63fd7a49c4dcd9f66bdcd50d1057d539')

build() {
  cd "${srcdir}/encodings-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/encodings-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/COPYING"

  # regenerate encodngs file not to include non-compressed versions
  cd $pkgdir/usr/share/fonts/encodings/large
  mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings/large -e . .
  cd $pkgdir/usr/share/fonts/encodings/
  mkfontscale -b -s -l -n -r -p /usr/share/fonts/encodings -e . -e large .
}
