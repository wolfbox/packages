# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xorg-fonts-type1
pkgver=7.4
pkgrel=1
pkgdesc="X.org Type1 fonts"
arch=('any')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=(xorg-fonts-encodings xorg-fonts-alias xorg-font-util fontconfig)
install=xfonts.install

source=(${url}/releases/individual/font/font-adobe-utopia-type1-1.0.2.tar.bz2
        ${url}/releases/individual/font/font-bh-type1-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-bitstream-type1-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-ibm-type1-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-xfree86-type1-1.0.2.tar.bz2)
sha256sums=('8c5f2752e44af6219c1b4cfc178f9721f6dd573039e973f61029136c2756e233'
            'da3b5f26c6cde0baf3831f50c79a7a107806f885e0205073855bfbc5a357bb45'
            '51027cc1d49140153620c1a2b653f1773fe92cb694b221396d68265343e113ab'
            '887ea895cc0fdd5c50d4960aee7cb6ed813cf90f53b53ce0e0e068dd606cc919'
            '7a026aeaafe5a619b2c1b35cd361125b169b2212eaf0f2f5ce796038b1cf06cd')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
        --with-fontdir=/usr/share/fonts/Type1
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.${dir%-*}"
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/Type1/fonts.*
}
