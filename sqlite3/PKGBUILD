pkgname=sqlite3
pkgver=3.8.8.3
_amalver=3080803
pkgrel=1
pkgdesc="A lightweight embedded SQL database engine"
arch=("x86_64")
url="http://www.sqlite.org"
license=('custom')

depends=('glibc' 'ncurses' 'readline')
provides=("sqlite=${pkgver}")

source=(http://www.sqlite.org/2015/sqlite-autoconf-${_amalver}.tar.gz)
sha256sums=('74576964cd9db44c13bf95d6a7c36b06cfdfb2fe4e3e26c9ce1ae0b2e328b411')

build() {
	cd sqlite-autoconf-${_amalver}
	
	export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY=1 -DSQLITE_SECURE_DELETE=1"
	
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
	            --localstatedir=/var --enable-threadsafe \
	            --enable-static=no
	make
}

package() {
	cd sqlite-autoconf-${_amalver}
	make install DESTDIR="${pkgdir}"
}
