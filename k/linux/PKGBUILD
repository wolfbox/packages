pkgname=linux
pkgver=3.18.2
pkgrel=1
pkgdesc="The Linux kernel."
arch=("x86_64")
url="http://www.kernel.org"
license=('GPLv2')
options=(!strip)

install=linux.install
makedepends=('gcc' 'lz4')

_grsecver=3.0-3.18.2-201501120821
source=(https://www.kernel.org/pub/linux/kernel/v3.x/linux-${pkgver}.tar.{sign,xz}
        https://grsecurity.net/test/grsecurity-${_grsecver}.patch{,.sig}
        config
        x509.genkey)
sha256sums=('SKIP'
            '1090fa94ddddc38e514f07872315af84032ba7cdb9715c21d5a562a1072b1abf'
            '2f7245b0cda85e646298973efeda073d9caff53d6c0eb8b6b5546e2e86f1db90'
            'SKIP'
            'b14aaf6d92907e153774d56e00144d79f722bf493c4e50e8ae3c7d511496d53f'
            'ca581821060e883b03f660d69ed0e9a7cbbf284dd757995bd31ffefa551ef316')
validpgpkeys=(647F28654894E3BD457199BE38DBBDC86092693E # Greg K-H
              DE9452CE46F42094907F108B44D1C0F82525FE49) # Bradley Spengler

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	patch -p1 -i ../grsecurity-${_grsecver}.patch
	cp "${srcdir}/config" .config
	cp "${srcdir}"/x509.genkey ./
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	mkdir -p "${pkgdir}/boot"
	
	install -m 644 arch/x86/boot/bzImage "${pkgdir}"/boot/vmlinuz-${pkgver}
	make modules_install INSTALL_MOD_PATH="${pkgdir}"
	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"
	
	depmod -b "${pkgdir}" ${pkgver}-grsec
}
