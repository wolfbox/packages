pkgname=linux
pkgver=3.17.6
pkgrel=1
pkgdesc="The Linux kernel."
arch=("x86_64")
url="http://www.kernel.org"
license=('GPLv2')
options=(!strip)

install=linux.install
makedepends=('gcc' 'lz4')

_grsecver=3.0-${pkgver}-201412071639
source=(https://www.kernel.org/pub/linux/kernel/v3.x/linux-${pkgver}.tar.xz
        https://grsecurity.net/test/grsecurity-${_grsecver}.patch
        config
        x509.genkey)
sha256sums=('412909c1a08f77243bf965209aaef1e80c6f056f48e93e2b69df685445549d7c'
            'fc985e8f5549b4f35b586c8c3df36ac8f4bd6b65bc257bd75ac2e40af9c957a0'
            '8fe8f92a2c3c06c479539d19f5fbf5acfec1e60a376d4c759ce8d7909bb25574'
            'ca581821060e883b03f660d69ed0e9a7cbbf284dd757995bd31ffefa551ef316')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	cat ../grsecurity-${_grsecver}.patch | patch -p1
	cp "${srcdir}/config" .config
	cp "${srcdir}"/x509.genkey ./
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	mkdir -p "${pkgdir}/boot"
	
	install -m 644 arch/x86/boot/bzImage "${pkgdir}"/boot/vmlinuz-${pkgver}
	make modules_install INSTALL_MOD_PATH="${pkgdir}"
	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"
	
	depmod -b "${pkgdir}" ${pkgver}-grsec
}
