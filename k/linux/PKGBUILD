pkgname=linux
pkgver=3.17.3
pkgrel=1
pkgdesc="The Linux kernel."
arch=("x86_64")
url="http://www.kernel.org"
license=('GPLv2')

install=linux.install
makedepends=('gcc' 'lz4' 'xz')

_grsecver=3.0-${pkgver}-201411150027
source=(https://www.kernel.org/pub/linux/kernel/v3.x/linux-${pkgver}.tar.xz
        https://grsecurity.net/test/grsecurity-${_grsecver}.patch
        config.xz)
sha256sums=('f1ff54a392db4ad6432d07671f19c7da7cb73cae2bd7ea84369492abc8c172c2'
            '23ad271cc8b563d13b1fd8509a60a65da5683b2cdd6f7288afacbfd1dfb56ea6'
            '362419ede85a9286eb760c8c60f738ba7523e5841222e2a80a27ac1018b3df98')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	cat ../grsecurity-${_grsecver}.patch | patch -p1
	xzcat "${srcdir}/config.xz" > .config
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	mkdir -p "${pkgdir}/boot"
	
	install -m 644 arch/x86/boot/bzImage "${pkgdir}"/boot/vmlinuz-${pkgver}
	make modules_install INSTALL_MOD_PATH="${pkgdir}"
	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"
	
	depmod -b "${pkgdir}" ${pkgver}-grsec
}
