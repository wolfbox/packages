pkgname=docbook2x
pkgver=0.8.8
pkgrel=1
pkgdesc="A tool for converting from DocBook to Unix man pages and GNU Texinfo."
arch=('x86_64')
url="http://docbook2x.sourceforge.net"
license=('MIT')

depends=('glibc' 'libxslt' 'perl-xml-sax')

source=(http://downloads.sourceforge.net/project/docbook2x/docbook2x/${pkgver}/docbook2X-${pkgver}.tar.gz
        datadir.diff)
sha256sums=(4077757d367a9d1b1427e8d5dfc3c49d993e90deabc6df23d05cfe9cd2fcdc45
            ffb3fe7052ee0fb0422493e00f97b640f58fcade4ca9d5ac8c0c224e71666569)

prepare() {
	cd "${srcdir}/docbook2X-${pkgver}"
	cat "${srcdir}"/datadir.diff | patch -p1
}

build() {
	cd "${srcdir}/docbook2X-${pkgver}"
	autoreconf -i -f
	./configure --prefix=/usr \
	            --mandir=/usr/man \
	            --infodir=/usr/info \
	            --with-xslt-processor=libxslt \
	            --program-transform-name 's,2x-,2,'
	make
}

package() {
	cd "${srcdir}/docbook2X-${pkgver}"
	make DESTDIR="${pkgdir}" install
	
	mkdir -p "${pkgdir}"/usr/doc/${pkgname}-${pkgver}
	install -m644 COPYING "${pkgdir}"/usr/doc/${pkgname}-${pkgver}/

	# Fix doc2man trying an failing to go out to the network for things it
	# already has installed.	
	sed -i 's|http://docbook2x.sf.net/latest|/usr/share/docbook2X|g' "${pkgdir}"/usr/bin/db2x_xsltproc
}
