pkgname=gtk3
pkgver=3.18.2
pkgrel=1
pkgdesc="The modern GTK+ widget toolkit"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2')

makedepends=('python2' 'gobject-introspection')
depends=('atk' 'at-spi2-atk' 'pango>=1.37.3' 'glib2>=2.45.8' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage'
         'shared-mime-info' 'cairo' 'cups' 'gtk-update-icon-cache' 'colord' 'wayland' 'libxkbcommon')

source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.18/gtk+-$pkgver.tar.xz)
sha256sums=('5dbec561c4a00070073bf9cf4cfdd61fab4a14c8ff5b15d700bd378f8185e152')

build() {
    cd gtk+-$pkgver

    CXX=/bin/false ./configure --prefix=/usr \
        --libdir=/usr/lib/${MULTIARCH} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --enable-x11-backend \
        --enable-broadway-backend \
        --enable-wayland-backend \
        --enable-colord

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd gtk+-$pkgver
    make DESTDIR="$pkgdir" install
}
