pkgname=man-db
pkgver=2.7.1
pkgrel=1
pkgdesc="Manual page viewer"
arch=("x86_64")
url="http://www.nongnu.org/man-db/"
license=('GPL2')

depends=('glibc' 'groff' 'gdbm>=1.9.0' 'libpipeline')
makedepends=('pkg-config')

source=(http://download.savannah.gnu.org/releases/man-db/man-db-${pkgver}.tar.xz)
sha256sums=('c4876204a3fafb4d8ccc1d172c0431ee581d95558cbd6c4e79caca7717049e0e')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
	            --libexecdir=/usr/lib/${MULTIARCH} --sbindir=/usr/bin \
	            --mandir=/usr/share/man --docdir=/usr/share/doc/${pkgname} \
	            --disable-setuid --with-db=gdbm
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	rm "${pkgdir}"/usr/share/doc/${pkgname}/*.ps

	mkdir -p "${pkgdir}"/var/cache/man
}
