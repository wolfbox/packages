pkgname=dbus
pkgver=1.6.26
pkgrel=1
pkgdesc="The D-Bus userspace message bus system."
arch=("x86_64")
url="http://www.freedesktop.org/wiki/Software/dbus/"
license=("GPLv2")

install=dbus.install
depends=('glibc' 'expat')

source=("http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz"
        dbus-1.6.x-allow_root_globally.diff.gz
        dbus.set.home.diff.gz
        dbus
        dbus.filesystem)
sha256sums=('cd274341e14ad27c16f73f4d4f8c801bdae75a4cca21765cc9c1957f48b85c8e'
            'cd35621e4cc56403e9bb6ca6b431c8ae04f8cb8c626df40953e6f0f1bb488f64'
            '30bdcc5407a0bc08d98d21b949cf5535d1e08c64df28928ec8ce71c026ed8f39'
            '5a825c71fa65b15418c1b5a9a1b5bf7ebbb731858d5022c007ad6d019b5411ae'
            '8dc424c7e30f19bb1b4e1ee569629c6f515e858e958a66080fb8fc16b6aca213')

prepare() {
	cd "${srcdir}"/dbus-${pkgver}
	
	# Slackware patches
	# Root shouldn't be banned from doing anything ever
	zcat "${srcdir}"/dbus-1.6.x-allow_root_globally.diff.gz | patch -p1
	# Hack to set user profile's $HOME. Upstream has rejected this; I'm
	# thinking we should drop it.
	zcat "${srcdir}"/dbus.set.home.diff.gz | patch -p1
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Harden
	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
	            --localstatedir=/var --mandir=/usr/man --infodir=/usr/info \
	            --libexecdir=/usr/lib/${MULTIARCH} --docdir=/usr/doc/dbus-${pkgver} \
	            --disable-doxygen-docs --enable-shared=yes --enable-static=no \
	            --enable-inotify --with-system-pid-file=/var/run/dbus/dbus.pid \
	            --with-system-socket=/var/run/dbus/system_bus_socket \
	            --disable-tests
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	rm -r "${pkgdir}"/etc/rc.d
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m755 "${srcdir}"/dbus "${pkgdir}"/etc/rc.d/
	install -m755 "${srcdir}"/dbus.filesystem "${pkgdir}"/etc/rc.d/events/

	chmod 700 "${pkgdir}"/var/run/dbus
}
