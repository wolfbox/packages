pkgname=dbus
pkgver=1.6.24
pkgrel=1
pkgdesc="The D-Bus userspace message bus system."
arch=("x86_64")
url="http://www.freedesktop.org/wiki/Software/dbus/"
license=("GPLv2")

install=dbus.install
depends=('glibc' 'expat')

source=("http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz"
        dbus-1.6.x-allow_root_globally.diff.gz
        dbus.set.home.diff.gz
        dbus.upstart)
sha256sums=(c1a06bdac3e48c046bd54cf4acdd996fc7496cab803753d0de0982ccb2615749
            cd35621e4cc56403e9bb6ca6b431c8ae04f8cb8c626df40953e6f0f1bb488f64
            30bdcc5407a0bc08d98d21b949cf5535d1e08c64df28928ec8ce71c026ed8f39
            48863bb4ab2a2dea9616615b36747c360bf0b993fe8813e878f607cade2fad02)

prepare() {
	cd "${srcdir}"/dbus-${pkgver}
	
	# Slackware patches
	# Root shouldn't be banned from doing anything ever
	zcat "${srcdir}"/dbus-1.6.x-allow_root_globally.diff.gz | patch -p1
	# Hack to set user profile's $HOME. Upstream has rejected this; I'm
	# thinking we should drop it.
	zcat "${srcdir}"/dbus.set.home.diff.gz | patch -p1
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Harden
	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
	            --localstatedir=/var --mandir=/usr/man --infodir=/usr/info \
	            --docdir=/usr/doc/dbus-${pkgver} --disable-doxygen-docs \
	            --enable-shared=yes --enable-static=no --enable-inotify \
	            --with-system-pid-file=/var/run/dbus/dbus.pid \
	            --with-system-socket=/var/run/dbus/system_bus_socket \
	            --disable-tests
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}"/etc/init
	install -m 644 "${srcdir}"/dbus.upstart "${pkgdir}"/etc/init/dbus.conf
	rm -r "${pkgdir}"/etc/rc.d

	chmod 700 "${pkgdir}"/var/run/dbus
}
