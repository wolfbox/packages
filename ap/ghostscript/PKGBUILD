pkgname=ghostscript
pkgver=9.15
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
arch=('x86_64')
license=('AGPL' 'custom')
url="http://www.ghostscript.com/"

depends=('libxt' 'fontconfig' 'jasper' 'zlib' 'libpng' 'libjpeg'
         'libtiff' 'lcms2' 'dbus' 'libpaper')
makedepends=('gnutls')

source=(http://downloads.ghostscript.com/public/ghostscript-${pkgver}.tar.bz2
        ghostscript-sys-zlib.patch)
sha256sums=('e65daf25059e2f3dabd3535df3d936a42ca4933d36473a03095e39adcdd5315c'
            'c08c7e1354aaa243e753517c61ff86a799a49e0177c7bf6fe0029abc693386f6')

prepare() {
  cd ghostscript-${pkgver}
  # fix build with system zlib
  patch -Np1 -i ${srcdir}/ghostscript-sys-zlib.patch
}

build() {
  cd ghostscript-${pkgver}
  
  # force it to use system-libs
  # keep heavily patched included openjpeg, leads to segfault with system openjpeg
  # https://bugs.archlinux.org/task/38226
  rm -rf jpeg libpng zlib jasper expat tiff lcms lcms2 freetype cups/libs # jbig2dec is in community

  autoconf --force

  ./configure --prefix=/usr \
	--libdir=/usr/lib/${MULTIARCH} \
	--enable-dynamic \
	--with-ijs \
	--with-jbig2dec \
	--with-omni \
	--with-x \
	--with-drivers=ALL\
	--with-fontpath=/usr/share/fonts/Type1:/usr/share/fonts \
	--enable-fontconfig \
	--enable-freetype \
	--enable-openjpeg \
	--without-luratech \
	--without-omni \
	--with-system-libtiff \
	--with-libpaper \
	--disable-compile-inits #--help # needed for linking with system-zlib
  make

  # Build IJS
  cd ijs
  sed -i "s:AM_PROG_CC_STDC:AC_PROG_CC:g" configure.ac
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
              --enable-shared --disable-static
  make
}

package() {
  cd ghostscript-${pkgver}
  make DESTDIR="${pkgdir}" \
	cups_serverroot="${pkgdir}"/etc/cups \
	cups_serverbin="${pkgdir}"/usr/lib/${MULTIARCH}/cups install install-so

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # remove unwanted localized man-pages
  rm -rf "$pkgdir"/usr/share/man/[^man1]*

  # install IJS
  cd ijs
  make DESTDIR="${pkgdir}" install
  
  # remove filters that are now maintained in cups-filters as upstream home
  rm -rf "$pkgdir"/usr/lib/cups/filter/{gstopxl,gstoraster}
}
