pkgname=ruby
pkgdesc='An object-oriented language for quick and easy programming'
pkgver=2.2.0
pkgrel=1
arch=('x86_64')
url='http://www.ruby-lang.org/en/'
options=('!emptydirs' '!makeflags' 'staticlibs')
license=('BSD' 'custom')
backup=('etc/gemrc')

depends=('gdbm' 'openssl' 'libffi' 'libyaml' 'gmp' 'zlib')

source=("http://cache.ruby-lang.org/pub/ruby/2.2/ruby-${pkgver}.tar.xz"
        'gemrc')
sha256sums=('848714e280fc5fb44dbac3b060b206e56c1947006324ee68a174d68b483ef8ca'
            '4bb7eb2fe66e396ed16b589cdb656831407b39ad4e138d88536754c0448ac614')

build() {
  cd ruby-${pkgver}

  PKG_CONFIG=/usr/bin/pkg-config ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sharedstatedir=/var/lib \
    --libdir=/usr/lib/${MULTIARCH} \
    --libexecdir=/usr/lib/${MULTIARCH}/ruby \
    --enable-shared \
    --disable-rpath \
    --with-dbm-type=gdbm_compat

  make
}

check() {
  cd ruby-${pkgver}

  make test
}

package_ruby() {
  cd ruby-${pkgver}

  make DESTDIR="${pkgdir}" install-nodoc

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  install -D -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/LICENSE"
  install -D -m644 BSDL "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/BSDL"
}
