pkgname=init
pkgver=1.0
pkgrel=1
pkgdesc="A lightweight init system"
arch=("x86_64")
url="https://github.com/archsys/init"
license=('MIT')

conflicts=('upstart')
depends=('glibc' 'sh' 'util-linux')

source=(https://github.com/arachsys/init/archive/init-${pkgver}.tar.gz
        rc.startup
        rc.shutdown
        rc.functions
        rc.pidfile
        initemit
        initstatus)
sha256sums=('87b80e77935ace9e18e8e70255f046281550686526e5607912221ab6a1344e10'
            '9ae20df9bc2c137b1b3467fe952f71961b8d30663444d5276ef76d115c94ac57'
            '246c24765a0fedecfe7f284a11ab63be391fcb8aa8fb7917f7d6164aebd620e9'
            'c590a6310d8b1bfeb9a57b4b56e53eb0c316a839abf3a26cf856b4a03584538a'
            'a6f03e219fedfe8fefe4da16bd09523bb35f9f4f2271ca2b9e332058d40a513c'
            '4f6c6a12e4999c9db1a5f273c81eb5dd714706c8bedb61c57130e7de9c95cfe1'
            '4d53f5cae132415a48a8653c4cf361570bb86849d6ecdcb8952a17f77bf92cea')

build() {
	cd "${srcdir}"/init-${pkgname}-${pkgver}
	make
}

package() {
	cd "${srcdir}"
	
	(cd init-${pkgname}-${pkgver}
	  make install DESTDIR="${pkgdir}" PREFIX=/usr )
	
	# Don't package uevent or syslog at this point
	rm "${pkgdir}"/usr/bin/syslog*
	rm "${pkgdir}"/usr/bin/uevent*
	
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m744 rc.startup rc.shutdown "${pkgdir}"/etc/
	install -m644 rc.functions "${pkgdir}"/etc/rc.d/
	install -m644 rc.pidfile "${pkgdir}"/etc/rc.d/

	install -m755 initemit "${pkgdir}"/usr/bin
	install -m755 initstatus "${pkgdir}"/usr/bin
}
