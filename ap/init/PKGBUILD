pkgname=init
pkgver=1.0
pkgrel=1
pkgdesc="A lightweight init system"
arch=("x86_64")
url="https://github.com/archsys/init"
license=('MIT')

conflicts=('upstart')
depends=('glibc' 'sh' 'util-linux')

source=(https://github.com/arachsys/init/archive/init-${pkgver}.tar.gz
        rc.startup
        rc.shutdown
        rc.functions
        rc.pidfile
        initemit
        initstatus)
sha256sums=('87b80e77935ace9e18e8e70255f046281550686526e5607912221ab6a1344e10'
            'b143a88465c83a8ebf0c146b1d0fff7bb4558406751bfaae1276baa7f4568452'
            '246c24765a0fedecfe7f284a11ab63be391fcb8aa8fb7917f7d6164aebd620e9'
            'aa6649f002a5cf13cd956083341730c56fbd685519cd264bf4e72ab0cd05235c'
            'a6f03e219fedfe8fefe4da16bd09523bb35f9f4f2271ca2b9e332058d40a513c'
            '4f6c6a12e4999c9db1a5f273c81eb5dd714706c8bedb61c57130e7de9c95cfe1'
            '4d53f5cae132415a48a8653c4cf361570bb86849d6ecdcb8952a17f77bf92cea')

build() {
	cd "${srcdir}"/init-${pkgname}-${pkgver}
	make
}

package() {
	cd "${srcdir}"
	
	(cd init-${pkgname}-${pkgver}
	  make install DESTDIR="${pkgdir}" PREFIX=/usr )
	
	# Don't package uevent or syslog at this point
	rm "${pkgdir}"/usr/bin/syslog*
	rm "${pkgdir}"/usr/bin/uevent*
	
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m744 rc.startup rc.shutdown "${pkgdir}"/etc/
	install -m644 rc.functions "${pkgdir}"/etc/rc.d/
	install -m644 rc.pidfile "${pkgdir}"/etc/rc.d/

	install -m755 initemit "${pkgdir}"/usr/bin
	install -m755 initstatus "${pkgdir}"/usr/bin
}
