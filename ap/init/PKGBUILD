pkgname=init
pkgver=1.0
pkgrel=1
pkgdesc="A lightweight init system"
arch=("x86_64")
url="https://github.com/archsys/init"
license=('MIT')

conflicts=('upstart')
depends=('glibc' 'sh')

source=(https://github.com/arachsys/init/archive/init-${pkgver}.tar.gz
        rc.startup
        rc.shutdown
        rc.functions
        initemit)
sha256sums=('87b80e77935ace9e18e8e70255f046281550686526e5607912221ab6a1344e10'
            'bfa4415a39af15082770f13435862098ac68328852eb21e2e01f84a6d6656f5c'
            '246c24765a0fedecfe7f284a11ab63be391fcb8aa8fb7917f7d6164aebd620e9'
            '812f1a44bc2b319dddb068a03ffa394bbb1a865ef167af810d52c58170af617e'
            '4f6c6a12e4999c9db1a5f273c81eb5dd714706c8bedb61c57130e7de9c95cfe1')

build() {
	cd "${srcdir}"/init-${pkgname}-${pkgver}
	make
}

package() {
	cd "${srcdir}"
	
	(cd init-${pkgname}-${pkgver}
	  make install DESTDIR="${pkgdir}" PREFIX=/usr )
	
	# Don't package uevent or syslog at this point
	rm "${pkgdir}"/usr/bin/syslog*
	rm "${pkgdir}"/usr/bin/uevent*
	
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m744 rc.startup rc.shutdown "${pkgdir}"/etc/
	install -m644 rc.functions "${pkgdir}"/etc/rc.d/

	install -m755 initemit "${pkgdir}"/usr/bin
}
