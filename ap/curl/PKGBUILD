pkgname=curl
pkgver=7.39.0
pkgrel=1
pkgdesc="A URL transfer tool and library."
arch=("x86_64")
url="http://curl.haxx.se"
license=(custom)

depends=('glibc' 'zlib' 'openssl' 'libidn')

source=(http://curl.haxx.se/download/curl-${pkgver}.tar.bz2{,.asc}
		http://curl.haxx.se/ca/cacert.pem)
sha256sums=('b222566e7087cd9701b301dd6634b360ae118cc1cbc7697e534dc451102ea4e0'
            'SKIP'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --mandir=/usr/man \
	            --with-ca-bundle=/usr/share/curl/ca-bundle.crt
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	# Set up certs
	mkdir -p "${pkgdir}"/usr/share/curl
	cp -a "${srcdir}"/cacert.pem "${pkgdir}"/usr/share/curl/
	( cd "${pkgdir}"/usr/share/curl
	  mv cacert.pem ca-bundle.crt
	  chmod 644 ca-bundle.crt	
	)
	
	# Remove perl script
	rm -f "${pkgdir}"/usr/man/man1/mk-ca-bundle.1
}
