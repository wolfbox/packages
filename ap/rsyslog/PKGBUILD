pkgname=rsyslog
pkgver=8.4.2
pkgrel=1
pkgdesc="The rocket-fast syslog daemon."
arch=("x86_64")
url="http://www.rsyslog.com"
license=(GPLv3)

install=rsyslog.install
backup=('etc/rsyslog.conf')
depends=('glibc' 'zlib' 'libestr' 'liblogging' 'json-c')

source=("http://www.rsyslog.com/files/download/rsyslog/rsyslog-${pkgver}.tar.gz"
        rsyslog.upstart
        rsyslog.conf)
sha256sums=(71c3c6dac74fba2692f9fefb092cd3d22e2bd71eb702e0da06648de51b8b7b9a
            4423077338a0c1e91ad49948a9e3a750ce0b509296c65f3de81bbb757b8067d6
            624877f2147e9be89a1913bfbfaa73ae8dd726863336042b58caea008bf87721)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Harden
	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --mandir=/usr/man \
				--sbindir=/usr/bin --sysconfdir=/etc \
				--disable-generate-man-pages
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}/etc/init"
	install -m 644 "${srcdir}"/rsyslog.upstart "${pkgdir}"/etc/init/rsyslog.conf
	
	mkdir -p "${pkgdir}"/etc/
	install -m 644 "${srcdir}"/rsyslog.conf "${pkgdir}"/etc/

	mkdir -p "${pkgdir}"/var/lib/rsyslog
}
