pkgname=rsyslog
pkgver=8.4.2
pkgrel=1
pkgdesc="The rocket-fast syslog daemon."
arch=("x86_64")
url="http://www.rsyslog.com"
license=(GPLv3)

install=rsyslog.install
backup=('etc/rsyslog.conf')
depends=('glibc' 'zlib' 'libestr' 'liblogging' 'json-c' 'util-linux')

source=("http://www.rsyslog.com/files/download/rsyslog/rsyslog-${pkgver}.tar.gz"
        rsyslog.conf
        rsyslog
        rsyslog.filesystem)
sha256sums=('71c3c6dac74fba2692f9fefb092cd3d22e2bd71eb702e0da06648de51b8b7b9a'
            '5a0507d45ce53e0f8d16e639b729c067b977bf02718288664aff2b2bfba3fdff'
            'cc97caeecf776764422d892da8d9961815755d1ec58d9b940d98f6a1c2852cef'
            '42421a551cfb9087471bb92675122c891618e20ce599d70b7b34768644bc959d')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Harden
	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --mandir=/usr/man \
				--sbindir=/usr/bin --sysconfdir=/etc \
				--disable-generate-man-pages
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m 755 "${srcdir}"/rsyslog "${pkgdir}"/etc/rc.d/
	install -m 755 "${srcdir}"/rsyslog.filesystem "${pkgdir}"/etc/rc.d/events/
	install -m 644 "${srcdir}"/rsyslog.conf "${pkgdir}"/etc/

	mkdir -p "${pkgdir}"/var/run/rsyslog
	mkdir -p "${pkgdir}"/var/lib/rsyslog
	mkdir -p "${pkgdir}"/var/log/syslog
	chmod 770 "${pkgdir}"/var/log/syslog
}
