pkgname=findutils
pkgver=4.4.2
pkgrel=1
pkgdesc="GNU utilities to locate files"
arch=('x86_64')
license=('GPL3')
url="http://www.gnu.org/software/findutils"

depends=('glibc' 'sh')

source=(ftp://ftp.gnu.org/pub/gnu/findutils/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('434f32d171cbc0a5e72cfc5372c6fc4cb0e681f8dce566a0de5b6fccd702b62a'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't build or install locate because we use mlocate,
  # which is a secure version of locate.
  sed -i '/^SUBDIRS/s/locate//' Makefile.in

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir install
}
