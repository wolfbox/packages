pkgname=clamav
pkgver=0.98.5
pkgrel=1
pkgdesc="A malware detection tool"
arch=("x86_64")
url="http://www.clamav.net"
license=('GPLv2' 'LGPLv2.1')
install=clamav.install
backup=('etc/freshclam.conf')

depends=('gcc-libs' 'glibc' 'zlib' 'bzip2' 'openssl' 'curl' 'xz' 'libxml2')

source=("http://downloads.sourceforge.net/project/clamav/clamav/${pkgver}/clamav-${pkgver}.tar.gz")
sha256sums=('9c2f37d5fa9033936468237f6f4b29289f30791caa84765ef510acb56c8daa4d')

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}

	export CFLAGS="${CFLAGS} -fPIE"
	export CXXFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --sbindir=/usr/bin \
	            --sysconfdir=/etc \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --sysconfdir=/etc \
	            --mandir=/usr/man \
	            --enable-llvm \
	            --with-dbdir=/var/lib/clamav \
	            --build=${CHOST}
	make
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	make install DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}"/var/lib/clamav
	( cd "${pkgdir}"/etc
	  mv freshclam.conf.sample freshclam.conf )
}
