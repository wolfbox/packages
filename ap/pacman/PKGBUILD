pkgname=pacman
pkgver=4.2.0
pkgrel=1
pkgdesc="A simple and fast package manager."
arch=("x86_64")
url="https://www.archlinux.org/pacman/"
license=(GPLv2)

backup=('etc/makepkg.conf' 'etc/pacman.conf')
depends=('glibc' 'bash' 'libarchive' 'curl' 'gpgme' 'libxml2' 'fakeroot')

source=(ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz{,.sig}
        makepkg.conf)
sha256sums=('941aca897db3218f0cb4036de13422d74212afbcdbf4a7cd7074a7128e196a22'
            'SKIP'
            'f834a404fb47862563141bcc1bfecd2fb3540ef1b0e44476a03e12734949a01a')
validpgpkeys=(6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD) # Allan McRae

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
				--localstatedir=/var --mandir=/usr/man \
				--with-ldconfig=/usr/bin/ldconfig \
				--with-scriptlet-shell=/usr/bin/bash \
				--build=${CHOST}
	make
	make -C contrib
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	make install -C contrib DESTDIR="${pkgdir}"
	install -m644 "${srcdir}"/makepkg.conf "${pkgdir}"/etc/
	
	rm -rf "${pkgdir}"/etc/bash_completion.d
}
