pkgname=acl
pkgver=2.2.52
pkgrel=1
pkgdesc="Tools for using POSIX Access Control Lists."
arch=("x86_64")
url="http://savannah.nongnu.org/projects/acl"
license=("LGPLv2.1" "GPLv2")

depends=('glibc')

source=(http://download.savannah.gnu.org/releases/acl/acl-${pkgver}.src.tar.gz{,.sig})
sha256sums=('179074bb0580c06c4b4137be4c5a92a701583277967acdb5546043c7874e0d23'
            'SKIP')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34') # Brandon Philips <brandon@ifup.co>

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export INSTALL_USER=root INSTALL_GROUP=root
	./configure --prefix=/usr --sbindir=/usr/bin --bindir=/usr/bin \
				--libdir=/usr/lib/${MULTIARCH} --libexecdir=/usr/lib/${MULTIARCH} \
				--datadir=/usr/share
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DIST_ROOT="${pkgdir}" install install-lib install-dev
}
