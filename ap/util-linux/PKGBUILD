pkgname=util-linux
pkgver=2.25.1
pkgrel=1
pkgdesc="A broad collection of Linux system utilities."
arch=("x86_64")
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=(GPLv2)
install=util-linux.install

depends=('glibc' 'ncurses')

source=(https://www.kernel.org/pub/linux/utils/util-linux/v2.25/util-linux-${pkgver}.tar.xz
        upstart-scripts.tar.xz)
sha256sums=(4701305ae22790c3a92ce48e50794fa05b7ee01f4227f419a171c100d08986e8
            647e08c8d1a3c2f8c741281a7c73fefcd78ec80d3f0ed56802b2c7c93c9aab34)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
				--localstatedir=/var --mandir=/usr/man --infodir=/usr/info \
				--docdir=/usr/doc/util-linux-${pkgver} --sbindir=/usr/bin \
				--bindir=/usr/bin --disable-static --disable-uuidd \
				--disable-login --disable-nologin --disable-sulogin
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make install DESTDIR="${pkgdir}"

	# Fix the binary paths
	mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
	rmdir "${pkgdir}"/usr/sbin

	# Install the upstart scripts
	mkdir -p "${pkgdir}"/etc/init
	install -m 644 -t "${pkgdir}"/etc/init "${srcdir}"/upstart-scripts/*
	
	# Remove setuid-root permissions
	chmod -s "${pkgdir}"/usr/bin/{mount,umount,wall}
}
