# Derived from Arch Linux
# Arch Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Arch Contributor: dorphell <dorphell@archlinux.org>

pkgname=zip
pkgver=3.0
_pkgver=${pkgver/./}
pkgrel=4
pkgdesc='Compressor/archiver for creating and modifying zipfiles'
url='http://www.info-zip.org/Zip.html'
arch=('x86_64')
license=('custom')
options=('!makeflags')

depends=('bzip2')

source=("http://downloads.sourceforge.net/infozip/${pkgname}${_pkgver}.tar.gz")
sha256sums=('f0e8bb1f9b7eb0b01285495a2699df3a4b766784c1765a8f1aeedf63c0806369')


build() {
	cd "${srcdir}/${pkgname}${_pkgver}"
	make -f unix/Makefile prefix=/usr \
		LOCAL_ZIP="$CFLAGS $CPPFLAGS $LDFLAGS" \
		generic_gcc
}

package() {
	cd "${srcdir}/${pkgname}${_pkgver}"
	make -f unix/Makefile prefix="${pkgdir}"/usr \
		MANDIR=${pkgdir}/usr/share/man/man1 \
		install
	install -Dm644 LICENSE "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
