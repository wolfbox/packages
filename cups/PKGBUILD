pkgname=cups
pkgver=2.0.1
pkgrel=1
pkgdesc="The CUPS Printing System"
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
backup=(etc/cups/client.conf
        etc/cups/cupsd.conf
        etc/cups/snmp.conf
        etc/cups/printers.conf
        etc/cups/classes.conf
        etc/cups/cups-files.conf
        etc/cups/subscriptions.conf
        etc/dbus-1/system.d/cups.conf)
install=cups.install

makedepends=('gzip' 'autoconf' 'perl')
depends=('gnutls' 'libtiff>=4.0.0' 'libpng>=1.5.7' 'avahi' 'libusb'
         'acl' 'dbus' 'libpaper' 'hicolor-icon-theme' 'colord')

source=(http://www.cups.org/software/${pkgver}/cups-${pkgver}-source.tar.bz2
        # improve build and linking
        cups-no-export-ssllibs.patch
        cups-no-gcrypt.patch
        cups-no-gzip-man.patch
        cups-1.6.2-statedir.patch
        cups-1.6.0-fix-install-perms.patch
        cups)
sha256sums=('60f7f2cca69f3b761a029fb314ee4299e3ea0fc02350f06d192cf31d58215525'
            'ff3eb0782af0405f5dafe89e04b1b4ea7a49afc5496860d724343bd04f375832'
            '1423673e16e374ed372c5b69aebc785b6674bf40601c74a5c08454f672ffa7f1'
            '125e42dc86ecc4adc7c8d0f3743b5ce444708c96c77546448dc2d7a06f6ada75'
            '23349c96f2f7aeb7d48e3bcd35a969f5d5ac8f55a032b0cfaa0a03d7e37ea9af'
            '4a4a885bb2e111bd67bcb90a5780f33841b18bc02382317fb5e64c384aa0c4c8'
            'SKIP')

prepare() {
  cd ${pkgbase}-${pkgver}
  
  # improve build and linking
  # Do not export SSL libs in cups-config
  patch -Np1 -i ${srcdir}/cups-no-export-ssllibs.patch
  # https://www.cups.org/str.php?L4399
  patch -Np1 -i ${srcdir}/cups-no-gcrypt.patch
  # don't zip man pages in make install, let makepkg do that / Fedora
  patch -Np1 -i ${srcdir}/cups-no-gzip-man.patch
  # move /var/run -> /run for pid file
  patch -Np1 -i ${srcdir}/cups-1.6.2-statedir.patch
  # fix permissions on some files (by Gentoo) - alternative: cups-0755.patch by FC
  patch -Np0 -i ${srcdir}/cups-1.6.0-fix-install-perms.patch

  # bug fixes 
  
  # set MaxLogSize to 0 to prevent using cups internal log rotation
  sed -i -e '5i\ ' conf/cupsd.conf.in
  sed -i -e '6i# Disable cups internal logging - use logrotate instead' conf/cupsd.conf.in
  sed -i -e '7iMaxLogSize 0' conf/cupsd.conf.in
  
  # Rebuild configure script for not zipping man-pages.
  aclocal -I config-scripts
  autoconf -I config-scripts
}

build() {
  cd ${pkgbase}-${pkgver}

  # Hardening
  export CFLAGS="${CFLAGS} -fPIE"
  export CXXFLAGS="${CXXFLAGS} -fPIE"
  export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

  ./configure --prefix=/usr \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --sbindir=/usr/bin \
     --libdir=/usr/lib/${MULTIARCH} \
     --with-logdir=/var/log/cups \
     --with-docdir=/usr/share/cups/doc \
     --with-cups-user=lp \
     --with-cups-group=lp \
     --enable-pam=no \
     --enable-raw-printing \
     --enable-dbus --with-dbusdir=/etc/dbus-1 \
     --enable-ssl=yes \
     --enable-threads \
     --enable-avahi \
     --enable-libpaper \
     --with-optim="$CFLAGS" #--help
  make
}

package() {
  cd ${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-headers install-libs
  # put this into the libs pkg to make other software find the libs(no pkg-config file included)
  mkdir -p ${pkgdir}/usr/bin 
  install -m755 ${srcdir}/${pkgbase}-${pkgver}/cups-config ${pkgdir}/usr/bin/cups-config
  
  # install client.conf man page and config file
  install -dm755 ${pkgdir}/usr/share/man/man5
  install -Dm644  ${srcdir}/${pkgbase}-${pkgver}/man/client.conf.5 ${pkgdir}/usr/share/man/man5/
  install -dm755 -g lp ${pkgdir}/etc/cups
  touch ${pkgdir}/etc/cups/client.conf
  echo "# see 'man client.conf'" >> ${pkgdir}/etc/cups/client.conf
  echo "ServerName /run/cups/cups.sock #  alternative: ServerName hostname-or-ip-address[:port] of a remote server" >> ${pkgdir}/etc/cups/client.conf
  chgrp -R lp ${pkgdir}/etc/cups

  make BUILDROOT=${pkgdir} install

  # kill the sysv stuff
  rm -rf ${pkgdir}/etc/rc*.d
  rm -rf ${pkgdir}/etc/init.d
  
  # fix perms on /var/spool and /etc
  chmod 755 ${pkgdir}/var/spool
  chmod 755 ${pkgdir}/etc

  # install ssl directory where to store the certs, solves some samba issues
  install -dm700 -g lp ${pkgdir}/etc/cups/ssl
  # remove directory from package, it will be recreated at each server start
  rm -rf ${pkgdir}/run

  # install some more configuration files that will get filled by cupsd
  touch ${pkgdir}/etc/cups/printers.conf
  touch ${pkgdir}/etc/cups/classes.conf
  touch ${pkgdir}/etc/cups/subscriptions.conf 
  chgrp -R lp ${pkgdir}/etc/cups
  
  # Remove .desktop file
  rm -r "${pkgdir}"/usr/share/applications
  
  # compress some driver files, adopted from Fedora
  find ${pkgdir}/usr/share/cups/model -name "*.ppd" | xargs gzip -n9f
  
  # remove client.conf man page
  rm -f ${pkgdir}/usr/share/man/man5/client.conf.5
  
  # remove files now part of cups-filters
  rm -v ${pkgdir}/usr/share/cups/banners/*
  rm -v ${pkgdir}/usr/share/cups/data/testprint
  # comment out all conversion rules which use any of the removed filters
  perl -p -i -e 's:^(.*\s+bannertops\s*)$:#\1:' $pkgdir/usr/share/cups/mime/mime.convs
  
  # comment out unnecessary PageLogFormat entry
  sed -i -e 's:PageLogFormat:#PageLogFormat:' $pkgdir/etc/cups/cupsd.conf*
  
  mkdir -p "${pkgdir}"/etc/rc.d
  install -m755 "${srcdir}"/cups "${pkgdir}"/etc/rc.d/
}
