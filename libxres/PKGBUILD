# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libxres
pkgver=1.0.7
pkgrel=1
pkgdesc="X11 Resource extension library"
arch=('x86_64')
url="http://xorg.freedesktop.org"
license=('custom')

makedepends=('resourceproto' 'damageproto' 'compositeproto' 'scrnsaverproto' 'xorg-util-macros')
depends=('libxext')

source=("${url}/releases/individual/lib/libXres-${pkgver}.tar.bz2")
sha256sums=('26899054aa87f81b17becc68e8645b240f140464cf90c42616ebb263ec5fa0e5')

build() {
  cd "${srcdir}/libXres-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
              --libdir=/usr/lib/${MULTIARCH}
  make
}

package() {
  cd "${srcdir}/libXres-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
