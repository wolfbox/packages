pkgname=dcron
pkgver=4.5
pkgrel=1
pkgdesc="A clean periodical command scheduler"
arch=("x86_64")
url="http://www.jimpryor.net/linux/dcron.html"
license=('GPL')

depends=('glibc')

source=(http://www.jimpryor.net/linux/releases/dcron-${pkgver}.tar.gz
        dcron
        dcron.filesystem)
sha256sums=('9e50edb6f5bd8153b16bad05087d985e5153ce45cc01ae77e7f842213fb4a824'
            '461d88ec67e39a5b38bb43a081c0f426e2593b08370a83228406b96064443997'
            'c4b78cea7a6fff00edf9b9056d3274ec7786cd5b5ad4093f4e711bf827e6fde0')

_MAKEOPTS="PREFIX=/usr SBINDIR=/usr/bin BINDIR=/usr/bin MANDIR=/usr/share/man CRONTABS=/var/lib/cron/crontabs CRONSTAMPS=/var/lib/cron/cronstamps SHELL=/usr/bin/sh"

prepare() {
	cd "${srcdir}"/${pkgname}-${pkgver}

	# We don't use /var/spool, so update the manpages accordingly
	sed -i "s:/var/spool:/var/lib:" crond.8 crontab.1
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"
	make ${_MAKEOPTS} LDFLAGS="${LDFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" ${_MAKEOPTS} install

	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m755 "${srcdir}"/dcron "${pkgdir}"/etc/rc.d/
	install -m755 "${srcdir}"/dcron.filesystem "${pkgdir}"/etc/rc.d/events/
}
