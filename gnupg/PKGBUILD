pkgname=gnupg
pkgver=2.1.9
pkgrel=1
pkgdesc="The GNU Privacy Guard cryptography tool"
arch=("x86_64")
url="http://gnupg.org"
license=('GPL3' 'LGPL3')
install=gnupg.install

depends=('glibc' 'libgpg-error' 'libgcrypt>=1.6' 'npth' 'zlib' 'readline'
         'ncurses' 'libassuan' 'libksba' 'pinentry')

source=(ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-${pkgver}.tar.bz2{,.sig})
sha256sums=('1cb7633a57190beb66f9249cb7446603229b273d4d89331b75c652fa4a29f7b6'
            'SKIP')
validpgpkeys=('031EC2536E580D8EA286A9F22071B08A33BD3F06'  # NIIBE Yutaka (GnuPG Release Key) <gniibe@fsij.org>
              'D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch
build() {
    cd ${pkgname}-${pkgver}

    export CFLAGS="${CFLAGS} -fPIE"
    export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --sysconfdir=/etc \
                --docdir=/usr/share/doc/${pkgname} \
                --libdir=/usr/lib/${MULTIARCH} \
                --libexecdir=/usr/lib/${MULTIARCH}/gnupg \
                --enable-large-secmem \
                --with-capabilities \
                --enable-symcryptrun \
                --enable-gpgtar \
                --build=${CHOST}
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make install DESTDIR="${pkgdir}"
	
    # Link gpg to gpg2
    ( cd "${pkgdir}"/usr
      ln -s gpg2 bin/gpg
      ln -s gpgv2 bin/gpgv
      ln -s gpg2.1.gz share/man/man1/ )

    # Redundant
    rm "${pkgdir}"/usr/share/gnupg/com-certs.pem
}
