pkgname=linux
pkgver=3.18.5
pkgrel=1
pkgdesc="The Linux kernel."
arch=("x86_64")
url="http://www.kernel.org"
license=('GPL2')
options=(!strip)

install=linux.install
makedepends=('gcc' 'lz4' 'bc')

_grsecver=3.0-${pkgver}-201501310706
source=(https://www.kernel.org/pub/linux/kernel/v3.x/linux-${pkgver}.tar.{sign,xz}
        https://grsecurity.net/test/grsecurity-${_grsecver}.patch{,.sig}
        config
        x509.genkey)
sha256sums=('SKIP'
            'e4442436e59c74169e98d38d2e2a434c7b73f8eda0aa8f20e454eaf52270fc90'
            '56fbe38b2700d85c84c535c77921e77d35d389fb5eaa4c4f025327d8dd8c1225'
            'SKIP'
            '186ac761b4ee344f429336c286b3f6019cf690a7907e02a7b90277b9dbd28ee7'
            'ca581821060e883b03f660d69ed0e9a7cbbf284dd757995bd31ffefa551ef316')
validpgpkeys=(647F28654894E3BD457199BE38DBBDC86092693E # Greg K-H
              DE9452CE46F42094907F108B44D1C0F82525FE49) # Bradley Spengler

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	patch -p1 -i ../grsecurity-${_grsecver}.patch
	cp "${srcdir}/config" .config
	cp "${srcdir}"/x509.genkey ./
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	mkdir -p "${pkgdir}/boot"
	
	install -m 644 arch/x86/boot/bzImage "${pkgdir}"/boot/vmlinuz-${pkgver}
	make modules_install INSTALL_MOD_PATH="${pkgdir}"
	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"
	
	depmod -b "${pkgdir}" ${pkgver}-grsec

	# Remove broken "build" symlink
	rm "${pkgdir}"/lib/modules/${pkgver}-grsec/build
}
