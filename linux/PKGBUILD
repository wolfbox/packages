pkgname=linux
pkgver=3.18.7
pkgrel=1
pkgdesc="The Linux kernel"
arch=("x86_64")
url="http://www.kernel.org"
license=('GPL2')
options=(!strip)

install=linux.install
makedepends=('gcc' 'lz4' 'bc')
depends=('linux-firmware' 'kmod')

_grsecver=3.1-${pkgver}-201502222138
source=(https://www.kernel.org/pub/linux/kernel/v3.x/linux-${pkgver}.tar.{sign,xz}
        https://grsecurity.net/test/grsecurity-${_grsecver}.patch{,.sig}
        config
        x509.genkey)
sha256sums=('SKIP'
            'df27f4dd3b2657fb29d76af2190289f54fb651b82d2bcd225aa846973a22f421'
            '2bfe1b3e6b3eeb10eeca68735e796f98e4856a2014feb2447510239dfce4d636'
            'SKIP'
            '186ac761b4ee344f429336c286b3f6019cf690a7907e02a7b90277b9dbd28ee7'
            'ca581821060e883b03f660d69ed0e9a7cbbf284dd757995bd31ffefa551ef316')
validpgpkeys=(647F28654894E3BD457199BE38DBBDC86092693E # Greg K-H
              DE9452CE46F42094907F108B44D1C0F82525FE49) # Bradley Spengler

prepare() {
	cd ${pkgname}-${pkgver}
	
	patch -p1 -i ../grsecurity-${_grsecver}.patch
	cp "${srcdir}/config" .config
	cp "${srcdir}"/x509.genkey ./
}

build() {
	cd ${pkgname}-${pkgver}
	make ${MAKEFLAGS}
}

package() {
	cd ${pkgname}-${pkgver}
	
	mkdir -p "${pkgdir}/boot"
	
	install -m 644 arch/x86/boot/bzImage "${pkgdir}"/boot/vmlinuz-${pkgver}
	make modules_install INSTALL_MOD_PATH="${pkgdir}"
	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"
	
	depmod -b "${pkgdir}" ${pkgver}-grsec

    # Remove firmware; we package it separately
    rm -r "${pkgdir}"/lib/firmware

	# Remove broken "build" symlink
	rm "${pkgdir}"/lib/modules/${pkgver}-grsec/build
}
