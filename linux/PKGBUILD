pkgname=linux
pkgver=3.18.3
pkgrel=1
pkgdesc="The Linux kernel."
arch=("x86_64")
url="http://www.kernel.org"
license=('GPL2')
options=(!strip)

install=linux.install
makedepends=('gcc' 'lz4')

_grsecver=3.0-3.18.3-201501211944
source=(https://www.kernel.org/pub/linux/kernel/v3.x/linux-${pkgver}.tar.{sign,xz}
        https://grsecurity.net/test/grsecurity-${_grsecver}.patch{,.sig}
        config
        x509.genkey)
sha256sums=('SKIP'
            '0e024cd0bba93f777e733381b1ee2d55a76e4b30e096b0668aba979390e94255'
            '3b32e99637e05ddf98100a8e290fcbb67e689eb5405515c837541062e848df8b'
            'SKIP'
            '09befee4e8f71f55bece8389fd198c25da845b66fe209ae434941c457582636f'
            'ca581821060e883b03f660d69ed0e9a7cbbf284dd757995bd31ffefa551ef316')
validpgpkeys=(647F28654894E3BD457199BE38DBBDC86092693E # Greg K-H
              DE9452CE46F42094907F108B44D1C0F82525FE49) # Bradley Spengler

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	patch -p1 -i ../grsecurity-${_grsecver}.patch
	cp "${srcdir}/config" .config
	cp "${srcdir}"/x509.genkey ./
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make ${MAKEFLAGS}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	mkdir -p "${pkgdir}/boot"
	
	install -m 644 arch/x86/boot/bzImage "${pkgdir}"/boot/vmlinuz-${pkgver}
	make modules_install INSTALL_MOD_PATH="${pkgdir}"
	make headers_install INSTALL_HDR_PATH="${pkgdir}/usr"
	
	depmod -b "${pkgdir}" ${pkgver}-grsec

	# Remove broken "build" symlink
	rm "${pkgdir}"/lib/modules/${pkgver}-grsec/build
}
