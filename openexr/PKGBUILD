pkgname=openexr
pkgver=2.1.0
pkgrel=1
pkgdesc="An high dynamic-range image file format library"
url="http://www.openexr.com/"
arch=('x86_64')
license=('BSD')

depends=('zlib' 'ilmbase')

source=("http://download.savannah.nongnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('54486b454073c1dcb5ae9892cf0f730ffefe62f38176325281505093fd218a14')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix testing dir; /var/tmp isn't actually a thing
  sed -i 's:/var/tmp:/tmp:' IlmImfTest/tmpDir.h
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
