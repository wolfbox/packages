pkgname=libtheora
pkgver=1.1.1
pkgrel=1
pkgdesc="An open video codec developed by the Xiph.org"
arch=('x86_64')
url="http://www.xiph.org"
license=('BSD')

depends=('libogg')
makedepends=('libvorbis')

source=(http://downloads.xiph.org/releases/theora/libtheora-${pkgver}.tar.bz2
        libtheora-1.1.1-libpng16.patch)
sha256sums=('b6ae1ee2fa3d42ac489287d3ec34c5885730b1296f0801ae577a35193d3affbc'
            'e4c9a8dc798c596ed32a2a720020ae27a0e72f5add1a47cb8fadebe0e7180d7e')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -Np0 -i ../libtheora-1.1.1-libpng16.patch
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --enable-shared --disable-static
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
    install -m644 LICENSE COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
