# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xf86-input-evdev
pkgver=2.9.1
pkgrel=1
pkgdesc="X.org evdev input driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
options=('!makeflags')

makedepends=('xorg-server-devel' 'resourceproto' 'scrnsaverproto')
depends=('glibc' 'mtdev' 'libevdev')
conflicts=('xorg-server<1.16.0' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22')

source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('0ce30328adfeac90a6f4b508d992fb834d8e50b484b29d3d58cf6683fa8502f9'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
