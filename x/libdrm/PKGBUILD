# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libdrm
pkgver=2.4.58
pkgrel=1
pkgdesc="Userspace access to DRM"
arch=("x86_64")
url="http://dri.freedesktop.org"
license=('x11')
options=(!staticlibs)

depends=('glibc' 'libpciaccess')

source=(http://dri.freedesktop.org/libdrm/libdrm-${pkgver}.tar.bz2)
sha256sums=('b155fae6b9c9a3b02ef8b77f58c7c219194c996a4018dc55ba66c03996a365dd')

prepare() {
	cd "${srcdir}"/${pkgname}-${pkgver}

	# Remove unneeded pthreads thing
	sed -i "/pthread-stubs/d" configure.ac
	autoreconf --force --install
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
	            --enable-udev
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
