# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libxcb
pkgver=1.11
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xcb.freedesktop.org/"
license=('custom')

makedepends=('libxslt' 'python' 'xorg-util-macros')
depends=('xcb-proto>=1.11' 'libxdmcp' 'libxau')

source=($url/dist/$pkgname-$pkgver.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch)
sha256sums=('03635d70045b9ede90778e67516135828a57de87ac508f987024f43c03620ff7'
            '3923bcb1930b851012968435909597d8d5251c72153511cb2982636c97100cc3')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../libxcb-1.1-no-pthread-stubs.patch
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --libdir=/usr/lib/${MULTIARCH} \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/doc/$pkgname"
}
