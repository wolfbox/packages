pkgname=kdbusaddons
pkgver=5.6.0
pkgrel=1
pkgdesc='KDE convenience methods on top of the Qt DBus framework'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kdbusaddons'
license=('LGPLv2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5')

source=(http://download.kde.org/stable/frameworks/5.6/${pkgname}-${pkgver}.tar.xz)
sha256sums=('79989ebb951f617326f574fd22fd9d60d257128e698f8ef4e136b481526dc4be')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
