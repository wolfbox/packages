pkgname=kcompletion
pkgver=5.6.0
pkgrel=1
pkgdesc='KDE autocompletion component'
arch=('x86_64')
url='https://projects.kde.org/projects/frameworks/kcompletion'
license=('LGPLv2.1')

makedepends=('cmake' 'extra-cmake-modules')
depends=('glibc' 'gcc-libs' 'qt5' 'kconfig' 'kwidgetsaddons')

source=(http://download.kde.org/stable/frameworks/5.6/${pkgname}-${pkgver}.tar.xz)
sha256sums=('729f04bae36d292cf97bc058451b0e51e2c17c0aaf27c40280d2f9531706657e')

build() {
  mkdir -p build && cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib/${MULTIARCH} \
    -DLIBEXEC_INSTALL_DIR=lib/${MULTIARCH} \
    -DQML_INSTALL_DIR=lib/${MULTIARCH}/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
