# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xorg-appres
pkgver=1.0.4
pkgrel=1
pkgdesc="List X application resource database"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxt')
makedepends=('xorg-util-macros')
conflicts=('xorg-res-utils')

source=("http://xorg.freedesktop.org/archive/individual/app/appres-${pkgver}.tar.bz2")
sha256sums=('9f614e9427cd9641a4a801ed5055700afa410674c0b7aa186b86078f11b83e8d')

build() {
  cd "${srcdir}/appres-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/appres-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
