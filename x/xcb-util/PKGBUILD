# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xcb-util
pkgver=0.4.0
pkgrel=1
pkgdesc="Utility libraries for XC Binding"
arch=('x86_64')
url="http://xcb.freedesktop.org"
license=('custom')

makedepends=('gperf' 'xorg-util-macros')
depends=('libxcb')

source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('46e49469cb3b594af1d33176cd7565def2be3fa8be4371d62271fabb5eae50e9')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() { 
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYING \
    "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
