# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=gdk-pixbuf2
pkgver=2.31.1
pkgrel=1
pkgdesc="An image loading library"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
install=gdk-pixbuf2.install

makedepends=('gobject-introspection')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11' 'jasper')

source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-$pkgver.tar.xz)
sha256sums=('25a75e3c61dac11e6ff6416ad846951ccafac6486b1c6a1bfb0b213b99db52cd')

build() {
  cd gdk-pixbuf-$pkgver

  ./configure --prefix=/usr \
    --libdir=/usr/lib/${MULTIARCH} \
    --with-x11 \
    --with-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd gdk-pixbuf-$pkgver

  make DESTDIR="$pkgdir" install
}
