# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=vte
pkgver=0.28.2
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK2"
arch=('x86_64')
license=('LGPL')
options=('!emptydirs')
url="http://www.gnome.org"

makedepends=('intltool' 'gobject-introspection' 'python2')
depends=('gtk2')

source=(http://ftp.gnome.org/pub/GNOME/sources/vte/0.28/vte-$pkgver.tar.xz)
sha256sums=('86cf0b81aa023fa93ed415653d51c96767f20b2d7334c893caba71e42654b0ae')

prepare() {
  cd $pkgname-$pkgver
  rm -r doc/reference/html
}
build() {
  cd $pkgname-$pkgver
  #warning: type-punning to incomplete type might break strict-aliasing rules
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libdir=/usr/lib/${MULTIARCH} \
      --libexecdir=/usr/lib/${MULTIARCH}/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=2.0
  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
