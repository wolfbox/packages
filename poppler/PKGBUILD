pkgname=poppler
pkgver=0.30.0
pkgrel=1
pkgdesc="PDF rendering library"
arch=('x86_64')
license=('GPL')
options=('!emptydirs')
url="http://poppler.freedesktop.org/"

makedepends=('gtk2' 'pkg-config' 'gobject-introspection' 'icu' 'python2')
depends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg' 'lcms2' 'glib2' 'qt5')
optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')

source=(http://poppler.freedesktop.org/${pkgname}-${pkgver}.tar.xz)
sha256sums=('b616ee869d0b1f8a7a2c71cf346f55c1bff624cce4badebe17f506ec8ce7ddf5')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib/${MULTIARCH} \
      --localstatedir=/var --disable-static \
      --enable-cairo-output \
      --enable-xpdf-headers \
      --enable-libjpeg --enable-zlib \
      --enable-poppler-qt5 \
      --enable-poppler-glib
  make
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
