# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libxcursor
pkgver=1.1.14
pkgrel=1
pkgdesc="X cursor management library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
backup=(usr/share/icons/default/index.theme)

makedepends=('xorg-util-macros')
depends=('libxfixes' 'libxrender')
optdepends=('gnome-themes-standard: fallback icon theme')

source=(${url}/releases/individual/lib/libXcursor-${pkgver}.tar.bz2
        index.theme)
sha256sums=('9bc6acb21ca14da51bda5bc912c8955bc6e5e433f0ab00c5e8bef842596c33df'
            '9e2707bc3466b848a70687ae34b505930b9c260a49b3d2f014145e70c4cddcd6')

build() {
  cd "${srcdir}/libXcursor-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
              --libdir=/usr/lib/${MULTIARCH}
  make
}

package() {
  cd "${srcdir}/libXcursor-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/icons/default"
  install -m644 "$srcdir"/index.theme "${pkgdir}/usr/share/icons/default/"

  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
