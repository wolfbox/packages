pkgname=networkmanager
pkgver=1.0.0
pkgrel=1
pkgdesc="A pain-free networking service for desktops and laptops"
arch=("x86_64")
url="https://wiki.gnome.org/Projects/NetworkManager"
license=('GPL' 'LGPLv2')
backup=('etc/NetworkManager/NetworkManager.conf')

makedepends=('dbus-glib' 'vala')
depends=('glibc' 'dbus' 'dbus-glib' 'libndp' 'libsoup' 'nss' 'polkit'
         'readline' 'udev' 'libnl3' 'libffi' 'nspr' 'zlib' 'ncurses')

source=(https://download.gnome.org/sources/NetworkManager/1.0/NetworkManager-${pkgver}.tar.xz
        NetworkManager.conf
        networkmanager
        networkmanager.start_dbus)
sha256sums=('3a66afec670c975edd9832e620b725a5f16ed267a1b9e1b2d51ef27250d85947'
            'ac30cf5d6f795a4a85f33aefc351f74b1d21d7a9856878e73f9c69930f326fe8'
            '73bbaeeede33a5b62a11a4e55ac978f86e0b0209295df5b97e9d472d40e81d1e'
            'b6a0d94d494e86cd51dc6ba136911d7c1b01e54c9e0e24bc816f54ef644d3114')

build() {
	cd "${srcdir}/NetworkManager-${pkgver}"

	export CFLAGS="${CFLAGS} -fPIE"
	export CXXFLAGS="${CXXFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --sysconfdir=/etc \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --libexecdir=/usr/lib/${MULTIARCH} \
	            --sbindir=/usr/bin \
	            --mandir=/usr/share/man \
	            --with-dhcpcd=/usr/bin/dhcpcd \
	            --with-dhclient=/usr/bin/dhclient \
	            --with-crypto=nss \
	            --without-modem-manager-1 \
	            --disable-ppp \
	            --with-session-tracking=consolekit \
	            --enable-polkit=yes
	make
}

package() {
	cd "${srcdir}/NetworkManager-${pkgver}"

	make install DESTDIR="${pkgdir}"
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m755 "${srcdir}"/networkmanager "${pkgdir}"/etc/rc.d/
	install -m755 "${srcdir}"/networkmanager.start_dbus "${pkgdir}"/etc/rc.d/events/
	install -m 644 "${srcdir}"/NetworkManager.conf "${pkgdir}"/etc/NetworkManager/
}
