pkgname=mono
pkgver=3.12.1
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=('x86_64')
license=('GPL' 'LGPL2.1' 'MPL' 'custom:MITX11')
url="http://www.mono-project.com/"
options=('!makeflags')

depends=('zlib' 'libgdiplus>=3.8' 'sh' 'python')
provides=('monodoc')

source=(http://download.mono-project.com/sources/mono/${pkgname}-${pkgver}.tar.bz2
        mono.binfmt.d)
sha256sums=('5d8cf153af2948c06bc9fbf5088f6834868e4db8e5f41c7cff76da173732b60d'
            '9a657fc153ef4ce23bf5fc369a26bf4a124e9304bde3744d04c583c54ca47425')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --bindir=/usr/bin \
      --libdir=/usr/lib/${MULTIARCH} \
      --sbindir=/usr/bin \
      --disable-quiet-build \
      --disable-system-aot \
      --disable-static \
      --with-mcs-docs=no

    make

    cd mcs/jay
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    # install jay
    pushd "${srcdir}"/${pkgname}-${pkgver}/mcs/jay
    make DESTDIR="${pkgdir}" prefix=/usr INSTALL=../../install-sh install
    popd

    # install binfmt conf file and pathes
    install -D -m644 "${srcdir}"/mono.binfmt.d "${pkgdir}"/usr/lib/binfmt.d/mono.conf

    #install license
    mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
    install -m644 mcs/MIT.X11 "${pkgdir}"/usr/share/licenses/${pkgname}/

    #fix .pc file to be able to request mono on what it depends, fixes #go-oo build
    sed -i -e "s:#Requires:Requires:" "${pkgdir}"/usr/lib/${MULTIARCH}/pkgconfig/mono.pc
}
