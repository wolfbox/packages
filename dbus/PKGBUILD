pkgname=dbus
pkgver=1.6.30
pkgrel=1
pkgdesc="The D-Bus userspace message bus system."
arch=("x86_64")
url="http://www.freedesktop.org/wiki/Software/dbus/"
license=("GPLv2")

depends=('glibc' 'expat' 'libx11')

source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz{,.asc}
        dbus-1.6.x-allow_root_globally.diff.gz
        30-dbus
        dbus
        dbus.filesystem)
sha256sums=('e1a0066942895e285e449feef3c4799ab969cca0bc3b4bf805c0b6daa4e16695'
            'SKIP'
            'cd35621e4cc56403e9bb6ca6b431c8ae04f8cb8c626df40953e6f0f1bb488f64'
            '54f7304b8857f39a06ae5da6854d395520da544e64b6bb6c492de648a7888380'
            '375226316c9a4de9e4e5baa43af8420a56d86668f52bcd55c14fb843d0a227a9'
            '8dc424c7e30f19bb1b4e1ee569629c6f515e858e958a66080fb8fc16b6aca213')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90') # Simon McVittie <smcv@debian.org>

prepare() {
    cd "${srcdir}"/dbus-${pkgver}
	
    # Slackware patches
    # Root shouldn't be banned from doing anything ever
    zcat "${srcdir}"/dbus-1.6.x-allow_root_globally.diff.gz | patch -p1
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Harden
    export CFLAGS="${CFLAGS} -fPIE"
    export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

    ./configure --prefix=/usr \
                --libdir=/usr/lib/${MULTIARCH} \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libexecdir=/usr/lib/${MULTIARCH} \
                --docdir=/usr/share/doc/dbus-${pkgver} \
                --disable-doxygen-docs \
                --enable-shared=yes \
                --enable-static=no \
                --enable-inotify \
                --with-system-pid-file=/run/dbus/dbus.pid \
                --with-system-socket=/run/dbus/system_bus_socket \
                --disable-tests \
                --enable-x11-autolaunch
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make install DESTDIR="${pkgdir}"
    rm -f "${pkgdir}"/etc/rc.d/rc.messagebus

    mkdir -p "${pkgdir}"/etc/rc.d/events
    install -m755 "${srcdir}"/dbus "${pkgdir}"/etc/rc.d/
    install -m755 "${srcdir}"/dbus.filesystem "${pkgdir}"/etc/rc.d/events/
    install -Dm755 "${srcdir}"/30-dbus "${pkgdir}"/etc/X11/xinit/xinitrc.d/30-dbus

    # Remove the socket path; it has to be created at runtime since /run is a tmpfs
    rm -r "${pkgdir}"/var/run
}
