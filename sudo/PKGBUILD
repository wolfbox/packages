pkgname=sudo
pkgver=1.8.12
pkgrel=1
pkgdesc="A tool for giving and logging limited root privileges to select users"
arch=('x86_64')
url="http://www.courtesan.com/sudo/sudo.html"
license=('ISC')

backup=('etc/sudoers' 'etc/pam.d/sudo')
depends=('glibc' 'openpam' 'shadow')

source=(http://www.courtesan.com/sudo/dist/sudo-${pkgver}.tar.gz{,.sig}
        sudoers.conf)
sha256sums=('163b51841de8ad19276581a6782d61f5948f1f72a0a843371a1c167d3dc4f3b0'
            'SKIP'
            'edde4ddd9a5daaf56a4f4047645cc742a431177fd7bec2797742fd61addc6c05')
validpgpkeys=('CCB24BE9E9481B15D34159535A89DFA27EE470C4') # Todd C. Miller <Todd.Miller@courtesan.com>

build() {
    cd ${pkgname}-${pkgver}

    export LDFLAGS="${LDFLAGS} -Wl,-z,now"
    ./configure --prefix=/usr \
                --sbindir=/usr/bin \
                --libexecdir=/usr/lib/${MULTIARCH} \
                --docdir=/usr/share/doc/sudo \
                --with-env-editor \
                --with-rundir=/run/sudo \
                --with-vardir=/var/lib/sudo \
                --without-lecture \
                --with-logging=syslog \
                --with-logfac=auth \
                --with-secure-path \
                --enable-pie \
                --with-pam
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    # Install our sudoers file
    install -m644 "${srcdir}"/sudoers.conf "${pkgdir}"/etc/sudoers

    # Lock down permissions
    chmod 4711 "${pkgdir}"/usr/bin/sudo
    chmod 600 "${pkgdir}"/etc/sudoers
    chmod 700 "${pkgdir}"/etc/sudoers.d

    # Fix install path
    mv "${pkgdir}"/usr/share/examples "${pkgdir}"/usr/share/doc/examples

    # Set up PAM file
    mkdir -p "${pkgdir}"/etc/pam.d
    ln -s /etc/pam.d/su "${pkgdir}"/etc/pam.d/sudo
}
