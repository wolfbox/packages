pkgname=sudo
pkgver=1.8.11p2
pkgrel=1
pkgdesc="A tool for giving and logging limited root privileges to select users."
arch=('x86_64')
url="http://www.courtesan.com/sudo/sudo.html"
license=('ISC')

backup=('etc/sudoers')
depends=('glibc')

source=(http://www.courtesan.com/sudo/dist/sudo-${pkgver}.tar.gz
        sudoers.conf)
sha256sums=(8133849418fa18cf6b6bb6893d1855ff7afe21db8923234a00bf045c90fba1ad
            edde4ddd9a5daaf56a4f4047645cc742a431177fd7bec2797742fd61addc6c05)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	export LDFLAGS="${LDFLAGS} -Wl,-z,now"
	./configure --prefix=/usr --sbindir=/usr/bin --mandir=/usr/man \
	            --libexecdir=/usr/lib/${MULTIARCH} --docdir=/usr/doc/sudo-${pkgver} \
	            --with-env-editor --disable-pam-session --with-pam=no \
	            --with-rundir=/var/lib/sudo --with-vardir=/var/lib/sudo --without-lecture \
	            --with-logging=syslog --with-logfac=auth --with-secure-path \
	            --enable-pie
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Install our sudoers file
	install -m644 "${srcdir}"/sudoers.conf "${pkgdir}"/etc/sudoers

	# Lock down permissions
	chmod 4711 "${pkgdir}"/usr/bin/sudo
	chmod 600 "${pkgdir}"/etc/sudoers
	chmod 700 "${pkgdir}"/etc/sudoers.d
}
