pkgbase=gcc
pkgname=(gcc-libs gcc)
pkgver=4.9.2
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=("x86_64")
url="http://gcc.gnu.org"
license=(GPL3)

options=('strip')
makedepends=('zlib' 'glibc' 'lib32-glibc')
checkdepends=('autogen' 'dejagnu')

source=(http://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2{,.sig}
		gcc-no_fixincludes.diff
		t-linux64)
sha256sums=('2020c98295856aa13fda0f2f3a4794490757fc24bcca918d52cc8b4917b972dd'
            'SKIP'
            'ed0565cc5cd1f4dd459b9a665447a85fb3a8293f8eecabf5c3065a4405465382'
            'SKIP')
validpgpkeys=('33C235A34C46AA3FFB293709A328C3A2C3C45C06') # Jakub Jelinek <jakub@redhat.com>

_target_dir=usr/lib/${MULTIARCH}/gcc/${CHOST}/${pkgver}

MULTIARCH32=i386-linux-gnu

prepare() {
	cd gcc-${pkgver}

	# No fixincludes
	patch -p1 -i "${srcdir}"/gcc-no_fixincludes.diff

	# Fix libdirs
	install -m644 ../t-linux64 gcc/config/i386/t-linux64
	
	mkdir -p "${srcdir}/build"
}

build() {
	cd build

	# GCC doesn't compile with D_FORTIFY_SOURCE
	unset CPPFLAGS
	unset CFLAGS
	unset CXXFLAGS

	../gcc-${pkgver}/configure --prefix=/usr \
				--libexecdir=/usr/lib/${MULTIARCH} \
				--libdir=/usr/lib/${MULTIARCH} \
				--enable-shared \
				--enable-bootstrap \
				--enable-languages=c,c++,objc,go,fortran,lto \
				--enable-threads=posix \
				--enable-checking=release \
				--enable-objc-gc \
				--with-system-zlib \
				--disable-libunwind-exceptions \
				--enable-__cxa_atexit \
				--enable-libssp \
				--enable-vtable-verify \
				--enable-lto \
				--with-gnu-ld \
				--verbose \
				--disable-gtktest \
				--enable-multilib \
				--enable-multiarch \
				--target=${CHOST} \
				--build=${CHOST} \
				--host=${CHOST}
	make
}

check() {
    cd build

    # Some "failures" are expected; this just makes sure that the compiler
    # hasn't gone wildly crazy-pants.
    #make -k check || true
    #${srcdir}/gcc-${pkgver}/contrib/test_summary
}

# Packaging functions
package_gcc-libs() {
	pkgdesc="${pkgdesc} - Runtime libraries"
	depends=('glibc')
	
	# Set up our initial install tree
	cd "${srcdir}/build"
	rm -rf ../out
	mkdir ../out
	make install DESTDIR="${srcdir}"/out/

	# KLUDGE ALERT
	# Avoid ldconfig error
	rm -f "${srcdir}"/out/usr/lib/{${MULTIARCH},${MULTIARCH32}}/*gdb.py
	
	# Move runtime libraries
	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}
	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH32}
	mv "${srcdir}"/out/usr/lib/${MULTIARCH}/lib{asan,atomic,gcc_s,gfortran,gomp,itm,objc,quadmath,stdc++,tsan}.so* "${pkgdir}"/usr/lib/${MULTIARCH}
	mv "${srcdir}"/out/usr/lib/${MULTIARCH32}/lib{asan,atomic,gcc_s,gfortran,gomp,itm,objc,quadmath,stdc++}.so* "${pkgdir}"/usr/lib/${MULTIARCH32}

	# Info files
	mkdir -p "${pkgdir}"/usr/share/info
	mv "${srcdir}"/out/usr/share/info/lib{gomp,itm,quadmath}.info* "${pkgdir}"/usr/share/info/

	# Locale	
	mkdir -p "${pkgdir}"/usr/share/locale/de/LC_MESSAGES/
	mkdir -p "${pkgdir}"/usr/share/locale/fr/LC_MESSAGES/
	mv "${srcdir}"/out/usr/share/locale/de/LC_MESSAGES/libstdc++.mo "${pkgdir}"/usr/share/locale/de/LC_MESSAGES/
	mv "${srcdir}"/out/usr/share/locale/fr/LC_MESSAGES/libstdc++.mo "${pkgdir}"/usr/share/locale/fr/LC_MESSAGES/

	# License
	mkdir -p "${pkgdir}"/usr/share/doc/gcc-libs
	install -m644 "${srcdir}"/gcc-${pkgver}/COPYING.RUNTIME "${pkgdir}"/usr/share/doc/gcc-libs
}

package_gcc() {
	depends=('glibc' 'gcc-libs' 'binutils' 'libmpc' 'attr')
    install=gcc.install

	mv "${srcdir}"/out/* "${pkgdir}"/

	# Compatibility links
	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/c89
	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/c99
	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/cc
	ln -s /usr/bin/gfortran "${pkgdir}"/usr/bin/f95
}
