pkgbase=gcc
pkgname=(gcc-libs gcc)
pkgver=4.9.2
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=("x86_64")
url="http://gcc.gnu.org"
license=(GPLv3)

options=('strip')
makedepends=('zlib')

source=(http://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
		gcc-no_fixincludes.diff.gz)

sha256sums=(2020c98295856aa13fda0f2f3a4794490757fc24bcca918d52cc8b4917b972dd
			cdf880bd4fc1a35f5f3c6ec7d2eb1ce7ab55bbf60aa64db495cb07071a7af40b)

_target_dir=usr/lib/${MULTIARCH}/gcc/${CHOST}/${pkgver}

prepare() {
	cd "${srcdir}"/gcc-${pkgver}

	# No fixincludes
	zcat "${srcdir}"/gcc-no_fixincludes.diff.gz | patch -p1

	# Fix libdir
	sed -i "/m64=/s/lib64/${MULTIARCH}/" gcc/config/i386/t-linux64
	
	mkdir -p "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	# GCC doesn't work well with our hardening options.
	export CPPFLAGS=""
	export CCFLAGS="-O2"
	export CXXFLAGS="-O2"

	../gcc-${pkgver}/configure --prefix=/usr \
				--libexecdir=/usr/lib/${MULTIARCH} \
				--libdir=/usr/lib/${MULTIARCH} \
				--enable-shared \
				--enable-bootstrap \
				--enable-languages=c,c++,objc,go,fortran,lto \
				--enable-threads=posix \
				--enable-checking=release \
				--enable-objc-gc \
				--with-system-zlib \
				--disable-libunwind-exceptions \
				--enable-__cxa_atexit \
				--enable-libssp \
				--enable-vtable-verify \
				--enable-lto \
				--with-gnu-ld \
				--verbose \
				--disable-gtktest \
				--disable-multilib \
				--enable-multiarch \
				--target=${CHOST} \
				--build=${CHOST} \
				--host=${CHOST}
	make
}

# Packaging functions
package_gcc-libs() {
	pkgdesc="${pkgdesc} - Runtime libraries"
	depends=('glibc')
	
	# Set up our initial install tree
	cd "${srcdir}/build"
	rm -rf ../out
	mkdir ../out
	make install DESTDIR="${srcdir}"/out/

	# KLUDGE ALERT
	# Avoid ldconfig error
	rm -f "${srcdir}"/out/usr/lib/${MULTIARCH}/*gdb.py
	
	# Move runtime libraries
	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}
	mv "${srcdir}"/out/usr/lib/${MULTIARCH}/lib{asan,atomic,gcc_s,gfortran,gomp,itm,objc,quadmath,stdc++,tsan}.so* "${pkgdir}"/usr/lib/${MULTIARCH}

	# Info files
	mkdir -p "${pkgdir}"/usr/share/info
	mv "${srcdir}"/out/usr/share/info/lib{gomp,itm,quadmath}.info* "${pkgdir}"/usr/share/info/

	# Locale	
	mkdir -p "${pkgdir}"/usr/share/locale/de/LC_MESSAGES/
	mkdir -p "${pkgdir}"/usr/share/locale/fr/LC_MESSAGES/
	mv "${srcdir}"/out/usr/share/locale/de/LC_MESSAGES/libstdc++.mo "${pkgdir}"/usr/share/locale/de/LC_MESSAGES/
	mv "${srcdir}"/out/usr/share/locale/fr/LC_MESSAGES/libstdc++.mo "${pkgdir}"/usr/share/locale/fr/LC_MESSAGES/

	# License
	mkdir -p "${pkgdir}"/usr/share/doc/gcc-libs
	install -m644 "${srcdir}"/gcc-${pkgver}/COPYING.RUNTIME "${pkgdir}"/usr/share/doc/gcc-libs
}

package_gcc() {
	depends=('glibc' 'gcc-libs' 'binutils' 'libmpc')

	mv "${srcdir}"/out/* "${pkgdir}"/

	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/c89
	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/c99
	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/cc
	ln -s /usr/bin/gfortran "${pkgdir}"/usr/bin/f95
}
