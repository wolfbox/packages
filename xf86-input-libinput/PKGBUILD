# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=xf86-input-libinput
pkgver=0.8.0
pkgrel=1
pkgdesc="X.org input driver using libinput"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('MIT')

makedepends=('xorg-server-devel' 'resourceproto' 'scrnsaverproto')
depends=('glibc' 'mtdev' 'libinput>=0.11.0')
conflicts=('xorg-server<1.16.0' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22')

source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('2152b0984feffe8151934ad9af29bde7fc621e0a54b0cdedbaf1abc367dea43a'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
