pkgname=openssl0
pkgver=0.9.8zd
pkgrel=1
pkgdesc="A legacy cryptography toolkit - compatibility libraries"
arch=("x86_64")
url="http://www.openssl.org"
license=("custom")
options=(!makeflags)

depends=('glibc')
provides=("openssl=${pkgver}")

source=(http://www.openssl.org/source/openssl-${pkgver}.tar.gz{,.asc}
		openssl.soname.diff.gz)
sha256sums=('59266dcfb0be0fbe6181edead044ac3edaf83bc58991f264dcf532b01d531ee3'
            'SKIP'
            '922d68e43c06ccd56bd430a81d7ad752f620b5fb1b37e857d9a3662273c11d46')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491') # Matt Caswell <matt@openssl.org>

prepare() {
	cd "${srcdir}/openssl-${pkgver}"
	
	# Lower the internal OpenSSL version number to improve application
	# compatibility. This is very naughty, but it's what Slackware does...
	sed -i "s/#define OPENSSL_VERSION_NUMBER.*/\/* Use 0x009080efL (0.9.8o) below to avoid pointlessly breaking the ABI *\/\n#define OPENSSL_VERSION_NUMBER 0x009080efL/g" crypto/opensslv.h 
	
	# Use .so.1, not .so.1.0.0:
	zcat "${srcdir}"/openssl.soname.diff.gz | patch -p1 --backup --verbose --suffix=.orig
}

build() {
	cd "${srcdir}/openssl-${pkgver}"
	
	# Ensure ls field counts are right for parsing
	export LC_ALL=C
	
	# Disable RC5 due to patent expiring in 2015-03-03 
	./config --prefix=/usr --openssldir=/etc/ssl --libdir=lib/${MULTIARCH} \
	         no-rc5 shared
	make depend
	make
}

package() {
	cd "${srcdir}/openssl-${pkgver}"

	# Copy .so's
	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}
	install -m755 *.so.* "${pkgdir}"/usr/lib/${MULTIARCH}/
	
	# Create symlinks
	( cd "${pkgdir}"/usr/lib/${MULTIARCH} ; ldconfig -l lib*.so.* )	

	# Install copyright notice
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}/
	cp -a LICENSE "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}/
}
