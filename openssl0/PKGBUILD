pkgname=openssl0
pkgver=0.9.8zh
pkgrel=1
pkgdesc="A legacy cryptography toolkit - compatibility libraries"
arch=("x86_64")
url="http://www.openssl.org"
license=("custom")
options=(!makeflags)

depends=('glibc')
provides=("openssl=${pkgver}")

source=(http://www.openssl.org/source/openssl-${pkgver}.tar.gz{,.asc}
		openssl.soname.diff)
sha256sums=('f1d9f3ed1b85a82ecf80d0e2d389e1fda3fca9a4dba0bf07adbf231e1a5e2fd6'
            'SKIP'
            'd75bcbb9566d0f7d5f9c8b5e06a44c676b79089b90f9ba324fc046978209a9f4')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491') # Matt Caswell <matt@openssl.org>

prepare() {
	cd openssl-${pkgver}
	
	# Lower the internal OpenSSL version number to improve application
	# compatibility. This is very naughty, but it's what Slackware does...
	sed -i "s/#define OPENSSL_VERSION_NUMBER.*/\/* Use 0x009080efL (0.9.8o) below to avoid pointlessly breaking the ABI *\/\n#define OPENSSL_VERSION_NUMBER 0x009080efL/g" crypto/opensslv.h 
	
	# Use .so.1, not .so.1.0.0:
	patch -p1 -i ../openssl.soname.diff --backup --verbose --suffix=.orig
}

build() {
	cd openssl-${pkgver}
	
	# Ensure ls field counts are right for parsing
	export LC_ALL=C
	
	# Disable RC5 due to patent expiring in 2015-03-03 
	./config --prefix=/usr --openssldir=/etc/ssl --libdir=lib/${MULTIARCH} \
	         no-rc5 shared
	make depend
	make
}

package() {
	cd openssl-${pkgver}

	# Copy .so's
	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}
	install -m755 *.so.* "${pkgdir}"/usr/lib/${MULTIARCH}/
	
	# Create symlinks
	( cd "${pkgdir}"/usr/lib/${MULTIARCH} ; ldconfig -l lib*.so.* )	

	# Install copyright notice
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}/
	cp -a LICENSE "${pkgdir}"/usr/share/doc/${pkgname}/
}
