# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=('qt5' 'qt5-libs' 'qt5-tools')
pkgver=5.4.0
pkgrel=1
pkgdesc='A cross-platform application and UI framework'
arch=('x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL' 'FDL' 'custom')
options=('!makeflags' 'staticlibs' '!strip') #libQt5PlatformSupport builds static only

makedepends=('python2' 'perl' 'ruby')

_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/5.4/${pkgver}/single/${_pkgfqn}.tar.xz")
sha256sums=('b2d9df47b9fd2fcfa5ebd162e1373b7eddf4759e6ba148c4589f3d8b6628c39c')

prepare() {
  mkdir -p path
  ln -sf /usr/bin/python2 path/python

  cd ${_pkgfqn}

  sed -i "s|-O2|${CXXFLAGS}|" qtbase/mkspecs/common/{g++,gcc}-base.conf
  sed -i "/^QMAKE_LFLAGS_RPATH/s| -Wl,-rpath,||g" qtbase/mkspecs/common/gcc-base-unix.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= ${LDFLAGS}|g" qtbase/mkspecs/common/gcc-base.conf

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py')
}

build() {
  cd ${_pkgfqn}

  export QTDIR="${srcdir}"/${_pkgfqn}
  export LD_LIBRARY_PATH="${QTDIR}"/qtbase/lib:"${QTDIR}"/qttools/lib:"${LD_LIBRARY_PATH}"
  export QT_PLUGIN_PATH="${QTDIR}"/qtbase/plugins

  export PATH="${srcdir}/path:${PATH}"
  export PYTHON=python2

  ./configure -confirm-license -opensource \
    -prefix /usr \
    -bindir /usr/lib/qt/bin \
    -libdir /usr/lib/${MULTIARCH} \
    -docdir /usr/share/doc/qt \
    -headerdir /usr/include/qt \
    -archdatadir /usr/lib/${MULTIARCH}/qt \
    -datadir /usr/share/qt \
    -sysconfdir /etc/xdg \
    -examplesdir /usr/share/doc/qt/examples \
    -system-sqlite \
    -openssl-linked \
    -nomake examples \
    -no-rpath \
    -no-journald \
    -optimized-qmake \
    -dbus-linked \
    -system-harfbuzz \
    -reduce-relocations \
    -no-pch

  make

  # Fix paths
  find "${QTDIR}" -name Makefile -exec sed -i "s|/usr/lib/qt/bin/qdoc|${QTDIR}/qtbase/bin/qdoc|g" {} +
  find "${QTDIR}" -name Makefile.qmake-docs -exec sed -i "s|/usr/lib/qt/bin/qdoc|${QTDIR}/qtbase/bin/qdoc|g" {} +
  find "${QTDIR}" -name Makefile -exec sed -i "s|/usr/lib/qt/bin/qhelpgenerator|${QTDIR}/qttools/bin/qhelpgenerator|g" {} +
  find "${QTDIR}" -name Makefile.qmake-docs -exec sed -i "s|/usr/lib/qt/bin/qhelpgenerator|${QTDIR}/qttools/bin/qhelpgenerator|g" {} +
  sed -i "s|/usr/lib/qt/bin/qhelpgenerator|${QTDIR}/qttools/bin/qhelpgenerator|g" qtwebkit/Source/Makefile.api
}

package_qt5() {
  depends=('qt5-libs')
  provides=("qt=${pkgver}")

  (
    msg "Installing qtbase..."
    cd ${_pkgfqn}/qtbase
    make INSTALL_ROOT="${pkgdir}" install

    install -D -m644 LGPL_EXCEPTION.txt \
      "${pkgdir}"/usr/share/doc/${pkgname}/LGPL_EXCEPTION.txt

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

    # Fix wrong qmake path in pri file
    #sed -i "s|${srcdir}/${_pkgfqn}/qtbase|/usr|" \
    #  "${pkgdir}"/usr/lib/qt/mkspecs/modules/qt_lib_bootstrap_private.pri
  )

  (
    msg "Installing qtconnectivity..."
    cd ${_pkgfqn}/qtconnectivity
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtdeclarative..."
    cd ${_pkgfqn}/qtdeclarative
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

    # Useful symlinks
    #for b in "${pkgdir}"/usr/lib/qt/bin/*; do
    #  ln -s /usr/lib/qt/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
    #done

    install -D -m644 LGPL_EXCEPTION.txt \
      "${pkgdir}"/usr/share/doc/${pkgname}/LGPL_EXCEPTION.txt
  )

  (
    msg "Installing qtxmlpatterns..."
    cd ${_pkgfqn}/qtxmlpatterns
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

    # Useful symlinks
    #for b in "${pkgdir}"/usr/lib/qt/bin/*; do
    #  ln -s /usr/lib/qt/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
    #done
  )

  (
    msg "Installing qtenginio..."
    cd ${_pkgfqn}/qtenginio
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtgraphicaleffects..."
    cd ${_pkgfqn}/qtgraphicaleffects
    make INSTALL_ROOT="${pkgdir}" install
  )

  (
    msg "Installing qtimageformats..."
    cd ${_pkgfqn}/qtimageformats
    make INSTALL_ROOT="${pkgdir}" install
  )

  (
    msg "Installing qtlocation..."
    cd ${_pkgfqn}/qtlocation
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtquick1..."
    cd ${_pkgfqn}/qtquick1
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

    # Useful symlinks
    #for b in "${pkgdir}"/usr/lib/qt/bin/*; do
    #  ln -s /usr/lib/qt/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
    #done
  )

  (
    msg "Installing qtquickcontrols..."
    cd ${_pkgfqn}/qtquickcontrols
    make INSTALL_ROOT="${pkgdir}" install
  )

  (
    msg "Installing qtscript..."
    cd ${_pkgfqn}/qtscript
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtsensors..."
    cd ${_pkgfqn}/qtsensors
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtserialport..."
    cd ${_pkgfqn}/qtserialport
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtsvg..."
    cd ${_pkgfqn}/qtsvg
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtwebsockets..."
    cd ${_pkgfqn}/qtwebsockets
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtx11extras..."
    cd ${_pkgfqn}/qtx11extras
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )
  
  (
    msg "Installing qttranslations..."
    cd ${_pkgfqn}/qttranslations
    make INSTALL_ROOT="${pkgdir}" install
  )
  
  (
    msg "Installing qtwebkit..."
    cd ${_pkgfqn}/qtwebkit
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  (
    msg "Installing qtmultimedia..."
    cd ${_pkgfqn}/qtmultimedia
    make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;
  )

  # Useful symlinks
  install -d "${pkgdir}"/usr/bin
  for b in "${pkgdir}"/usr/lib/qt/bin/*; do
    ln -s /usr/lib/qt/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done
  
  # Move runtime libraries into qt5-libs
  mv "${pkgdir}"/usr/lib/${MULTIARCH}/lib*.so* "${srcdir}"
  mv "${pkgdir}"/usr/lib/${MULTIARCH}/qt "${srcdir}"
}

package_qt5-libs() {
  pkgdesc="${pkgdesc} - Runtime libraries"
  depends=('glibc' 'jasper' 'libpng' 'libmng' 'libjpeg-turbo' 'libtiff' 'harfbuzz'
           'freetype2' 'libgl' 'sqlite3' 'zlib' 'pcre' 'icu' 'glib2' 'openssl' 'dbus'
           'udev' 'libx11' 'libxcb' 'fontconfig' 'libxi' 'libsm' 'libice'
           'libxrender')
  mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}
  mv "${srcdir}"/lib*.so* "${pkgdir}"/usr/lib/${MULTIARCH}
  mv "${srcdir}"/qt "${pkgdir}"/usr/lib/${MULTIARCH}
}

package_qt5-tools() {
  pkgdesc="${pkgdesc} - Development utilities"
  depends=('qt5')
  
  cd ${_pkgfqn}/qttools
  make INSTALL_ROOT="${pkgdir}" install

    # Fix wrong path in prl files
    find "${pkgdir}/usr/lib" -type f -name '*.prl' \
      -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

    # Useful symlinks
    install -d "${pkgdir}"/usr/bin
    for b in "${pkgdir}"/usr/lib/qt/bin/*; do
      ln -s /usr/lib/qt/bin/$(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
    done
}
