pkgname=libarchive
pkgver=3.1.2
pkgrel=1
pkgdesc="An archive processing library."
arch=("x86_64")
url="http://www.libarchive.org"
license=("BSD")

depends=('glibc' 'acl' 'attr' 'bzip2' 'expat' 'lzo' 'openssl' 'xz' 'zlib' 'nettle')
makedepends=('autoconf' 'automake' 'bash')

source=(git+https://github.com/wolfbox/libarchive.git#commit=658b4bf)
sha256sums=(SKIP)

prepare() {
	cd "${srcdir}/${pkgname}"
	./build/autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
			    --localstatedir=/var --mandir=/usr/man --disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install
	
	# Make bsdtar the default tar
	ln "${pkgdir}"/usr/bin/bsdtar "${pkgdir}"/usr/bin/tar
	ln -s /usr/man/man1/bsdtar.1.gz "${pkgdir}"/usr/man/man1/tar.1.gz

	# Make bsdcpio the default cpio
	ln "${pkgdir}"/usr/bin/bsdcpio "${pkgdir}"/usr/bin/cpio
	ln -s /usr/man/man1/bsdcpio.1.gz "${pkgdir}"/usr/man/man1/cpio.1.gz
}
