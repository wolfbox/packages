pkgname=perl-html-tagset
_realname=HTML-Tagset
pkgver=3.20
pkgrel=1
pkgdesc="Data tables useful in parsing HTML"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
options=(!emptydirs)

depends=('perl')

source=(http://www.cpan.org/authors/id/P/PE/PETDANCE/${_realname}-${pkgver}.tar.gz)
sha256sums=('adb17dac9e36cd011f5243881c9739417fd102fce760f8de4e9be4c7131108e2')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}
