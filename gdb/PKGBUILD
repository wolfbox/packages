pkgname=gdb
pkgver=7.8.2
pkgrel=1
pkgdesc="The GNU program debugger"
arch=("x86_64")
url="http://www.gnu.org/software/gdb/"
license=('GPL2')

depends=('glibc' 'python' 'zlib' 'xz' 'expat' 'ncurses')

source=(http://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.xz{,.sig})
sha256sums=('605954d5747d5f08ea4b7f48e958d1ebbf39265e18f7f36738deeabb83744485'
            'SKIP')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker <brobecker@adacore.com>

build() {
	cd ${pkgname}-${pkgver}
	
	# GDB doesn't like hardening
	export CPPFLAGS=""
	export CFLAGS=${CFLAGS/-D_FORTIFY_SOURCE=2/}
	
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
	            --with-python --build=${CHOST}
	make
}

package() {
	cd ${pkgname}-${pkgver}

	make install DESTDIR="${pkgdir}"

	rm -f "${pkgdir}"/usr/lib/${MULTIARCH}/{libbfd*,libiberty*,libopcodes*}
	rm -f "${pkgdir}"/usr/share/info/{annotate*,bfd*,configure*,standards*}
	rm -rf "${pkgdir}"/usr/include

	# Remove duplicate locale files
	find "${pkgdir}"/usr/share/locale -name "bfd.mo" -exec rm {} \;
	find "${pkgdir}"/usr/share/locale -name "opcodes.mo" -exec rm {} \;
}
