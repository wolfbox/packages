pkgname=polkit
pkgver=0.112
pkgrel=1
pkgdesc="Authentication policy framework."
arch=("x86_64")
url="http://www.freedesktop.org/wiki/software/polkit"
license=('LGPL2')
install=polkit.install

makedepends=('gobject-introspection')
depends=('gcc-libs' 'glibc' 'glib2' 'libffi' 'zlib' 'expat' 'js17')

source=(http://www.freedesktop.org/software/polkit/releases/polkit-${pkgver}.tar.gz{,.sign})
sha256sums=('d695f43cba4748a822fbe864dd32c4887c5da1c71694a47693ace5e88fcf6af6'
            'SKIP')
validpgpkeys=('C1976D9ED91A7459CBCE53145A33F660B38479DF') # Miloslav Trmac <mitr@volny.cz>

build() {
	cd ${pkgname}-${pkgver}

	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --libexecdir=/usr/lib/${MULTIARCH} \
	            --sysconfdir=/etc \
	            --localstatedir=/var \
	            --with-authfw=shadow \
	            --with-mozjs=mozjs-17.0 \
	            --enable-verbose-mode \
	            --disable-static \
	            --enable-introspection \
	            --enable-man-pages \
	            --disable-gtk-doc
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make check
}

package() {
	( cd ${pkgname}-${pkgver}
	  make DESTDIR="${pkgdir}" install )
}
