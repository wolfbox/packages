pkgname=e2fsprogs
pkgver=1.42.12
pkgrel=1
pkgdesc="Tools for the Linux Ext2/3/4 filesystems"
arch=('x86_64')
url="http://e2fsprogs.sourceforge.net"
license=('GPL2')
backup=('etc/mke2fs.conf')

depends=('glibc' 'util-linux')

source=(http://kernel.org/pub/linux/kernel/people/tytso/e2fsprogs/v${pkgver}/e2fsprogs-${pkgver}.tar.gz)
sha256sums=(8c790e8708dbb7c766c88c8152c2bf2d0628937e9dd6571eef8f59080d0fb58a)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
	            --sysconfdir=/etc \
	            --bindir=/usr/bin \
	            --sbindir=/usr/bin \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --mandir=/usr/share/man \
	            --infodir=/usr/share/info \
	            --disable-libuuid \
	            --disable-uuidd \
	            --disable-libblkid \
	            --disable-fsck
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
}
