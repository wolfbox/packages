pkgname=nginx
pkgver=1.6.2
pkgrel=1
pkgdesc="A fast and lightweight HTTP server."
arch=("x86_64")
url="http://nginx.org"
license=('custom')

install=nginx.install
depends=('glibc' 'zlib' 'pcre' 'openssl')

source=(http://nginx.org/download/nginx-${pkgver}.tar.gz
        nginx
        nginx.networking)
sha256sums=('b5608c2959d3e7ad09b20fc8f9e5bd4bc87b3bc8ba5936a513c04ed8f1391a18'
            '14563806d392ad5438a0f1c5846164d4f40f49290a9dcfadc872011fd3848dbf'
            '03cef496256c67379e285e2a857a98456432eae66a9d40a9cf0b5512614cfb0f')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/etc/nginx \
	            --conf-path=/etc/nginx/nginx.conf \
	            --sbin-path=/usr/bin/nginx \
	            --pid-path=/var/run/nginx.pid \
	            --lock-path=/var/lock/nginx.lock \
	            --http-log-path=/var/log/nginx/access.log \
	            --error-log-path=stderr \
	            --http-client-body-temp-path=/var/lib/nginx/client-body \
	            --http-proxy-temp-path=/var/lib/nginx/proxy \
	            --http-fastcgi-temp-path=/var/lib/nginx/fastcgi \
	            --http-scgi-temp-path=/var/lib/nginx/scgi \
	            --http-uwsgi-temp-path=/var/lib/nginx/uwsgi \
	            --with-imap \
	            --with-imap_ssl_module \
	            --with-ipv6 \
	            --with-pcre-jit \
	            --with-http_dav_module \
	            --with-http_ssl_module \
	            --with-file-aio \
	            --with-http_spdy_module \
	            --with-http_gzip_static_module \
	            --with-http_gunzip_module \
	            --user=nginx \
	            --group=http \
	            --with-ld-opt="${LDFLAGS}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"

	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m755 "${srcdir}"/nginx "${pkgdir}"/etc/rc.d
	install -m755 "${srcdir}"/nginx.networking "${pkgdir}"/etc/rc.d/events

	mkdir -p "${pkgdir}"/usr/man/man8
	install -m644 man/nginx.8 "${pkgdir}"/usr/man/man8/

	mkdir -p "${pkgdir}"/var/lib/nginx
}
