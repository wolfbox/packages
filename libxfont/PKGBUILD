# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libxfont
pkgver=1.5.1
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')

makedepends=('xorg-util-macros' 'xtrans')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto>=2.1.3')

source=(${url}/archive/individual/lib/libXfont-${pkgver}.tar.bz2)
sha256sums=('b70898527c73f9758f551bbab612af611b8a0962202829568d94f3edf4d86098')

build() {
  cd "${srcdir}/libXfont-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
              --libdir=/usr/lib/${MULTIARCH}
  make
}

package() {
  cd "${srcdir}/libXfont-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
