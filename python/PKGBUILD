pkgname=python
pkgver=3.4.3
pkgrel=1
pkgdesc="The Python programming langauge"
arch=("x86_64")
url="https://www.python.org"
license=(custom)

depends=('glibc' 'bzip2' 'zlib')
optdepends=('ncurses' 'readline' 'gdbm' 'db48' 'xz' 'openssl' 'sqlite3' 'tk')

source=(https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz
        python-libdir.diff.xz)
sha256sums=('b5b3963533768d5fc325a4d7a6bd6f666726002d696f1d399ec06b043ea996b8'
            'c3dd857f9a28cea07eb15261ca8bd11088e11e73b995f0b60e6a9b1a1a8305fb')

prepare() {
	cd "${srcdir}"/Python-${pkgver}

	# Fix libdir
	cat ../python-libdir.diff | patch -p1
	sed -i "s:@MULTIARCH@:${MULTIARCH}:" Modules/getpath.c
}

build() {
	cd "${srcdir}/Python-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man --libdir=/usr/lib/${MULTIARCH} \
	            --with-threads --enable-ipv6 --enable-shared
	make
}

package() {
	cd "${srcdir}/Python-${pkgver}"

	make install DESTDIR="${pkgdir}"
	ln -s /usr/bin/python3 "${pkgdir}"/usr/bin/python
	ln -s /usr/bin/python3-config "${pkgdir}"/usr/bin/python-config
	
	mkdir -p "${pkgdir}"/usr/share/doc/python
	install -m644 LICENSE "${pkgdir}"/usr/share/doc/python
}
