pkgname=docbook-xsl
pkgver=1.78.1
pkgrel=1
pkgdesc="A set of XML stylesheets for Docbook-xml transformations"
arch=('any')
url="http://docbook.sourceforge.net"
license=('custom')
install=docbook-xsl.install

depends=('libxml2' 'docbook-xml')

source=(http://downloads.sourceforge.net/project/docbook/docbook-xsl/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=(c98f7296ab5c8ccd2e0bc07634976a37f50847df2d8a59bdb1e157664700b467)

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	_root="${pkgdir}"/usr/share/xml/docbook/xsl-stylesheets-${pkgver}
	mkdir -p "${_root}"

	install -m644 VERSION VERSION.xsl "${_root}"
	for dir in assembly common eclipse epub epub3 fo highlighting html htmlhelp \
	           javahelp lib manpages params profiling roundtrip template website \
	           xhtml xhtml-1_1 xhtml5; do
		install -dm655 "${_root}"/${dir}
		install -m644 ${dir}/*.{xml,xsl,dtd,ent} "${_root}"/${dir}/ || true
	done

	mkdir -p "${pkgdir}"/etc/xml
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}/
	install -m644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/
}
