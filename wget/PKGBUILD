pkgname=wget
pkgver=1.16.1
pkgrel=1
pkgdesc="A tool for retrieving files over the network."
arch=('x86_64')
url="http://www.gnu.org/software/wget"
license=('GPLv3')
backup=('etc/wgetrc')

depends=('glibc' 'openssl' 'zlib' 'pcre' 'util-linux')

source=(http://ftpmirror.gnu.org/wget/wget-${pkgver}.tar.xz{,.sig})
sha256sums=('2b96c707ee15fe847f5bc2f872a7a530dca803b927a750419b44f50803e14d33'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Harden
	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --sysconfdir=/etc \
	            --mandir=/usr/man \
	            --infodir=/usr/info \
	            --with-ssl=openssl
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
