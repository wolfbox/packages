pkgname=wget
pkgver=1.16.3
pkgrel=1
pkgdesc="A tool for retrieving files over the network"
arch=('x86_64')
url="http://www.gnu.org/software/wget"
license=('GPL3')
backup=('etc/wgetrc')

depends=('glibc' 'openssl' 'zlib' 'pcre' 'util-linux')

source=(http://ftpmirror.gnu.org/wget/wget-${pkgver}.tar.xz{,.sig})
sha256sums=('67f7b7b0f5c14db633e3b18f53172786c001e153d545cfc85d82759c5c2ffb37'
            'SKIP')
validpgpkeys=('AC404C1C0BF735C63FF4D562263D6DF2E163E1EA') # Giuseppe Scrivano <gscrivan@redhat.com>

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Harden
	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --sysconfdir=/etc \
	            --mandir=/usr/share/man \
	            --infodir=/usr/share/info \
	            --with-ssl=openssl
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
