pkgname=readline
_patchlevel=8
_basever=6.3
pkgver=${_basever}.${_patchlevel}
pkgrel=1
pkgdesc="A line input library with editing features"
arch=("x86_64")
url="http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html"
license=('GPL3')

depends=('glibc' 'ncurses')

MIRROR="http://mirrors.ibiblio.org/gnu/ftp/gnu/readline"
READLINE_PATCHES="${MIRROR}/readline-6.3-patches"
source=("${MIRROR}/readline-${_basever}.tar.gz"
		"${READLINE_PATCHES}/readline63-001"
		"${READLINE_PATCHES}/readline63-002"
		"${READLINE_PATCHES}/readline63-003"
		"${READLINE_PATCHES}/readline63-004"
		"${READLINE_PATCHES}/readline63-005"
		"${READLINE_PATCHES}/readline63-006"
		"${READLINE_PATCHES}/readline63-007"
		"${READLINE_PATCHES}/readline63-008")
sha256sums=('56ba6071b9462f980c5a72ab0023893b65ba6debb4eeb475d7a563dc65cafd43'
            '1a79bbb6eaee750e0d6f7f3d059b30a45fc54e8e388a8e05e9c3ae598590146f'
            '39e304c7a526888f9e112e733848215736fb7b9d540729b9e31f3347b7a1e0a5'
            'ec41bdd8b00fd884e847708513df41d51b1243cecb680189e31b7173d01ca52f'
            '4547b906fb2570866c21887807de5dee19838a60a1afb66385b272155e4355cc'
            '877788f9228d1a9907a4bcfe3d6dd0439c08d728949458b41208d9bf9060274b'
            '5c237ab3c6c97c23cf52b2a118adc265b7fb411b57c93a5f7c221d50fafbe556'
            '4d79b5a2adec3c2e8114cbd3d63c1771f7c6cf64035368624903d257014f5bea'
            '3bc093cf526ceac23eb80256b0ec87fa1735540d659742107b6284d635c43787')

prepare() {
	cd "${srcdir}/${pkgname}-${_basever}"

	PATCHLEVEL=$( ls -1 "${srcdir}"/readline63-??? | wc -l )
	if [ ${PATCHLEVEL} != ${_patchlevel} ] ; then
		echo "Patch level is incorrect! Should be ${PATCHLEVEL}."
		exit 1
	fi

	for file in "${srcdir}"/readline63-??? ; do
		patch -p0 -i "${file}"
	done
}

build() {
	cd "${srcdir}/${pkgname}-${_basever}"

	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
				--with-curses --enable-multibyte
	make SHLIB_LIBS=-lncurses
}

package() {
	cd "${srcdir}/${pkgname}-${_basever}"
	make install DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}/usr/share/doc/readline"
	cp -a COPYING* "${pkgdir}/usr/share/doc/readline"
}
