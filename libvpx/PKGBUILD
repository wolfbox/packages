pkgname=libvpx
pkgver=1.3.0
pkgrel=1
pkgdesc='The VP8 Codec SDK'
arch=('x86_64')
url='http://www.webmproject.org/'
license=('BSD')

depends=('glibc')
makedepends=('yasm' 'git')

source=(https://webm.googlecode.com/files/libvpx-v${pkgver}.tar.bz2)
sha256sums=('d3dcc8d84af51c6c382b214397c62402e37a799e8ebcda6f4217aef0010451a9')

build() {
    cd $pkgname-v${pkgver}
    ./configure --prefix=/usr \
        --libdir=/usr/lib/${MULTIARCH} \
        --enable-vp8 \
        --enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd $pkgname-v${pkgver}
    make DIST_DIR="$pkgdir/usr" install
    install -Dm0644 LICENSE "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
