# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xorg-fonts-100dpi
pkgver=1.0.1
pkgrel=1
pkgdesc="X.org 100dpi fonts"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
groups=('xorg')
install=xfonts.install

source=(${url}/releases/individual/font/font-adobe-100dpi-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-adobe-utopia-100dpi-1.0.2.tar.bz2
        ${url}/releases/individual/font/font-bh-100dpi-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-bh-lucidatypewriter-100dpi-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-bitstream-100dpi-1.0.1.tar.bz2)
sha256sums=('a742bca8d6f8fb76b511f11c2ad2a1d326687fe9f0490a9cf64524c3782cb52c'
            'dd8c3f5509589f9232b26273fecde2694bd2bb069c6aaa8f504ad3d24775955e'
            'c46d68487b09993266f34ac87a47e035af8a3f42c38f33d26ff18ac0c31c3397'
            '8156f7c4e5a3fbd478c287154c7e3c55c6631c148a7b185f604753d118018da9'
            '3e05c9b74d0dc00e6e46ec758888e66514ab24fc75930877d4ba6db7d49f0347')

build() {
  cd "${srcdir}"
  for dir in font-*-100dpi*; do
    if [ -d "${dir}" ]; then
      pushd ${dir}
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/100dpi
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  for dir in font-*-100dpi*; do
    if [ -d "${dir}" ]; then
      pushd ${dir}
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.${dir%-100dpi-*}"
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/100dpi/fonts.*
}
