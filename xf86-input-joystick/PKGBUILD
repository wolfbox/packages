# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xf86-input-joystick
pkgver=1.6.2
pkgrel=1
pkgdesc="X.Org Joystick input driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')

makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=21' 'resourceproto' 'scrnsaverproto')
depends=('glibc')
conflicts=('xorg-server<1.16' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22')

source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        50-joystick.conf)
sha256sums=('ee3b86ed72f3b5befdeed39909320f4b6970401cfbcc2cc0dfd8fb1c1576150d'
            'd6ddaec1767e1a73f8eacc6e58b0fd1d1ca5e73bbf58b6a106ebf92589b87483')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/X11/xorg.conf.d"
  install -m644 "${srcdir}/50-joystick.conf" "${pkgdir}/usr/share/X11/xorg.conf.d/"

  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
