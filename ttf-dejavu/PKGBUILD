# Derived from Arch Linux

pkgname=ttf-dejavu
pkgver=2.34
pkgrel=1
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters"
arch=('any')
url="http://dejavu-fonts.org/wiki/Main_Page"
license=('custom')
install=ttf-dejavu.install

depends=('fontconfig')
provides=('ttf-font')

source=(http://downloads.sourceforge.net/project/dejavu/dejavu/${pkgver}/dejavu-fonts-ttf-${pkgver}.tar.bz2)
sha256sums=('d0a72c3ae99f5b8d7470daf091ec43f78dc942795aed5c996ab1aa678702eb5d')

package() {
    install -dm755 "${pkgdir}"/etc/fonts/conf.avail
    install -dm755 "${pkgdir}"/usr/share/fonts/TTF

    cd "${srcdir}"/dejavu-fonts-ttf-${pkgver}
    install -m644 ttf/*.ttf "${pkgdir}"/usr/share/fonts/TTF/
    install -m644 fontconfig/*.conf "${pkgdir}"/etc/fonts/conf.avail/

    install -Dm644 LICENSE "${pkgdir}"/usr/share/doc/${pkgname}/LICENSE
}
