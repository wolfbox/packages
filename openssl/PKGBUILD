pkgname=openssl
_ver=1.0.2e
pkgver=1.0.2.e
pkgrel=1
pkgdesc="A legacy cryptography toolkit"
arch=("x86_64")
url="http://www.openssl.org"
license=("custom")
options=(!makeflags)

depends=('glibc' 'zlib')
provides=("openssl=${pkgver}")
backup=('etc/ssl/openssl.cnf')

source=(http://www.openssl.org/source/openssl-${_ver}.tar.gz{,.asc}
		openssl.soname.diff)
sha256sums=('e23ccafdb75cfcde782da0151731aa2185195ac745eea3846133f2e05c0e0bff'
            'SKIP'
            'a8c07358e2f48a9d0db9668aaf8ea88ee9abd979b3fd12dc278b9c0523b242d7')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491') # Matt Caswell <matt@openssl.org>

prepare() {
	cd openssl-${_ver}
	
	# Lower the internal OpenSSL version number to improve application
	# compatibility. This is very naughty, but it's what Slackware does...
	sed -i "s/#define OPENSSL_VERSION_NUMBER.*/\/* Use 0x1000103fL (1.0.1c) below to avoid pointlessly breaking the ABI *\/\n#define OPENSSL_VERSION_NUMBER 0x1000103fL/g" crypto/opensslv.h 
	
	# Use .so.1, not .so.1.0.0:
	patch -p1 -i ../openssl.soname.diff --backup --verbose --suffix=.orig
}

build() {
	cd openssl-${_ver}
	
	# Ensure ls field counts are right for parsing
	export LC_ALL=C
	
	# Disable RC5 due to patent expiring in 2015-03-03 
	./config --prefix=/usr --openssldir=/etc/ssl --libdir=lib/${MULTIARCH} \
	         no-rc5 shared zlib "${CPPFLAGS} ${CFLAGS} ${LDFLAGS} -Wa,--noexecstack"

	make depend
	make
}

package() {
	cd openssl-${_ver}
	make install INSTALL_PREFIX="${pkgdir}" MANDIR=/usr/share/man MANSUFFIX=ssl

	# Create symlinks
	( cd "${pkgdir}"/usr/lib/${MULTIARCH} ; ldconfig -l lib*.so.* )	
	
	# Install copyright notice
	mkdir -p "${pkgdir}"/usr/share/doc/openssl/
	cp -a LICENSE doc "${pkgdir}"/usr/share/doc/openssl/
}
