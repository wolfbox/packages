pkgname=openssl
pkgver=1.0.2
pkgrel=1
pkgdesc="A legacy cryptography toolkit"
arch=("x86_64")
url="http://www.openssl.org"
license=("custom")
options=(!makeflags)

depends=('glibc' 'zlib')
provides=("openssl=${pkgver}")
backup=('etc/ssl/openssl.cnf')

source=(http://www.openssl.org/source/openssl-${pkgver}.tar.gz{,.asc}
		openssl.soname.diff.gz)
sha256sums=('8c48baf3babe0d505d16cfc0cf272589c66d3624264098213db0fb00034728e9'
            'SKIP'
            '866b84408c72d4362f39b10a91bfe2582b2db5d21afc007cb2d53a5fccd9cf76')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491') # Matt Caswell <matt@openssl.org>

prepare() {
	cd openssl-${pkgver}
	
	# Lower the internal OpenSSL version number to improve application
	# compatibility. This is very naughty, but it's what Slackware does...
	sed -i "s/#define OPENSSL_VERSION_NUMBER.*/\/* Use 0x1000103fL (1.0.1c) below to avoid pointlessly breaking the ABI *\/\n#define OPENSSL_VERSION_NUMBER 0x1000103fL/g" crypto/opensslv.h 
	
	# Use .so.1, not .so.1.0.0:
	zcat "${srcdir}"/openssl.soname.diff.gz | patch -p1 --backup --verbose --suffix=.orig
}

build() {
	cd openssl-${pkgver}
	
	# Ensure ls field counts are right for parsing
	export LC_ALL=C
	
	# Disable RC5 due to patent expiring in 2015-03-03 
	./config --prefix=/usr --openssldir=/etc/ssl --libdir=lib/${MULTIARCH} \
	         no-rc5 shared zlib "${CPPFLAGS} ${CFLAGS} ${LDFLAGS} -Wa,--noexecstack"

	make depend
	make
}

package() {
	cd openssl-${pkgver}
	make install INSTALL_PREFIX="${pkgdir}" MANDIR=/usr/share/man MANSUFFIX=ssl

	# Create symlinks
	( cd "${pkgdir}"/usr/lib/${MULTIARCH} ; ldconfig -l lib*.so.* )	
	
	# Install copyright notice
	mkdir -p "${pkgdir}"/usr/share/doc/openssl/
	cp -a LICENSE doc "${pkgdir}"/usr/share/doc/openssl/
}
