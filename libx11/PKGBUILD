# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libx11
pkgver=1.6.3
pkgrel=1
pkgdesc="X11 client-side library"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')

makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
depends=('libxcb' 'xproto' 'kbproto')

source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2)
sha256sums=('cf31a7c39f2f52e8ebd0db95640384e63451f9b014eed2bb7f5de03e8adc8111')

build() {
  cd libX11-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
              --disable-static --disable-xf86bigfont
  make
}

check() {
  cd libX11-${pkgver}
  make check
}

package() {
  cd libX11-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
