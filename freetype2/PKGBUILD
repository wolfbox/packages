pkgname=freetype2
pkgver=2.5.5
pkgrel=1
pkgdesc="A high-quality free typography library"
arch=("x86_64")
url="http://www.freetype.org"
license=('GPLv2' 'custom')

depends=('glibc' 'zlib' 'bzip2' 'libpng>=1.6' 'harfbuzz')

source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2{,.sig})
sha256sums=('387bcc4b780b12484aa2ec9f7db1a55d8286eb5639f45fbc0fbba7a4e5a1afb9'
            'SKIP')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5') # Werner Lemberg <wl@gnu.org>

build() {
	cd "${srcdir}/freetype-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
	            --with-harfbuzz=yes
	make
}

package() {
	cd "${srcdir}/freetype-${pkgver}"
	make DESTDIR="${pkgdir}" install
	
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
	install -m644 docs/{FTL.TXT,LICENSE.TXT,GPLv2.TXT} "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
}
