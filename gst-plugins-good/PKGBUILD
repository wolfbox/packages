pkgname=gst-plugins-good
pkgver=1.4.5
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=('x86_64')
license=('LGPL2.1')
options=(!emptydirs)
url="http://gstreamer.freedesktop.org/"

makedepends=('pkg-config' 'gobject-introspection' 'python2')
depends=('gstreamer' 'gst-plugins-base' 'orc' 'taglib'
         'wavpack' 'cairo' 'libvpx' 'speex' 'flac'
         'pulseaudio' 'libjpeg' 'libpng' 'gdk-pixbuf2')

source=(http://gstreamer.freedesktop.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('79b1b5f3f7bcaa8a615202eb5e176121eeb8336960f70687e536ad78dbc7e641')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --libdir=/usr/lib/${MULTIARCH} \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --disable-static \
                --enable-experimental \
                --with-package-name="GStreamer Good Plugins (Wolfbox Linux)" \
                --with-package-origin="http://wolfbox.org"

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make

    sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

package_gst-plugins-good() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
