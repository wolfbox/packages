# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xorg-fonts-cyrillic
pkgver=1.0.1
pkgrel=1
pkgdesc="X.org cyrillic fonts"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(xorg-fonts-encodings xorg-fonts-alias xorg-font-utils fontconfig)
install=xfonts.install

source=(${url}/releases/individual/font/font-cronyx-cyrillic-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-misc-cyrillic-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-screen-cyrillic-1.0.2.tar.bz2
        ${url}/releases/individual/font/font-winitzki-cyrillic-${pkgver}.tar.bz2)
sha256sums=('f7b43a428afe3111513e8ff0ebccc00fbddf2b57cec261ad9017d582fc9a3422'
            'dafafbfd93d95820449972fd86ec312266eb73e5297966ee6e0163d294d8af75'
            'c5dd881fb13ff16d358acb27f99f6c281415d3824beb342f3a02603f0efebe11'
            '7c57c29f70db18e543a3eb8f785b6b5d1372b78eee27707dbb10359d8ddb1446')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
		      --with-fontdir=/usr/share/fonts/cyrillic
      make
      popd
    fi
  done
}

package() {
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.${dir%-*}"
      popd
    fi    
  done
  rm -f "${pkgdir}"/usr/share/fonts/*/fonts.*
}
