pkgname=openldap
pkgver=2.4.40
pkgrel=1
pkgdesc="OpenLDAP Lightweight Directory Access Protocol implementation."
arch=("x86_64")
url="http://www.openldap.org"
license=(custom)

depends=('glibc' 'openssl' 'cyrus-sasl' 'zlib')

source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz)
sha256sums=('d12611a5c25b6499293c2bb7b435dc2b174db73e83f5a8cb7e34f2ce5fa6dadb')

build() {
	cd "${srcdir}/openldap-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --localstatedir=/var/lib \
				--sysconfdir=/etc --mandir=/usr/share/man --with-cyrus-sasl \
				--with-tls --with-threads --enable-syslog \
				--enable-dynamic --enable-local --enable-proctitle \
				--disable-static --enable-shared --disable-slapd
	make depend
	make
}

package() {
	cd "${srcdir}"/openldap-${pkgver}
	make install DESTDIR="${pkgdir}"

	# Fix permissions
	chmod 755 "${pkgdir}"/usr/lib/${MULTIARCH}/*.so.*

	# Install copyright and docs stuff
	mkdir -p "${pkgdir}"/usr/share/doc/openldap
	cp -a COPYRIGHT LICENSE README "${pkgdir}"/usr/share/doc/openldap/

	# We don't ship the servers right now, so remove manpages
	find "${pkgdir}"/usr/share/man -name slap* -exec rm -f {} \;
	find "${pkgdir}"/usr/share/man -name slurp* -exec rm -f {} \;
}
