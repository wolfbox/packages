# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xf86-video-neomagic
pkgver=1.2.8
pkgrel=1
pkgdesc="X.org neomagic video driver"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=18')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<18' 'X-ABI-VIDEODRV_VERSION>=19')
groups=('xorg-drivers' 'xorg')

source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('50abf6ac02547900edeaa5073c5f6d2f153f12dd47067cc7e4758e1b74d78874')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
}
