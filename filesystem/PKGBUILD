pkgname=filesystem
pkgver=1
pkgrel=1
pkgdesc="Basic filesystem links for Wolf Box."
arch=('any')
license=('MIT')
backup=('etc/passwd' 'etc/group' 'etc/ld.so.conf' 'etc/shadow' 'etc/gshadow'
        'etc/hosts' 'etc/hostname' 'etc/shells' 'etc/securetty' 'etc/motd' 
        'etc/nsswitch.conf')

source=('profile'
        'os-release'
        'ld.so.conf.in'
        'passwd'
        'group'
        'shadow'
        'gshadow'
        'hosts'
        'hostname'
        'shells'
        'securetty'
        'motd'
        'nsswitch.conf')
sha256sums=('5c56e94f10e8a87a82954801459eb4cd588bcb234af354ea08dbf2761ea62f1e'
            'd176841d0f92e962d4d48f1a51f2445a6bb21af5750dc4696d5d4968fbc36ddc'
            '6f1a6930364701c8ac86d54c4063e8326c6a64177144cbd83ab52a833a2c5814'
            '822afab94e325dcc9e791c2ceac8abfb7b1d01175b3a692e5385c8753ed89481'
            'b94ac86151627d845ac594da555d4ecd6d7a4cffec1e900a8cf204714930c4cb'
            '8bf8b6e07db4ff19eb73daffa71fca87f846a79a52b29d4f4b80981ff82a0ae5'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            '1f2b341cf1f7caf4c9c137f511691cc82b31c82106e7ab111b29a557b58e1d02'
            'f86d965dfd9c291a8cc38b75f06be00157ecfc324b5301654f83a449a723ae2d'
            '316c5f450db4a4062b419464f4586d88c11ae48fa81b13029eb0b022ddf64870'
            'db016bfe2a4ba4cf210997d1386f1a9a2616f9578ef58a156355eed03c0d50b2'
            'e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855'
            'a750d4be838467410f2415999b5a79aa083f7779eedb439cd51b792c5a5f028e')

package() {
	cd "${pkgdir}"
	
	install -d -m555 proc
	install -d -m555 sys
	install -d -m750 root
	install -d -m777 tmp

	mkdir -p {boot,dev,home,mnt,media,opt,run,lib}
	mkdir -p etc/{ld.so.conf.d,profile.d,skel}
	mkdir -p usr/{bin,include,lib,lib/modprobe.d,man,info,share}
	mkdir -p usr/share/man/man{1,2,3,4,5,6,7,8}
	mkdir -p usr/local/{bin,lib,etc,share,man,info,include}
	mkdir -p usr/local/share/man/man{1,2,3,4,5,6,7,8}
	mkdir -p var/{lib,log}

	install -dm777 var/tmp

	ln -s /usr/bin bin
	ln -s /usr/bin usr/sbin
	ln -s /usr/sbin sbin
	ln -s /run var/run
	ln -s /proc/self/mounts etc/mtab
	
	install -m755 "${srcdir}"/profile etc/
	install -m644 "${srcdir}"/os-release etc/
	install -m644 "${srcdir}"/passwd etc/
	install -m644 "${srcdir}"/group etc/
	install -m600 "${srcdir}"/shadow etc/
	install -m600 "${srcdir}"/gshadow etc/
	install -m644 "${srcdir}"/hostname etc/
	install -m644 "${srcdir}"/hosts etc/
	install -m644 "${srcdir}"/shells etc/
	install -m644 "${srcdir}"/securetty etc/
	install -m644 "${srcdir}"/motd etc/
	install -m644 "${srcdir}"/nsswitch.conf etc/
	sed "s:@MULTIARCH@:${MULTIARCH}:" "${srcdir}"/ld.so.conf.in > etc/ld.so.conf
}
