pkgname=libjpeg-turbo
pkgver=1.4.2
pkgrel=1
pkgdesc="A highly-optimized implementation of the JPEG image format"
arch=("x86_64")
url="http://www.libjpeg-turbo.org"
license=('BSD')

depends=('glibc')
replaces=('libjpeg')
provides=('libjpeg')

source=(http://downloads.sourceforge.net/project/libjpeg-turbo/${pkgver}/libjpeg-turbo-${pkgver}.tar.gz)
sha256sums=('521bb5d3043e7ac063ce3026d9a59cc2ab2e9636c655a2515af5f4706122233e')

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr \
	            --mandir=/usr/share/man \
	            --libdir=/usr/lib/${MULTIARCH}
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make test
}

package() {
	cd ${pkgname}-${pkgver}
	make install DESTDIR="${pkgdir}"
	
	rm -rf "${pkgdir}"/usr/share/doc/*
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
	install -m644 README* "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}/
}
