pkgname=clamav
pkgver=0.98.6
pkgrel=1
pkgdesc="A malware detection tool"
arch=("x86_64")
url="http://www.clamav.net"
license=('GPL2' 'LGPL2.1')
install=clamav.install
backup=('etc/freshclam.conf')

depends=('gcc-libs' 'glibc' 'zlib' 'bzip2' 'openssl' 'curl' 'xz' 'libxml2')

source=(http://downloads.sourceforge.net/project/clamav/clamav/${pkgver}/clamav-${pkgver}.tar.gz{,.sig})
sha256sums=('6591245d55445a2ddcc1700964c33b8bf62fe20c75bd9c7746f4fe0735502951'
            'SKIP')
validpgpkeys=('B964E6D7BC7D7C82CCB8D45840B8EA2364221D53') # Sourcefire VRT (Sourcefire VRT GPG Key) <research@sourcefire.com>

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}

	export CFLAGS="${CFLAGS} -fPIE"
	export CXXFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --sbindir=/usr/bin \
	            --sysconfdir=/etc \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --sysconfdir=/etc \
	            --enable-llvm \
	            --with-dbdir=/var/lib/clamav \
	            --build=${CHOST}
	make
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	make install DESTDIR="${pkgdir}"
	
	install -dm770 -o64 -g64 "${pkgdir}"/var/lib/clamav
	( cd "${pkgdir}"/etc
	  mv freshclam.conf.sample freshclam.conf )
}
