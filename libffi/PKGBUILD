pkgname=libffi
pkgver=3.2.1
pkgrel=1
pkgdesc="A portable Foreign Function Interface library."
arch=("x86_64")
url="http://sourceware.org/libffi/"
license=('custom')

depends=('glibc')

source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz
        0001-Fix-paths-in-libffi.pc.in.patch)
sha256sums=('d06ebb8e1d9a22d19e38d63fdb83954253f39bedc5d46232a05645685722ca37'
            '9abb0944ee86d49838743e14985b9da391a2ee94d3f4dfd49358ec817469d5f8')

prepare() {
	cd "${srcdir}/libffi-${pkgver}"
	patch -p1 -R -i "${srcdir}"/0001-Fix-paths-in-libffi.pc.in.patch
}

build() {
	cd "${srcdir}/libffi-${pkgver}"

	./configure --prefix=/usr \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --enable-pax_emutramp \
	            --disable-static
	make
}

package() {
	cd "${srcdir}/libffi-${pkgver}"
	make DESTDIR="${pkgdir}" install

	mkdir -p "${pkgdir}"/usr/share/doc/libffi-${pkgver}
	cp -a LICENSE "${pkgdir}"/usr/share/doc/libffi-${pkgver}/
}
