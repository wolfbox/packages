# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libxkbcommon
pkgver=0.5.0
pkgrel=1
pkgdesc="Keyboard handling library using XKB data"
arch=('x86_64')
url="http://xkbcommon.org/"
license=(custom)

makedepends=(libxcb xorg-util-macros)
depends=(xkeyboard-config glibc libxcb)

source=("http://xkbcommon.org/download/$pkgname-$pkgver.tar.xz")
sha256sums=('90bd7824742b9a6f52a6cf80e2cadd6f5349cf600a358d08260772615b89d19c')

prepare() {
  mkdir -p x11/usr/{include/xkbcommon,lib/pkgconfig}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib/${MULTIARCH}
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/doc/libxkbcommon/LICENSE"
}
