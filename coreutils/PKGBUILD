pkgname=coreutils
pkgver=8.23
pkgrel=1
pkgdesc="The GNU core system utilities"
arch=("x86_64")
url="http://www.gnu.org/software/coreutils"
license=('GPL3')

depends=('glibc' 'gmp' 'openssl' 'acl' 'attr' 'libcap')

source=(ftp://ftp.gnu.org/gnu/coreutils/coreutils-${pkgver}.tar.xz)
sha256sums=(ec43ca5bcfc62242accb46b7f121f6b684ee21ecd7d075059bf650ff9e37b82d)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	            --mandir=/usr/share/man \
	            --infodir=/usr/share/info	\
	            --with-gmp \
	            --with-openssl=yes \
	            --enable-no-install-program=groups,hostname,kill,uptime \
	            --libexecdir=/usr/lib/${MULTIARCH}
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	
	ln "${pkgdir}"/usr/bin/install "${pkgdir}"/usr/bin/ginstall
	
	mkdir -p "${pkgdir}"/etc/profile.d
	echo -e "#!/bin/sh\nalias ls='ls --color=auto'" > coreutils.sh
	install -m755 coreutils.sh "${pkgdir}"/etc/profile.d/
}
