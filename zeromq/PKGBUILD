pkgname=zeromq
pkgver=4.0.5
pkgrel=1
pkgdesc="Fast messaging system built on sockets"
arch=('x86_64')
url="http://www.zeromq.org"
license=('LGPL')
options=('staticlibs')

depends=('gcc-libs' 'util-linux' 'libsodium')
makedepends=('python2')

source=(http://download.zeromq.org/$pkgname-$pkgver.tar.gz
        https://raw.githubusercontent.com/zeromq/cppzmq/1f05e0d11/zmq.hpp)
sha256sums=('3bc93c5f67370341428364ce007d448f4bb58a0eaabd0a60697d8086bc43342b'
            '7815f3f9976eb80b35dac771924993c6ce643255cd250ced83371affd4b4648d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure prefix=/usr --libdir=/usr/lib/${MULTIARCH} --with-pgm --with-libsodium
  sed -i 's/python$/&2/' foreign/openpgm/build-staging/openpgm/pgm/{Makefile,version_generator.py}
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/zmq.hpp" "$pkgdir/usr/include/zmq.hpp"
}
