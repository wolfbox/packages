# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xorg-xrandr
pkgver=1.4.3
pkgrel=1
pkgdesc="Primitive command line interface to RandR extension"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxrandr' 'libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')

source=(http://xorg.freedesktop.org/archive/individual/app/xrandr-${pkgver}.tar.bz2{,.sig})
sha256sums=('7154ac3486b86923692f2d6cdb2991a2ee72bc32af2c4379a6f1c068f204be1b'
            'SKIP')
validpgpkeys=('BD68A042C603DDAD9AA354B0F56ACC8F09BA9635')

build() {
  cd "${srcdir}/xrandr-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xrandr-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/doc/${pkgname}/"
  rm -f "${pkgdir}/usr/bin/xkeystone"
}
