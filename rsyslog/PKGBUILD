pkgname=rsyslog
pkgver=8.6.0
pkgrel=1
pkgdesc="The rocket-fast syslog daemon."
arch=("x86_64")
url="http://www.rsyslog.com"
license=(GPLv3)

install=rsyslog.install
backup=('etc/rsyslog.conf')
depends=('glibc' 'zlib' 'libestr' 'liblogging' 'json-c' 'util-linux')
optdepends=('gpg-error' 'libgcrypt')

source=("http://www.rsyslog.com/files/download/rsyslog/rsyslog-${pkgver}.tar.gz"
        rsyslog.conf
        rsyslog
        rsyslog.filesystem)
sha256sums=('759f836be460c794a7649f2b5b5ef8d423388ec599bf3b49f51fded3f8c02431'
            '5a0507d45ce53e0f8d16e639b729c067b977bf02718288664aff2b2bfba3fdff'
            'cc97caeecf776764422d892da8d9961815755d1ec58d9b940d98f6a1c2852cef'
            '42421a551cfb9087471bb92675122c891618e20ce599d70b7b34768644bc959d')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# Harden
	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --libdir=/usr/lib/${MULTIARCH} \
				--sbindir=/usr/bin \
				--sysconfdir=/etc \
				--disable-generate-man-pages \
				--enable-gnutls \
				--enable-imfile \
				--enable-libgcrypt
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}"/etc/rc.d/events
	install -m 755 "${srcdir}"/rsyslog "${pkgdir}"/etc/rc.d/
	install -m 755 "${srcdir}"/rsyslog.filesystem "${pkgdir}"/etc/rc.d/events/
	install -m 644 "${srcdir}"/rsyslog.conf "${pkgdir}"/etc/

	mkdir -p "${pkgdir}"/run/rsyslog
	mkdir -p "${pkgdir}"/var/lib/rsyslog
	mkdir -p "${pkgdir}"/var/log/syslog
	chmod 770 "${pkgdir}"/var/log/syslog
	chown 115:115 "${pkgdir}"/{,var/lib/rsyslog,run/rsyslog}
	chown 115:syslog "${pkgdir}"/var/log/syslog
}
