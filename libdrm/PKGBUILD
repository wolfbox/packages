# Derived from Arch Linux
# Maintainer: Andrew Aldridge <i80and@foxquill.com>

pkgname=libdrm
pkgver=2.4.65
pkgrel=1
pkgdesc="Userspace access to DRM"
arch=("x86_64")
url="http://dri.freedesktop.org"
license=('x11')

depends=('glibc' 'libpciaccess')

source=(http://dri.freedesktop.org/libdrm/libdrm-${pkgver}.tar.bz2{,.sig})
sha256sums=('71960ac8bde7d710992b1bc8879935e8300a870c36bd06f22412d0447e3d96c4'
            'SKIP')
validpgpkeys=('D6285B5E899299F3DA746184191C9B905522B045') # Rob Clark <robclark@freedesktop.org>

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # Remove unneeded pthreads thing
    sed -i "/pthread-stubs/d" configure.ac
    autoreconf --force --install
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
                --libdir=/usr/lib/${MULTIARCH} \
                --enable-udev
    make V=1 VERBOSE=1
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
