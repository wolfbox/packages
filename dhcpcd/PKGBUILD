pkgname=dhcpcd
pkgver=6.7.1
pkgrel=1
pkgdesc="A DHCP network autoconfiguration implementation"
arch=("x86_64")
url="http://roy.marples.name/projects/dhcpcd/"
license=('BSD')
options=('emptydirs')
backup=('etc/dhcpcd.conf')

depends=('glibc')

source=("http://roy.marples.name/downloads/dhcpcd/dhcpcd-${pkgver}.tar.bz2")
sha256sums=('cb65d3e783fa5484ed095bb940c3274a5a91d3ed4a9d9ae66a49d9637165bd98')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	export CFLAGS="${CFLAGS} -fPIE"
	export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"

	./configure --prefix=/usr \
	            --sbindir=/usr/bin \
	            --sysconfdir=/etc \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --libexecdir=/usr/lib/${MULTIARCH} \
	            --mandir=/usr/share/man \
	            --dbdir=/var/lib/dhcpcd \
	            --rundir=/run \
	            --build=${CHOST}
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
}
