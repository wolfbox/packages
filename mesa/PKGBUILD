# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=mesa
pkgver=10.5.1
pkgrel=1
arch=('x86_64')
url="http://mesa3d.sourceforge.net"
license=('custom')

makedepends=('python2' 'python2-mako' 'libxml2' 'glproto' 'dri2proto' 'dri3proto'
             'presentproto' 'libvdpau' 'llvm' 'libclc' 'wayland' 'libva')
depends=('gcc-libs' 'glibc' 'expat' 'libdrm' 'libxcb' 'libx11' 'libxdamage'
         'libxshmfence' 'libxxf86vm' 'libxext' 'libxau' 'libxdmcp' 'llvm-libs'
         'libpciaccess' 'elfutils')
optdepends=('libtxc_dxtn')
provides=('libgl')
replaces=('libgl')

source=(ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/mesa-${pkgver}.tar.xz{,.sig}
        llvm-link-fix.patch
        LICENSE)
sha256sums=('ffc51943d15c6812ee7611d053d8980a683fbd6a4986cff567b12cc66637d679'
            'SKIP'
            '0a0aebededec771ca11176b666aec129b1333b86037eb6d5db84219773ae371a'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')
validpgpkeys=('8703B6700E7EE06D7A39B8D6EDAE37B02CEB490D') # Emil Velikov <emil.l.velikov@gmail.com>

prepare() {
  cd mesa-${pkgver}
  patch -p1 -i "${srcdir}"/llvm-link-fix.patch
}

build() {
  cd ${srcdir}/mesa-${pkgver}

  autoreconf -vfi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib/${MULTIARCH} \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-egl-platforms=x11,drm,wayland \
    --enable-llvm-shared-libs \
    --enable-egl \
    --enable-gbm \
    --enable-gallium-llvm \
    --enable-llvm-shared-libs \
    --enable-shared-glapi \
    --enable-glx \
    --enable-glx-tls \
    --enable-dri \
    --enable-dri3 \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --enable-omx \
    --enable-opencl --enable-opencl-icd \
    --with-clang-libdir=/usr/lib/${MULTIARCH}

  make
}

package() {
  cd "${srcdir}/mesa-${pkgver}"
  make install DESTDIR="${pkgdir}"
  
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"
  
  # If mesa is installed, use the GLX that comes with X.org
  mkdir -p "${pkgdir}"/usr/lib/xorg/modules/extensions
  ln -s libglx.xorg "${pkgdir}"/usr/lib/xorg/modules/extensions/libglx.so
}
