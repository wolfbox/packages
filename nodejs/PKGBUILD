pkgname=nodejs
pkgver=0.10.33
pkgrel=1
pkgdesc="A server-side JavaScript environment"
arch=("x86_64")
url="http://nodejs.org"
license=('custom')

depends=('gcc-libs' 'glibc' 'zlib' 'openssl')

source=(http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz)
sha256sums=(75dc26c33144e6d0dc91cb0d68aaf0570ed0a7e4b0c35f3a7a726b500edd081e)

prepare() {
	cd "${srcdir}"/node-v${pkgver}
	
	# sed incantations taken from Arch
	msg "Fixing up python2"
	find -type f -exec sed \
		-e 's_^#!/usr/bin/env python$_&2_' \
		-e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
		-e 's_^#!/usr/bin/python$_&2_' \
		-e "s_'python'_'python2'_" -i {} \;
}

build() {
	cd "${srcdir}/node-v${pkgver}"

	export PYTHON=python2
	./configure --prefix=/usr \
	            --shared-openssl \
	            --shared-zlib
	make
}

package() {
	cd "${srcdir}/node-v${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}"/usr/share/doc/nodejs
	install -m644 LICENSE "${pkgdir}"/usr/share/doc/nodejs
	
	find "${pkgdir}"/usr \( -name '.git*' -o -name '.travis.yml' \) -exec rm {} \;
}
