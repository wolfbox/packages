pkgname=shadow
pkgver=4.2.1
pkgrel=1
pkgdesc="The shadow password suite"
arch=("x86_64")
url="http://pkg-shadow.alioth.debian.org/releases/"
license=(BSD)
backup=('etc/login.defs'
        'etc/pam.d/su'
        'etc/pam.d/login'
        'etc/pam.d/passwd'
        'etc/pam.d/chpasswd'
        'etc/pam.d/chgpasswd')

depends=('glibc' 'openpam' 'openpam-modules')

source=(http://pkg-shadow.alioth.debian.org/releases/shadow-${pkgver}.tar.xz{,.sig}
		login.defs
		useradd.xz
		shadow.CVE-2005-4890.relax.diff
		chgpasswd
        chpasswd
        newusers
        passwd
        su
        login
        defaults.pam)
sha256sums=('3b0893d1476766868cd88920f4f1231c4795652aa407569faff802bcda0f3d41'
            'SKIP'
            '8f3a37af0953aff2c067b3b87036422983e8f47277e8818111eb4d6a2fd5f5a2'
            '11625abe877a9eb3c9482ed8635a400d8935e11f4fbd962b5a6ab4fc277c444a'
            '13f17759687374707fc170bdc8001f8124a6f9e275c0b4556624ebd09f3f2965'
            '933e1eb84fd922242e661e8f96b4f94f7faadb7eb7880bae66b1403a6357e604'
            '933e1eb84fd922242e661e8f96b4f94f7faadb7eb7880bae66b1403a6357e604'
            '41e91678b673947f2eac75847d5a85a4289b9b8d01679a181708ee10ff1559cc'
            '2e3a1bbd1c8b4c77afc280e9fdfc9ffd2f74d3d7d50bf034d8a890ec108dc1b7'
            '70f0232f385066872fc13c3d134f6856d108bf272fc92fe104c69c84dc9627ee'
            'a328cfdf8b4516d6f7a1b9e68e32b59ec312f04fad22f59fd8c482f3082c4ce5'
            '4902920671abb084ab6301302c62718fe22cdb38563f5f95771d54ac7aa70385')
validpgpkeys=('D5C2F9BFCA128BBA22A77218872F702C4D6E25A8') # Christian Perrier <bubulle@debian.org>

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../shadow.CVE-2005-4890.relax.diff
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --bindir=/usr/bin \
                --sbindir=/usr/bin \
                --docdir=/usr/share/doc/shadow \
                --enable-subordinate-ids \
                --with-libpam \
                --disable-shared \
                --without-libcrack \
                --without-selinux
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make install DESTDIR="${pkgdir}"

    # For some reason, shadow puts files in usr/sbin
    mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin/
    rmdir "${pkgdir}/usr/sbin"

    # Replace login.defs
    rm -f "${pkgdir}/etc/login.defs"
    install -m 644 "${srcdir}"/login.defs "${pkgdir}"/etc/login.defs
	
    # Slackware doesn't like this
    rm -f "${pkgdir}/etc/limits"
    
    # And Arch doesn't like this
    rm -f "${pkgdir}"/usr/bin/logoutd
    rm -f "${pkgdir}"/usr/share/man/man8/logoutd*
	
    # Add sulogin
    cp -a "src/sulogin" "${pkgdir}/usr/bin/sulogin"

    # Our default PAM modules are fine for most things. Install our own files for the rest.
    rm -r "${pkgdir}"/etc/pam.d/*
    install -m644 "${srcdir}"/{chgpasswd,chpasswd,newusers,passwd,su,login} "${pkgdir}"/etc/pam.d/
    for file in chage groupadd groupdel groupmod shadow \
                useradd usermod userdel; do
        install -Dm644 "$srcdir/defaults.pam" "$pkgdir/etc/pam.d/$file"
    done

    # Make permissions more strict on setuid
    find "${pkgdir}" -type f -perm 4755 -exec chmod 4711 "{}" \;
	
    # Install useradd defaults
    mkdir -p "${pkgdir}"/etc/default
    install -m644 "${srcdir}"/useradd "${pkgdir}"/etc/default
}
