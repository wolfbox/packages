pkgname=python2
pkgver=2.7.9
pkgrel=1
pkgdesc="The Python 2 (legacy) programming langauge."
arch=("x86_64")
url="https://www.python.org"
license=(custom)

provides=("python=${pkgver}")
depends=('glibc' 'bzip2' 'zlib')
optdepends=('ncurses' 'readline' 'gdbm' 'db48' 'xz' 'openssl' 'sqlite3' 'tk')

source=(https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz
        python-libdir.diff.xz)
sha256sums=('90d27e14ea7e03570026850e2e50ba71ad20b7eb31035aada1cf3def8f8d4916'
            '8d7807d3eda9913f565b95e6c6404a309814d3219290336ac5ed6376812a7c6e')

prepare() {
	cd "${srcdir}"/Python-${pkgver}

 	# Fix libdir
	cat ../python-libdir.diff | patch -p1
	sed -i "s:@MULTIARCH@:${MULTIARCH}:" Modules/getpath.c
}

build() {
	cd "${srcdir}/Python-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man --libdir=/usr/lib/${MULTIARCH} \
	            --with-threads --enable-ipv6 --enable-shared

	make
}

package() {
	cd "${srcdir}/Python-${pkgver}"

	make install DESTDIR="${pkgdir}"
	
	# Remove things that are canonically shipped in our Python 3 package.
	rm "${pkgdir}"/usr/bin/python
	rm "${pkgdir}"/usr/bin/python-config
	rm "${pkgdir}"/usr/bin/2to3

	# Install license info
	mkdir -p "${pkgdir}"/usr/share/doc/python2
	install -m644 LICENSE "${pkgdir}"/usr/share/doc/python2
}
