pkgname=js185
pkgver=1.0.0
pkgrel=1
pkgdesc="Legacy JavaScript interpreter"
arch=("x86_64")
url="https://developer.mozilla.org/En/SpiderMonkey/1.8.5"
license=('MPL')

depends=('glibc' 'nspr')

source=(http://ftp.mozilla.org/pub/mozilla.org/js/js185-${pkgver}.tar.gz
        allocgcchunk-infinite-loop.patch)
sha256sums=('5d12f7e1f5b4a99436685d97b9b7b75f094d33580227aa998c406bbae6f2a687'
            '2f660fec3f091e0646ed8b5f3d886f527faf1ac0e06eca34afb93372c572899f')

prepare() {
	cd js-1.8.5

	# Fix infinite loop in chunk allocation.  See Mozilla bugzilla #613089
	patch -p1 -i "${srcdir}"/allocgcchunk-infinite-loop.patch
}

build() {
	cd js-1.8.5/js/src

	unset CPPFLAGS
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH}
	make
}

check() {
	cd js-1.8.5/js/src
	make check
}

package() {
	cd js-1.8.5/js/src
	make DESTDIR="${pkgdir}" install

	install -Dm755 shell/js "${pkgdir}"/usr/bin/js
	
	# Fix symlinks
	( cd "${pkgdir}"/usr/lib/${MULTIARCH}
	  ln -sf libmozjs185.so.1.0.0 libmozjs185.so.1.0
	  ln -sf libmozjs185.so.1.0 libmozjs185.so )
}
