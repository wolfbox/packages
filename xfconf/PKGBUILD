# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=xfconf
pkgver=4.10.0
pkgrel=1
pkgdesc="A simple client-server configuration storage and query system"
arch=('x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
options=('!emptydirs')

# http://www.xfce.org/documentation/requirements
# keep dbus, see also http://bugs.archlinux.org/task/14536
depends=('libxfce4util' 'dbus-glib' 'dbus')
makedepends=('intltool' 'chrpath')

source=(http://archive.xfce.org/src/xfce/$pkgname/4.10/$pkgname-$pkgver.tar.bz2)
sha256sums=('175219a441cc7d0f210bbd1a3b0abba41598627cd9db27235811400c3e100576')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/${MULTIARCH}/xfce4 \
    --libdir=/usr/lib/${MULTIARCH} \
    --localstatedir=/var \
    --disable-static \
    --disable-gtk-doc \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # fix insecure rpath, http://bugs.archlinux.org/task/19980
  # chrpath -d "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.so"
}
