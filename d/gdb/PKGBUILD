pkgname=gdb
pkgver=7.8.1
pkgrel=1
pkgdesc="The GNU program debugger."
arch=("x86_64")
url="http://www.gnu.org/software/gdb/"
license=('GPL')

depends=('glibc' 'python' 'zlib' 'xz' 'expat' 'ncurses')

source=(http://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.xz)
sha256sums=(497722b58386f4428c3474281eb3ef1c872404998b6ace907edb3bba1fabdc35)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	# GDB doesn't like hardening
	export CPPFLAGS=""
	export CFLAGS=${CFLAGS/-D_FORTIFY_SOURCE=2/}
	
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
	            --mandir=/usr/man --infodir=/usr/info \
	            --with-python --build=${CHOST}
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"

	rm -f "${pkgdir}"/usr/lib/${MULTIARCH}/{libbfd*,libiberty*,libopcodes*}
	rm -f "${pkgdir}"/usr/info/{annotate*,bfd*,configure*,standards*}
	rm -rf "${pkgdir}"/usr/include

	# Remove duplicate locale files
	find "${pkgdir}"/usr/share/locale -name "bfd.mo" -exec rm {} \;
	find "${pkgdir}"/usr/share/locale -name "opcodes.mo" -exec rm {} \;
}
