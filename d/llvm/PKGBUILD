pkgname=llvm
pkgver=3.5.0
pkgrel=1
pkgdesc="Low Level Virtual Machine compiler toolkit."
arch=("x86_64")
url="http://llvm.org"
license=('BSD')

depends=('glibc')

source=(http://llvm.org/releases/${pkgver}/llvm-3.5.0.src.tar.xz
        http://llvm.org/releases/${pkgver}/cfe-${pkgver}.src.tar.xz)
sha256sums=(28e199f368ef0a4666708f31c7991ad3bcc3a578342b0306526dd35f07595c03
            fc80992e004b06f6c7afb612de1cdaa9ac9d25811c55f94fcf7331d9b81cdb8b)

prepare() {
	cd "${srcdir}"/${pkgname}-${pkgver}.src/tools
	mv ../../cfe-"${pkgver}".src ./clang
}

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}.src
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
	            --localstatedir=/var --mandir=/usr/man --enable-optimized \
	            --disable-assertions --enable-pic --enable-shared \
	            --with-clang-resource-dir="../lib64/clang/${pkgver}" \
	            --build=${CHOST} --host=${CHOST}

	# Fix hardcoded libdir
	sed -i "s|\$(PROJ_prefix)/lib|\$(PROJ_prefix)/lib64|" \
		Makefile.config
	sed -i "s|\$(PROJ_prefix)/lib|\$(PROJ_prefix)/lib64|" \
		tools/clang/lib/Headers/Makefile
	sed -i "s|\"lib\"|\"lib64\"|" \
		tools/clang/lib/Frontend/CompilerInvocation.cpp
	sed -i "s|\"lib\"|\"lib64\"|" \
		tools/clang/lib/Driver/Tools.cpp
	sed -i "s|ActiveLibDir = ActivePrefix + \"/lib\"|ActiveLibDir = ActivePrefix + \"/lib64\"|g" \
		tools/llvm-config/llvm-config.cpp

	make
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}.src

	make install DESTDIR="${pkgdir}"
	
	# Add target symlinks
	( cd "${pkgdir}"/usr/bin
	  ln clang ${CHOST}-clang
	  ln clang++ ${CHOST}-clang++
	)
	
	# Install static analyzer
	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}/clang-analyzer
	cp -pr tools/clang/tools/scan-{build,view} \
	    "${pkgdir}"/usr/lib/${MULTIARCH}/clang-analyzer/
	
	for i in scan-{build,view}; do
		ln -s /usr/lib/${MULTIARCH}/clang-analyzer/$i/$i "${pkgdir}"/usr/bin/$i
	done
	
	ln -s /usr/bin/clang "${pkgdir}"/usr/lib/${MULTIARCH}/clang-analyzer/scan-build/clang
	
	# Remove example libraries
	rm -f "${pkgdir}"/usr/lib/${MULTIARCH}/LLVMHello*
	
	# Fix up manpages
	mv "${pkgdir}"/usr/share/man "${pkgdir}"/usr/man
	mv "${pkgdir}"/usr/lib/${MULTIARCH}/clang-analyzer/scan-build/scan-build.1 "${pkgdir}"/usr/man/man1
	
}
