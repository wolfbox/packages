pkgbase=gcc
pkgname=(gcc-libs gcc-go gcc-gfortran gcc)
pkgver=4.9.1
pkgrel=1
pkgdesc="The GNU Compiler Collection."
arch=("x86_64")
url="http://gcc.gnu.org"
license=(GPLv3)

options=('strip')
makedepends=('zlib')
checkdepends=('autotext')

source=(http://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
		gcc-no_fixincludes.diff.gz)

sha256sums=(d334781a124ada6f38e63b545e2a3b8c2183049515a1abab6d513f109f1d717e
			cdf880bd4fc1a35f5f3c6ec7d2eb1ce7ab55bbf60aa64db495cb07071a7af40b)

_target_dir=usr/lib/${MULTIARCH}/gcc/${CHOST}/${pkgver}

prepare() {
	cd "${srcdir}"/gcc-${pkgver}

	# No fixincludes
	zcat "${srcdir}"/gcc-no_fixincludes.diff.gz | patch -p1

	# Fix libdir
	sed -i "/m64=/s/lib64/${MULTIARCH}/" gcc/config/i386/t-linux64
	
	mkdir -p "${srcdir}/build"
}

build() {
	cd "${srcdir}/build"

	# GCC doesn't work well with our hardening options.
	export CPPFLAGS=""
	export CCFLAGS=""
	export CXXFLAGS=""

	../gcc-${pkgver}/configure --prefix=/usr --mandir=/usr/man \
				--libexecdir=/usr/lib/${MULTIARCH} --libdir=/usr/lib/${MULTIARCH} \
				--infodir=/usr/info --enable-shared --enable-bootstrap \
				--enable-languages=c,c++,objc,go,fortran,lto \
				--enable-threads=posix --enable-checking=release \
				--enable-objc-gc --with-system-zlib \
				--disable-libunwind-exceptions --enable-__cxa_atexit \
				--enable-libssp --enable-lto --with-gnu-ld --verbose \
				--disable-gtktest --disable-multilib --enable-multiarch \
				--target=${CHOST} --build=${CHOST} --host=${CHOST}
	make
}

# Packaging functions
package_gcc-libs() {
	pkgdesc="${pkgdesc} - Runtime libraries."
	depends=('glibc')
	
	# Set up our initial install tree
	cd "${srcdir}/build"
	rm -rf ../out
	mkdir ../out
	make install DESTDIR="${srcdir}"/out/

	# KLUDGE ALERT
	# Avoid ldconfig error
	rm -f "${srcdir}"/out/usr/lib/${MULTIARCH}/*gdb.py
	
	# Move runtime libraries
	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}
	mv "${srcdir}"/out/usr/lib/${MULTIARCH}/lib{asan,atomic,gcc_s,gfortran,gomp,itm,objc,quadmath,stdc++,tsan}.so* "${pkgdir}"/usr/lib/${MULTIARCH}

	# Info files
	mkdir -p "${pkgdir}"/usr/info
	mv "${srcdir}"/out/usr/info/lib{gomp,itm,quadmath}.info* "${pkgdir}"/usr/info/

	# Locale	
	mkdir -p "${pkgdir}"/usr/share/locale/de/LC_MESSAGES/
	mkdir -p "${pkgdir}"/usr/share/locale/fr/LC_MESSAGES/
	mv "${srcdir}"/out/usr/share/locale/de/LC_MESSAGES/libstdc++.mo "${pkgdir}"/usr/share/locale/de/LC_MESSAGES/
	mv "${srcdir}"/out/usr/share/locale/fr/LC_MESSAGES/libstdc++.mo "${pkgdir}"/usr/share/locale/fr/LC_MESSAGES/

	# License
	mkdir -p "${pkgdir}"/usr/doc/gcc-libs-${pkgver}
	install -m644 "${srcdir}"/gcc-${pkgver}/COPYING.RUNTIME "${pkgdir}"/usr/doc/gcc-libs-${pkgver}
}

package_gcc() {
	pkgdesc="${pkgdesc} - C and C++ frontends."
	depends=('glibc' 'gcc-libs' 'binutils' 'mpc')

	mv "${srcdir}"/out/* "${pkgdir}"/

	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/c89
	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/c99
	ln -s /usr/bin/gcc "${pkgdir}"/usr/bin/cc
}

package_gcc-gfortran() {
	pkgdesc="${pkgdesc} - Fortran support."
	depends=('gcc')

	mkdir -p "${pkgdir}"/usr/bin
	mv "${srcdir}"/out/usr/bin/*gfortran "${pkgdir}"/usr/bin/
	ln "${pkgdir}"/usr/bin/gfortran "${pkgdir}"/usr/bin/f95

	mkdir -p "${pkgdir}"/usr/lib/${MULTIARCH}
	mv "${srcdir}"/out/usr/lib/${MULTIARCH}/libgfortran.spec "${pkgdir}"/usr/lib/${MULTIARCH}

	mkdir -p "${pkgdir}"/${_target_dir}
	mv "${srcdir}"/out/${_target_dir}/finclude "${pkgdir}"/${_target_dir}/
	mv "${srcdir}"/out/${_target_dir}/{f951,libgfortranbegin.a} "${pkgdir}"/${_target_dir}/

	mkdir -p "${pkgdir}"/usr/info
	mv "${srcdir}"/out/usr/info/gfortran.info* "${pkgdir}"/usr/info

	mkdir -p "${pkgdir}"/usr/man/man1
	mv "${srcdir}"/out/usr/man/man1/gfortran* "${pkgdir}"/usr/man/man1
}

package_gcc-go() {
	pkgdesc="${pkgdesc} - Go support."
	depends=('gcc')
	
	mkdir -p "${pkgdir}"/usr/bin
	mv "${srcdir}"/out/usr/bin/*gccgo "${pkgdir}"/usr/bin
	
	mkdir -p "${pkgdir}"/${_target_dir}
	mv "${srcdir}"/out/usr/lib/${MULTIARCH}/{libgo.so*,libgobegin.a,go} "${pkgdir}"/usr/lib/${MULTIARCH}
	mv "${srcdir}"/out/${_target_dir}/go1 "${pkgdir}"/${_target_dir}/

	mkdir -p "${pkgdir}"/usr/man/man1
	mv "${srcdir}"/out/usr/man/man1/gccgo* "${pkgdir}"/usr/man/man1/

	mkdir -p "${pkgdir}"/usr/info
	mv "${srcdir}"/out/usr/info/gccgo* "${pkgdir}"/usr/info/
}

check() {
	cd "${srcdir}/gcc-${pkgver}"
	make -k check || true
}
