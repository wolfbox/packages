pkgname=subversion
pkgver=1.8.11
pkgrel=1
pkgdesc="A software revision control system."
arch=("x86_64")
url="https://subversion.apache.org"
license=('Apache')

depends=('glibc' 'zlib' 'openssl' 'file' 'expat' 'sqlite3'
         'openldap' 'neon' 'apr' 'apr-util' 'util-linux' 'cyrus-sasl'
         'serf')

source=(http://www.eng.lsu.edu/mirrors/apache/subversion/subversion-${pkgver}.tar.bz2
        http://www.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2.asc)
sha256sums=('10b056420e1f194c12840368f6bf58842e6200f9cb8cc5ebbf9be2e89e56e4d9'
            'SKIP')
validpgpkeys=('19BBCAEF7B19B280A0E2175E62D48FAD16A0DE01') # Ben Reser <breser@apache.org>

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	            --libdir=/usr/lib/${MULTIARCH} \
	            --docdir=/usr/share/doc/${pkgname}-${pkgver} \
	            --enable-shared --disable-static \
	            --with-zlib=/usr \
	            --with-pic \
	            --with-openssl \
	            --with-serf
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install DESTDIR="${pkgdir}"
	make install-tools DESTDIR="${pkgdir}"
	
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
	install -m 644 LICENSE "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}/
}
