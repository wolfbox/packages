pkgname=shared-mime-info
pkgver=1.3
pkgrel=1
pkgdesc="Freedesktop.org Shared MIME Info"
arch=('x86_64')
license=('GPL2')
url="http://freedesktop.org/Software/shared-mime-info"
install=shared-mime-info.install
options=(!makeflags)

makedepends=('intltool' 'pkg-config')
depends=('libxml2' 'glib2')

source=(http://freedesktop.org/~hadess/${pkgname}-${pkgver}.tar.xz)
sha256sums=('4fd49c8c7ca9ecb10c59845094a18dbb73b69c72b4bad3db5e864f2111cb323a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # https://bugs.archlinux.org/task/38836
  # https://bugs.freedesktop.org/show_bug.cgi?id=70366
  export ac_cv_func_fdatasync=no
  ./configure --prefix=/usr --disable-update-mimedb
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
