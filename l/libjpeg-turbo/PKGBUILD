pkgname=libjpeg-turbo
pkgver=1.4.0
pkgrel=1
pkgdesc="A highly-optimized implementation of the JPEG image format"
arch=("x86_64")
url="http://www.libjpeg-turbo.org"
license=('BSD')

depends=('glibc')
replaces=('libjpeg')
provides=('libjpeg')

source=(http://downloads.sourceforge.net/project/libjpeg-turbo/${pkgver}/libjpeg-turbo-${pkgver}.tar.gz)
sha256sums=('d93ad8546b510244f863b39b4c0da0fa4c0d53a77b61a8a3880f258c232bbbee')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	            --mandir=/usr/share/man \
	            --libdir=/usr/lib/${MULTIARCH}
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make install DESTDIR="${pkgdir}"
	
	rm -rf "${pkgdir}"/usr/share/doc/*
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
	install -m644 README* "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}/
}
