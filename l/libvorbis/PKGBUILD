pkgname=libvorbis
pkgver=1.3.4
pkgrel=1
pkgdesc="The reference Vorbis audio compression codec library"
arch=('x86_64')
url="http://www.xiph.org/vorbis/"
license=('BSD')

depends=('libogg')

source=(http://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz)
sha256sums=('eee09a0a13ec38662ff949168fe897a25d2526529bc7e805305f381c219a1ecb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #-march=i686 optimizes too much, strip it out
  LIBS+="-lm" ./configure --prefix=/usr \
                          --libdir=/usr/lib/${MULTIARCH} \
                          --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/doc/$pkgname/LICENSE"
}
