# Maintainer: Andrew Aldridge <i80and@foxquill.com>
# Derived from Arch Linux

pkgname=python-pyelftools
_pypiname=pyelftools
pkgver=0.23
pkgrel=1
pkgdesc="Python library for analyzing ELF files and DWARF debugging information"
arch=('any')
url="https://github.com/eliben/pyelftools"
license=('custom')

depends=('python')

source=("https://github.com/eliben/pyelftools/archive/v${pkgver}.zip")
sha256sums=('77dcd03db9ea80366d8e393292516e4b1701de50ea561fb69f4b6c3c3bb9c159')

build() {
  cd ${srcdir}/${_pypiname}-${pkgver}
  python setup.py build
}

check() {
  cd ${srcdir}/${_pypiname}-${pkgver}
  python test/run_all_unittests.py
}

package() {
  cd ${srcdir}/${_pypiname}-${pkgver}
  python setup.py install --root=${pkgdir}
  install -D -m 644 LICENSE ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
