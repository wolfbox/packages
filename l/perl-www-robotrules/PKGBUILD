pkgname=perl-www-robotrules
pkgver=6.02
pkgrel=1
pkgdesc="Database of robots.txt-derived permissions"
arch=('any')
url="https://metacpan.org/release/WWW-RobotRules"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')

depends=('perl' 'perl-uri')

source=(http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/WWW-RobotRules-${pkgver}.tar.gz)
sha256sums=('46b502e7a288d559429891eeb5d979461dd3ecc6a5c491ead85d165b6e03a51e')

build() {
  cd "${srcdir}/WWW-RobotRules-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/WWW-RobotRules-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/WWW-RobotRules-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
