pkgname=libomxil-bellagio
pkgver=0.9.3
pkgrel=1
pkgdesc="An implementation of the OpenMAX Integration Layer API"
arch=('x86_64')
url="http://omxil.sourceforge.net"
license=('LGPL')

depends=('glibc')

source=("http://downloads.sourceforge.net/project/omxil/omxil/Bellagio%200.9.3/${pkgname}-${pkgver}.tar.gz"
        fedora-fixes.patch)
sha256sums=('593c0729c8ef8c1467b3bfefcf355ec19a46dd92e31bfc280e17d96b0934d74c'
            '1683126ba747ae2f711cf352b289a26c17c56e92745a0ddbd5f066d66a9bd007')

prepare() {
  cd ${srcdir}/${pkgname}-$pkgver

  # Fixes from fedora repo
  patch -Np1 -i ../fedora-fixes.patch
  autoreconf -fiv
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --libdir=/usr/lib/${MULTIARCH} \
              --disable-static \
              --docdir=/usr/doc/${pkgname}-${pkgver} \
              --mandir=/usr/man/
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
