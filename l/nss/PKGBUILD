# Derived form the Arch Linux PKGBUILD

pkgname=nss
pkgver=3.17.2
pkgrel=1
pkgdesc="Mozilla Network Security Services"
arch=(i686 x86_64)
url="http://www.mozilla.org/projects/security/pki/nss/"
license=('MPL' 'GPL')

_nsprver=3.10.7
depends=('glibc' "nspr>=${_nsprver}" 'sqlite3' 'zlib' 'sh' 'p11-kit')
options=('!strip' '!makeflags' 'staticlibs')

source=("http://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/${pkgname}-${pkgver}.tar.gz"
        nss.pc.in
        nss-config.in
        ssl-renegotiate-transitional.patch)
sha256sums=('134929e44e44b968a4883f4ee513a71ae45d55b486cee41ee8e26c3cc84dab8b'
            'b9f1428ca2305bf30b109507ff335fa00bce5a7ce0434b50acd26ad7c47dd5bd'
            'e44ac5095b4d88f24ec7b2e6a9f1581560bd3ad41a3d198596d67ef22f67adb9'
            '12df04bccbf674db1eef7a519a28987927b5e9c107b1dc386686f05e64f49a97')

prepare() {
    cd nss-$pkgver

    # Adds transitional SSL renegotiate support - patch from Debian
    patch -Np3 -i ../ssl-renegotiate-transitional.patch

    # Respect LDFLAGS
    sed -e 's/\$(MKSHLIB) -o/\$(MKSHLIB) \$(LDFLAGS) -o/' \
          -i nss/coreconf/rules.mk
}


build() {
    cd nss-$pkgver/nss

    export BUILD_OPT=1
    export NSS_USE_SYSTEM_SQLITE=1
    export NSS_ENABLE_ECC=1
    export NSPR_INCLUDE_DIR="`nspr-config --includedir`"
    export NSPR_LIB_DIR="`nspr-config --libdir`"
    export XCFLAGS="${CFLAGS}"

    [ "$CARCH" = "x86_64" ] && export USE_64=1

    make -C coreconf
    make -C lib/dbm
    make
}

package() {
    cd nss-$pkgver
    install -d "$pkgdir"/usr/{bin,include/nss,lib/${MULTIARCH}/pkgconfig}

    NSS_VMAJOR=$(grep '#define.*NSS_VMAJOR' nss/lib/nss/nss.h | awk '{print $3}')
    NSS_VMINOR=$(grep '#define.*NSS_VMINOR' nss/lib/nss/nss.h | awk '{print $3}')
    NSS_VPATCH=$(grep '#define.*NSS_VPATCH' nss/lib/nss/nss.h | awk '{print $3}')

    sed ../nss.pc.in \
      -e "s,%libdir%,/usr/lib/${MULTIARCH},g" \
      -e "s,%prefix%,/usr,g" \
      -e "s,%exec_prefix%,/usr/bin,g" \
      -e "s,%includedir%,/usr/include/nss,g" \
      -e "s,%NSPR_VERSION%,${_nsprver},g" \
      -e "s,%NSS_VERSION%,${pkgver},g" \
      > "$pkgdir/usr/lib/${MULTIARCH}/pkgconfig/nss.pc"
    ln -s nss.pc "$pkgdir/usr/lib/${MULTIARCH}/pkgconfig/mozilla-nss.pc"

    sed ../nss-config.in \
      -e "s,@libdir@,/usr/lib/${MULTIARCH},g" \
      -e "s,@prefix@,/usr/bin,g" \
      -e "s,@exec_prefix@,/usr/bin,g" \
      -e "s,@includedir@,/usr/include/nss,g" \
      -e "s,@MOD_MAJOR_VERSION@,${NSS_VMAJOR},g" \
      -e "s,@MOD_MINOR_VERSION@,${NSS_VMINOR},g" \
      -e "s,@MOD_PATCH_VERSION@,${NSS_VPATCH},g" \
      > "$pkgdir/usr/bin/nss-config"
    chmod 755 "$pkgdir/usr/bin/nss-config"

    cd dist/*.OBJ/bin
    install -t "$pkgdir/usr/bin" *util shlibsign signtool signver ssltap

    cd ../lib
    install -t "$pkgdir/usr/lib/${MULTIARCH}" *.so
    install -t "$pkgdir/usr/lib/${MULTIARCH}" -m644 libcrmf.a *.chk

    cd ../../public/nss
    install -t "$pkgdir/usr/include/nss" -m644 *.h

    rm "$pkgdir/usr/lib/${MULTIARCH}/libnssckbi.so"
    ln -s pkcs11/p11-kit-trust.so "$pkgdir/usr/lib/${MULTIARCH}/libnssckbi.so"
}
