pkgname=graphite
pkgver=1.2.4
pkgrel=1
arch=('x86_64')
url="http://projects.palaso.org/projects/graphitedev"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('LGPL' 'GPL' 'custom')
options=('!emptydirs')

depends=('gcc-libs')
makedepends=('cmake' 'freetype2' 'python2')

source=(http://downloads.sourceforge.net/project/silgraphite/graphite2/graphite2-${pkgver}.tgz
        graphite2-1.2.0-cmakepath.patch)
sha256sums=('4bc3d5168029bcc0aa00eb2c973269d29407be2796ff56f9c80e10736bd8b003'
            'ff7e6452399b9bb30a86183f7ceb9e692f663604291b9fb73690bbd797719b65')

build() {
  cd "${srcdir}"
  
  # python2 fixes
  sed -i "s:\/usr\/bin\/python:\/usr\/bin\/python2:" graphite2-${pkgver}/tests/{jsoncmp,fuzztest,defuzz,corrupt.py}
  
  # fix install path - .cmake files contain architecture dependend content - patch taken from FC/Slackware
  pushd graphite2-${pkgver}
  patch -Np1 -i ${srcdir}/graphite2-1.2.0-cmakepath.patch
  popd
  
  mkdir build
  cd build
  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DLIB_SUFFIX=/${MULTIARCH} \
	-DCMAKE_BUILD_TYPE:STRING=Release \
	-DGRAPHITE2_COMPARE_RENDERER=OFF \
	#-DCMAKE_VERBOSE_MAKEFILE:BOOL=ON # remove when docs install target will be fixed

  # fix unwanted -O3 cflag (taken form Debian)
  find . -type f ! -name "rules" ! -name "changelog" -exec sed -i -e 's/\-O3//g' {} \;

  make 
}

check() {
  cd "${srcdir}"/build
  sed -i "s:python:python2:g" tests/CTestTestfile.cmake
  ctest
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install 

  # licenses
  mkdir -p "${pkgdir}"/usr/doc/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/doc/${pkgname}/
}
