pkgname=sqlite3
pkgver=3.8.6
_amalver=3080600
pkgrel=1
pkgdesc="A lightweight embedded SQL database engine."
arch=("x86_64")
url="http://www.sqlite.org"
license=('custom')

depends=('glibc' 'ncurses' 'readline')

source=(http://www.sqlite.org/2014/sqlite-autoconf-${_amalver}.tar.gz)
sha256sums=(53bab14255bc16c3d73fdbe54b364c1764709d68dda167587f8026c98b9f4e95)

build() {
	cd "${srcdir}/sqlite-autoconf-${_amalver}"
	
	export CFLAGS="${CFLAGS} -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY=1 -DSQLITE_SECURE_DELETE=1"
	
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --sysconfdir=/etc \
	            --localstatedir=/var --mandir=/usr/man --enable-threadsafe \
	            --enable-static=no
	make
}

package() {
	cd "${srcdir}/sqlite-autoconf-${_amalver}"
	make install DESTDIR="${pkgdir}"
}
