pkgname=tzdata
pkgver=2014i
pkgrel=1
pkgdesc="Timezone information database."
arch=(x86_64)
url="http://www.iana.org/time-zones"
license=(GPL)

options=('!emptydirs')

source=(http://www.iana.org/time-zones/repository/releases/tzdata${pkgver}.tar.gz
		http://www.iana.org/time-zones/repository/releases/tzcode${pkgver}.tar.gz)
sha256sums=(2af331bdd2b794ec59b8ded7300fb29dc3573a009ec14183f5c9ad38e3886153
			62591075fb0d47459889b71bac3c8487f09b7417b81dfa541d750b4889e84783)

timezones=('africa' 'antarctica' 'asia' 'australasia' 'europe' 'northamerica' \
           'southamerica' 'pacificnew' 'etcetera' 'backward' 'systemv' \
           'factory')

build() {
	cd "${srcdir}"
	make zic
}

package() {
	cd "${srcdir}"

	./zic -d "${pkgdir}"/usr/share/zoneinfo ${timezones[@]}
	./zic -d "${pkgdir}"/usr/share/zoneinfo/posix ${timezones[@]}
	./zic -d "${pkgdir}"/usr/share/zoneinfo/right -L leapseconds ${timezones[@]}
	install -m444 -t "${pkgdir}"/usr/share/zoneinfo iso3166.tab zone1970.tab

	mkdir -p "${pkgdir}"/usr/bin
	install -m755 -t "${pkgdir}"/usr/bin zic
}
