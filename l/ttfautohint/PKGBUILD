pkgname=ttfautohint
pkgver=1.3
pkgrel=1
pkgdesc="A font autohinting framework"
arch=("x86_64")
url="http://www.freetype.org/ttfautohint"
license=('GPLv2' 'custom')

makedepends=('bash')
depends=('gcc-libs' 'glibc' 'freetype2' 'harfbuzz')

source=(http://download.savannah.gnu.org/releases/freetype/ttfautohint-${pkgver}.tar.gz{,.sig})
sha256sums=('c39fa03790b2dfe711f66137cf0f0324eb19872932cef91da9c0bddf9e4ce104'
            'SKIP')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5') # Werner Lemberg <wl@gnu.org>

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	bash ./configure --prefix=/usr --with-qt=no
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	
	mkdir -p "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
	install -m644 {FTL.TXT,COPYING,GPLv2.TXT} "${pkgdir}"/usr/share/doc/${pkgname}-${pkgver}
}
