pkgbase=boost
pkgname=('boost-libs' 'boost')
pkgver=1.57.0
_boostver=${pkgver//./_}
pkgrel=1
url='http://www.boost.org/'
arch=('x86_64')
license=('custom')

makedepends=('icu>=53.1' 'python' 'python2' 'bzip2' 'zlib' 'llvm')

source=(https://downloads.sourceforge.net/project/${pkgbase}/${pkgbase}/${pkgver}/${pkgbase}_${_boostver}.tar.bz2
        0001-Add-missing-include-to-signals2-trackable.hpp.patch)
sha256sums=('910c8c022a33ccec7f088bd65d4f14b466588dda94ba2124e78b8c57db264967'
            '9835e0bb6e935fca5b2599b9ca3731bd05f9a0eb8214366c53396c5b126811d0')

prepare() {
   cd ${pkgbase}_${_boostver}

   # https://github.com/boostorg/signals2/pull/8
   patch -Np2 -i ../0001-Add-missing-include-to-signals2-trackable.hpp.patch
}

build() {
   export _stagedir="${srcdir}/stagedir"
   local JOBS="$(sed -e 's/.*\(-j *[0-9]\+\).*/\1/' <<< ${MAKEFLAGS})"

   cd ${pkgbase}_${_boostver}

   ./bootstrap.sh --with-toolset=clang --with-icu --with-python=/usr/bin/python2

   _bindir="bin.linuxx86"
   [[ "${CARCH}" = "x86_64" ]] && _bindir="bin.linuxx86_64"
   install -Dm755 tools/build/src/engine/$_bindir/b2 "${_stagedir}"/bin/b2

   # Add an extra python version. This does not replace anything and python 2.x
   # need to be the default.
   #echo "using python : 3.4 : /usr/bin/python3 : /usr/include/python3.4m : /usr/lib/${MULTIARCH} ;" \
   #   >> project-config.jam

   # boostbook is needed by quickbook
   install -dm755 "${_stagedir}"/share/boostbook
   cp -a tools/boostbook/{xsl,dtd} "${_stagedir}"/share/boostbook/

   # default "minimal" install: "release link=shared,static
   # runtime-link=shared threading=single,multi"
   # --layout=tagged will add the "-mt" suffix for multithreaded libraries
   # and installs includes in /usr/include/boost.
   # --layout=system no longer adds the -mt suffix for multi-threaded libs.
   # install to ${_stagedir} in preparation for split packaging
   "${_stagedir}"/bin/b2 \
      variant=release \
      debug-symbols=off \
      threading=multi \
      runtime-link=shared \
      link=shared,static \
      toolset=clang \
      python=2.7 \
      cflags="${CPPFLAGS} ${CFLAGS} -O3" linkflags="${LDFLAGS}" \
      --layout=system \
      --prefix="${_stagedir}" \
      ${JOBS} \
      install
}

package_boost() {
   pkgdesc='Free peer-reviewed portable C++ source libraries - development headers'
   depends=("boost-libs=${pkgver}")
   optdepends=('python: for python bindings'
               'python2: for python2 bindings')
   options=('staticlibs')

   install -dm755 "${pkgdir}"/usr
   cp -a "${_stagedir}"/{bin,include,share} "${pkgdir}"/usr

   install -d "${pkgdir}"/usr/lib/${MULTIARCH}
   cp -a "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/${MULTIARCH}/

   ln -s /usr/bin/b2 "$pkgdir"/usr/bin/bjam
}

package_boost-libs() {
   pkgdesc='Free peer-reviewed portable C++ source libraries - runtime libraries'
   depends=('bzip2' 'zlib' 'icu')

   install -dm755 "${pkgdir}"/usr/lib/${MULTIARCH}
   cp -a "${_stagedir}"/lib/* "${pkgdir}"/usr/lib/${MULTIARCH}
   rm "${pkgdir}"/usr/lib/${MULTIARCH}/*.a

   install -Dm644 "${srcdir}/"${pkgbase}_${_boostver}/LICENSE_1_0.txt \
      "${pkgdir}"/usr/share/doc/boost/LICENSE_1_0.txt
}
