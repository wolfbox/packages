pkgname=libxml2
pkgver=2.9.2
pkgrel=1
pkgdesc="XML parser library."
arch=("x86_64")
url="http://xmlsoft.org"
license=('custom')

depends=('glibc' 'xz' 'zlib')

source=(ftp://xmlsoft.org/libxml2/libxml2-${pkgver}.tar.gz{,.asc}
		libxml2.do-not-check-crc.diff.xz)
sha256sums=('5178c30b151d044aefb1b08bf54c3003a0ac55c59c866763997529d60770d5bc'
            'SKIP'
            'c195673ede058de75c24d94bf42b5ac971a5877131c839bbd55fbc84eac495fe')

prepare() {
	cd "${srcdir}"/libxml2-${pkgver}
	
	# From Slackware. Is this a good idea?
	xzcat ../libxml2.do-not-check-crc.diff.xz | patch -p1
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} --mandir=/usr/man
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	mkdir -p "${pkgdir}"/usr/doc/libxml2-${pkgver}
	cp -a COPYING "${pkgdir}"/usr/doc/libxml2-${pkgver}/
	
	# Remove the gtk-docs
	rm -rf "${pkgdir}"/usr/share/gtk-doc
}
