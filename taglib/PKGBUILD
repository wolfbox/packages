pkgname=taglib
pkgver=1.9.1
pkgrel=1
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats"
arch=('x86_64')
url="http://developer.kde.org/~wheeler/taglib.html"
license=('LGPL2.1' 'MPL')

makedepends=('cmake' 'pkg-config')
depends=('sh' 'zlib' 'gcc-libs')

source=("http://${pkgname}.github.io/releases/${pkgname}-${pkgver}.tar.gz")
sha256sums=('72d371cd1419a87ae200447a53bff2be219283071e80fd12337928cc967dc71a')

build() {
    mkdir -p build && cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DWITH_MP4=ON \
        -DWITH_ASF=ON \
        -DLIB_SUFFIX=/${MULTIARCH}
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
