pkgname=gradm
_ver=3.1-201502222102
pkgver=${_ver/-/_}
pkgrel=1
pkgdesc="GRSecurity RBAC administration utilities"
arch=("x86_64")
url="https://grsecurity.net"
license=('GPL2')
backup=('etc/grsec/learn_config' 'etc/grsec/policy')

depends=('glibc' 'openpam')

source=(https://grsecurity.net/stable/gradm-${_ver}.tar.gz{,.sig})
sha256sums=('37f7304ebf314a5480f5e00a502919ef2b5001874916412a93105e84bd947fb7'
            'SKIP')
validpgpkeys=(DE9452CE46F42094907F108B44D1C0F82525FE49) # Bradley Spengler

prepare() {
    cd "${srcdir}"/${pkgname}

    # Fix installation dir
    sed -i 's:/sbin:/usr/bin:' Makefile *.c *.h

    # Fix PAM support
    sed -i 's:-lpam_misc::g' Makefile
    sed -i 's:#include <security/pam_misc.h>::g' gradm_pam.c
}

build() {
    cd ${pkgname}

    export CFLAGS="${CFLAGS} -fPIE"
    export LDFLAGS="${LDFLAGS} -Wl,-z,now -pie"
    make OPT_FLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
    cd ${pkgname}

    make install DESTDIR="${pkgdir}" MANDIR=/usr/share/man
    rm -r "${pkgdir}"/dev
}
