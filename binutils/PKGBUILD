pkgname=binutils
pkgver=2.25
pkgrel=1
pkgdesc="GNU binary development tools."
arch=("x86_64")
url="http://www.gnu.org/software/binutils"
license=(GPLv2)

depends=(glibc)

source=(http://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
		binutils.export.demangle.h.diff.gz
		binutils.no-config-h-check.diff.gz)
sha256sums=('22defc65cfa3ef2a3395faaea75d6331c6e62ea5dfacfed3e2ec17b08c882923'
            'SKIP'
            '5860f065cf44a212699169eb4954da57eacfef17aef4b2237619d2403e38797c'
            '51d543c617bf335b546d084624ae7109b789d17cb70805591de3fc2671e89f5f')
validpgpkeys=('EAF1C276A747E9ED86210CBAC3126D3B4AE55E93') # Tristan Gingold <gingold@adacore.com>

DEFAULT_LD=ld.bfd

prepare() {
	cd "${srcdir}"/${pkgname}-${pkgver}
	
	# Fix build with _FORTIFY_SOURCE
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

	# Apply Slackware patches
	zcat "${srcdir}"/binutils.export.demangle.h.diff.gz | patch -p1
	zcat "${srcdir}"/binutils.no-config-h-check.diff.gz | patch -p1
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
				--with-docdir=/usr/share/doc/binutils \
				--enable-shared --enable-multilib --enable-64-bit-bfd \
				--enable-plugins --enable-threads \
				--disable-werror \
				--enable-gold=yes \
				--enable-ld=default \
				--disable-initfini-array \
				--build=${CHOST}

	make clean
	make tooldir=/usr
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install tooldir=/usr DESTDIR="${pkgdir}"

	# Install missing file
	cp -a include/libiberty.h "${pkgdir}"/usr/include/
	
	# Move ldscripts
	mv "${pkgdir}"/usr/lib/ldscripts "${pkgdir}"/usr/lib/${MULTIARCH}/
	
	# Set default linker
	( cd "${pkgdir}"/usr/bin ; rm -f ld ; ln -s /usr/bin/$DEFAULT_LD ld )

	# Remove unneeded documentation
	rm -f "${pkgdir}"/usr/share/man/man1/{dlltool,windres}.1
	rm -f "${pkgdir}"/usr/share/info/standards*
}
