pkgname=harfbuzz
pkgver=1.0.6
pkgrel=1
pkgdesc="OpenType text shaping engine"
arch=(x86_64)
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=(MIT)

depends=('glibc' 'zlib' 'bzip2' 'glib2' 'freetype2' 'icu' 'graphite' 'cairo')
makedepends=('gobject-introspection' 'python2')

source=(http://www.freedesktop.org/software/harfbuzz/release/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f616a7fbdc78a627043f9029000bf08c0c71df59cde4143fc92a014f6a993b26')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib/${MULTIARCH} \
    --with-glib --with-freetype --with-cairo --with-icu --with-graphite2 \
    --with-gobject
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/share/gtk-doc

  install -Dm644 COPYING "$pkgdir/usr/share/doc/harfbuzz/COPYING"
}
