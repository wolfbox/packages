pkgname=util-linux
pkgver=2.26
pkgrel=1
pkgdesc="A broad collection of Linux system utilities"
arch=("x86_64")
url="https://www.kernel.org/pub/linux/utils/util-linux/"
license=(GPL2)
install=util-linux.install
backup=('etc/sysctl.conf')
options=('emptydirs')

depends=('glibc' 'ncurses')

source=(https://www.kernel.org/pub/linux/utils/util-linux/v2.26/util-linux-${pkgver}.tar{.xz,.sign}
        sysctl.conf)
sha256sums=('a23c6f39dea0ed215ccd589509ffc7bb6f706f6e1a04760f493fb0fd7e93c489'
            'SKIP'
            '8b0fa8fe4444245e734695f60342edc5338c32ffe7a0a6488b6dfa514c0ac1cd')
validpgpkeys=('B0C64D14301CC6EFAEDF60E4E4B71D5EEC39C284') # Karel Zak <kzak@redhat.com>

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr \
                --libdir=/usr/lib/${MULTIARCH} \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --docdir=/usr/share/doc/util-linux \
                --sbindir=/usr/bin \
                --bindir=/usr/bin \
                --disable-static \
                --disable-uuidd \
                --disable-login \
                --disable-nologin \
                --disable-sulogin \
                --disable-su \
                --disable-chfn-chsh
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make install DESTDIR="${pkgdir}"

    # Fix the binary paths
    mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin
    rmdir "${pkgdir}"/usr/sbin

    mkdir -p "${pkgdir}"/etc/sysctl.d
    install -m600 "${srcdir}"/sysctl.conf "${pkgdir}"/etc/

    # Remove setuid-root permissions
    chmod -s "${pkgdir}"/usr/bin/{mount,umount,wall}
}
